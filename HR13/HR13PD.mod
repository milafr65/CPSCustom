******* HR13PD 34.1.13 <3915624771>
000100******************************************************************
000200*                             HR13PD                             *
000300******************************************************************
      *                                                                *
      *  JT#      TAG      DESCRIPTION                                 *
      *  ------   ------   ------------------------------------------  *
      *  307540 | J07540 | DOMESTIC PARTNER                            *
      *  ------   ------   ------------------------------------------- *
      *  316547 | J16547 | SEPARATE RELATIONSHIP CODES TO DEPENDENTS   *
      *         |        | AND BENEFICIARY.                            *
      *  ------   ------   ------------------------------------------  *
      *  845689 |J45689  | FIXED BENEFIT ELIGIBILITY CRITERIA FOR      *
      *         |        | DEPENDENTS FOR OPTION B,S,P AND C           *
      * -------   ------   ------------------------------------------  *
      * 1044921 | 044921 | REMOVED COMMENTED CODE TO MOVE HR13F4-PTB-  *
      *         |        | PARTICIPNT-FN TO HRHDB-PARTICIPNT-FN        *
      * -------   ------   ------------------------------------------  *
      * 1306202 | J06202 | DO NOT DISPLAY INACTIVE DEPENDENTS ON HR13.4*
      * -------   ------   ------------------------------------------  *
      * 1587700 | 587700 | NEW HR13.5 SCREEN LOGIC/CODE                *
      * MLF       06/22/2023                                           *
      * DUE TO ISSUES SUPPORTING CPS CUSTOM FIELDS PREVIOUSLY ADDED    *
      * TO HR13 SCREENS, THIS PATCH AND THE RELATED SCREEN             *
      * MODIFICATIONS WILL NOT BE APPLIED                              *
      * -------   ------   ------------------------------------------  *
      * 1673640 | (.SCR) | CHANGE GENDER EDIT                          *
      * -------   ------   ------------------------------------------  *
      ******************************************************************
      *               M O D I F I C A T I O N   L O G:                 *
      ******************************************************************
      *  Modified by Analysts International,MN                         *
      ******************************************************************
      *  SN0406  04/03/06  MODIFY TO ALLOW 4 NEW FIELDS: DISABILITY    *
      *                    APPROVED FOR CDSP, DISABILITY APPROVED FOR  *
      *                    CDSP DATE, DISABILITY APPROVED FOR CHP,     *
      *                    DISABILITY APPROVED FOR CHP DATE.           *
      *                                                                *
      *          04/05/06  ADDED 2 NEW MESSAGES NUMBERS: 212 AND 213   *
      *                    FOR HR13 SCREEN (230-EDIT-DATA).            *
      *                                                                *
      *  SN0306  03/29/06  MODIFY TO ALLOW HIC NUMBER                  *
      *                                                                *
      *  AI0080  03/25/03  MODIFY TO ALLOW 4 MORE DATES: DIVORCE,      *
      *                    MARRIAGE, AND STUDENT STATUS REVIEW DATES   *
      *                    AND DISABLED DATE                           *
      *  ACS004  04/01/10  MODIFIED TO TRIGGER WORKFLOW EMAIL          *
      *          ACS/AMS   NOTIFICATION.                               *
      *  ACS005  10/27/10  REAPPLIED CUSTOM CODE AFTER 9.0 APPS        *
      *          ACS       UPGRADE.                                    *
      *          08/25/11  REAPPLIED CUSTOM CODE AFTER 9.0.1 APPS      *
      *          ACS       UPGRADE.                                    *
      ******************************************************************

000400******************************************************************
000500 HR13S1-TRANSACTION              SECTION 41.
000600******************************************************************
000700 HR13S1-START.
000800
171600     MOVE CRT-SCREEN-CODE        TO HR13F1-SCREEN-CODE.

ACS004     SET SMOKER-NOT-CHANGED      TO TRUE.
ACS004
           IF (HR13F1-FC               NOT = "C")
               INITIALIZE HR13F1-EFF-DT-XMIT.

000900     PERFORM 210-EDIT-ACCESS
001000     THRU    210-END.
AI0080
AI0080     IF (HR13F1-FC = "A" OR "C")
AI0080         PERFORM 230-EDIT-DATA
AI0080         THRU    230-END.
001100
           IF (HR13F1-FC = "D")
000900         PERFORM 240-EDIT-DELETE
001000         THRU    240-END.
001100
001200     IF (NO-ERROR-FOUND)
001300         PERFORM 400-PROCESS-TRAN
001400         THRU    400-END.
001500
001600     GO TO HR13S1-TRANSACTION-END.
001700
001800******************************************************************
001900 210-EDIT-ACCESS.
002000******************************************************************
002100
002200     MOVE HR13F1-EMD-COMPANY     TO DB-COMPANY.
002300     MOVE SPACES                 TO DB-PROCESS-LEVEL.
002400
002500     PERFORM 840-FIND-PRSSET1.
002600     IF (PRSYSTEM-NOTFOUND)
002700         MOVE 111                                TO CRT-ERROR-NBR
002800         MOVE HR13F1-EMD-COMPANY-FN              TO CRT-FIELD-NBR
002900         GO TO 210-END.
003000
003100     MOVE PRS-NAME               TO HR13F1-PRS-NAME.
003200
003300     MOVE HR13F1-EMD-COMPANY     TO DB-COMPANY.
003400     MOVE HR13F1-EMD-EMPLOYEE    TO DB-EMPLOYEE.
003500     MOVE HR13F1-EMD-SEQ-NBR     TO DB-SEQ-NBR.
003600
003700     PERFORM 840-FIND-EMDSET1.
003800
003900     IF (HR13F1-FC = "N")
004000         IF (EMDEPEND-FOUND)
004100             PERFORM 850-FIND-NLT-EMDSET1
004200             PERFORM 860-FIND-NEXT-EMDSET1
004300         ELSE
004400             PERFORM 850-FIND-NLT-EMDSET1.
004500
004600     IF (HR13F1-FC = "P")
004700         PERFORM 850-FIND-NLT-EMDSET1
004800         PERFORM 870-FIND-PREV-EMDSET1.
004900
005000     IF (HR13F1-FC = "N" OR "P")
005100         IF (EMDEPEND-NOTFOUND)
005200         OR (EMD-COMPANY NOT = HR13F1-EMD-COMPANY)
005300             MOVE 12                          TO CRT-ERROR-NBR
005400             MOVE HR13F1-FC-FN                TO CRT-FIELD-NBR
005500             GO TO 210-END
005600         ELSE
005700             MOVE EMD-EMPLOYEE   TO HR13F1-EMD-EMPLOYEE
005800                                    DB-EMPLOYEE
005900             MOVE EMD-SEQ-NBR    TO HR13F1-EMD-SEQ-NBR
006000                                    DB-SEQ-NBR.
006100
006200     MOVE HR13F1-EMD-COMPANY     TO DB-COMPANY.
006300     MOVE HR13F1-EMD-EMPLOYEE    TO DB-EMPLOYEE.
006400     PERFORM 840-FIND-EMPSET1.
006500     IF (EMPLOYEE-NOTFOUND)
006600         MOVE 102                             TO CRT-ERROR-NBR
006700         MOVE HR13F1-EMD-EMPLOYEE-FN          TO CRT-FIELD-NBR
006800         GO TO 210-END.
006900
007000     MOVE ZEROS                  TO HRWS-NEXT-COUNT.
007100     MOVE EMP-COMPANY            TO CRT-COMPANY.
007200     MOVE EMP-PROCESS-LEVEL      TO CRT-PROCESS-LEVEL.
007300     PERFORM 700-HR-EMP-SECURITY.
007400     IF (HRWS-EMP-SECURED)
007500         IF (HR13F1-FC NOT = "N" AND "P")
007600             MOVE 101                     TO CRT-ERROR-NBR
007700             MOVE HR13F1-EMD-EMPLOYEE-FN  TO CRT-FIELD-NBR
007800             GO TO 210-END
007900         ELSE
008000             IF (HR13F1-FC = "N")
008100                 PERFORM 860-FIND-NEXT-EMDSET1
008200                 PERFORM 212-GET-NEXT-EMPLOYEE
008300                 THRU    212-END
008400                     UNTIL (EMDEPEND-NOTFOUND)
008500                     OR    (EMD-COMPANY     NOT = DB-COMPANY)
008600                     OR    (HRWS-EMP-NOT-SECURED)
008700                     OR   ((HRWS-NEXT-COUNT > PRS-SEC-SEARCH)
008800                     AND   (PRS-SEC-SEARCH  > ZEROS))
008900             ELSE
009000                 PERFORM 870-FIND-PREV-EMDSET1
009100                 PERFORM 212-GET-NEXT-EMPLOYEE
009200                 THRU    212-END
009300                     UNTIL (EMDEPEND-NOTFOUND)
009400                     OR    (EMD-COMPANY     NOT = DB-COMPANY)
009500                     OR    (HRWS-EMP-NOT-SECURED)
009600                     OR   ((HRWS-NEXT-COUNT > PRS-SEC-SEARCH)
009700                     AND   (PRS-SEC-SEARCH  > ZEROS)).
009800 
009900     IF  (HR13F1-FC = "N" OR "P")
010000     AND (HRWS-NEXT-COUNT > PRS-SEC-SEARCH)
010100     AND (PRS-SEC-SEARCH  > ZEROS)
010200         MOVE PRS-SEC-SEARCH              TO HRWS-SEARCH-NBR
010300         MOVE HRWS-SEARCH-VAR             TO CRT-ERR-VAR1
010400         MOVE 99                          TO CRT-ERROR-NBR
010500         MOVE HR13F1-FC-FN                TO CRT-FIELD-NBR
010600         GO TO 210-END.
010700
010800     IF (HR13F1-FC = "N" OR "P")
010900         IF (EMDEPEND-NOTFOUND)
011000         OR (EMD-COMPANY NOT = HR13F1-EMD-COMPANY)
011100             MOVE 12                          TO CRT-ERROR-NBR
011200             MOVE HR13F1-FC-FN                TO CRT-FIELD-NBR
011300             GO TO 210-END
011400         ELSE
011500             MOVE EMD-EMPLOYEE   TO HR13F1-EMD-EMPLOYEE
011600                                    DB-EMPLOYEE
011700             MOVE EMD-SEQ-NBR    TO HR13F1-EMD-SEQ-NBR
011800                                    DB-SEQ-NBR.
011900
012000     IF  (EMDEPEND-NOTFOUND)
012100     AND (HR13F1-FC = "I" OR "C" OR "D")
012200         MOVE 103                             TO CRT-ERROR-NBR
012300         MOVE HR13F1-EMD-SEQ-NBR-FN           TO CRT-FIELD-NBR
012400         GO TO 210-END.
012500
012600     IF  (EMDEPEND-FOUND)
012700     AND (HR13F1-FC = "A")
012800         MOVE 104                             TO CRT-ERROR-NBR
012900         MOVE HR13F1-EMD-SEQ-NBR-FN           TO CRT-FIELD-NBR
013000         GO TO 210-END.
013100
013200     PERFORM 840-FIND-PEMSET1.
013300
013400 210-END.
013500
013600******************************************************************
013700 212-GET-NEXT-EMPLOYEE.
013800******************************************************************
013900     IF (EMD-EMPLOYEE = DB-EMPLOYEE)
014000         GO TO 212-NEXT.
014100
014200     ADD 1                       TO HRWS-NEXT-COUNT.
014300     MOVE EMD-EMPLOYEE           TO DB-EMPLOYEE.
014400     PERFORM 840-FIND-EMPSET1.
014500     MOVE EMP-COMPANY            TO CRT-COMPANY.
014600     MOVE EMP-PROCESS-LEVEL      TO CRT-PROCESS-LEVEL.
014700     PERFORM 700-HR-EMP-SECURITY.
014800     IF (HRWS-EMP-NOT-SECURED)
014900         GO TO 212-END.
015000
015100 212-NEXT.
015200     IF (HR13F1-FC = "P")
015300         PERFORM 870-FIND-PREV-EMDSET1
015400     ELSE
015500         PERFORM 860-FIND-NEXT-EMDSET1.
015600
015700 212-END.
AI0080******************************************************************
AI0080 230-EDIT-DATA.
AI0080******************************************************************

AI0080     IF (HR13F1-MARRIAGE-DATE    < HR13F1-EMD-BIRTHDATE)
AI0080     AND (HR13F1-MARRIAGE-DATE   NOT = ZEROES)
AI0080         MOVE 203                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-MARRIAGE-DATE-FN    TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

AI0080     IF (HR13F1-MARRIAGE-DATE    > WS-SYSTEM-DATE-YMD)
AI0080     AND (HR13F1-MARRIAGE-DATE   NOT = ZEROES)
AI0080         MOVE 208                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-MARRIAGE-DATE-FN    TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

AI0080     IF (HR13F1-DIVORCE-DATE     < HR13F1-EMD-BIRTHDATE)
AI0080     AND (HR13F1-DIVORCE-DATE    NOT = ZEROES)
AI0080         MOVE 204                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-DIVORCE-DATE-FN     TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

AI0080     IF (HR13F1-DIVORCE-DATE     > WS-SYSTEM-DATE-YMD)
AI0080     AND (HR13F1-DIVORCE-DATE    NOT = ZEROES)
AI0080         MOVE 208                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-DIVORCE-DATE-FN     TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

AI0080     IF (HR13F1-STUDENT-DATE     < HR13F1-EMD-BIRTHDATE)
AI0080     AND (HR13F1-STUDENT-DATE    NOT = ZEROES)
AI0080         MOVE 205                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-STUDENT-DATE-FN     TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

AI0080     IF (HR13F1-DISABLED-DATE    < HR13F1-EMD-BIRTHDATE)
AI0080     AND (HR13F1-DISABLED-DATE   NOT = ZEROES)
AI0080         MOVE 206                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-DISABLED-DATE-FN    TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

AI0080     IF (HR13F1-DISABLED-DATE    > WS-SYSTEM-DATE-YMD)
AI0080     AND (HR13F1-DISABLED-DATE   NOT = ZEROES)
AI0080         MOVE 208                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-DISABLED-DATE-FN    TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

AI0080     IF (HR13F1-DATE-OF-DEATH    < HR13F1-EMD-BIRTHDATE)
AI0080     AND (HR13F1-DATE-OF-DEATH   NOT = ZEROES)
AI0080         MOVE 207                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-DATE-OF-DEATH-FN    TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

AI0080     IF (HR13F1-DATE-OF-DEATH    > WS-SYSTEM-DATE-YMD)
AI0080     AND (HR13F1-DATE-OF-DEATH   NOT = ZEROES)
AI0080         MOVE 208                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-DATE-OF-DEATH-FN    TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

AI0080     IF (HR13F1-DISABLED-DATE    > ZEROS)             
AI0080     AND (HR13F1-EMD-DISABLED     = "N")
AI0080         MOVE 211                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-DISABLED-DATE-FN    TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

AI0080     IF (HR13F1-DATE-OF-DEATH    > ZEROS)             
AI0080     AND (HR13F1-DECEASED         = "N")
AI0080         MOVE 209                        TO CRT-ERROR-NBR
AI0080         MOVE HR13F1-DATE-OF-DEATH-FN    TO CRT-FIELD-NBR
AI0080         GO TO 230-END.

SN0406     IF (HR13F1-DIS-APR-CDSP-D   > WS-SYSTEM-DATE-YMD)
SN0406     AND (HR13F1-DIS-APR-CDSP-D  NOT = ZEROES)
SN0406         MOVE 208                        TO CRT-ERROR-NBR
SN0406         MOVE HR13F1-DIS-APR-CDSP-D-FN   TO CRT-FIELD-NBR
SN0406         GO TO 230-END.

SN0406     IF (HR13F1-DIS-APR-CDSP-D   > ZEROS)             
SN0406     AND (HR13F1-DIS-APR-CDSP     = SPACES)
SN0406         MOVE 212                        TO CRT-ERROR-NBR
SN0406         MOVE HR13F1-DIS-APR-CDSP-D-FN   TO CRT-FIELD-NBR
SN0406         GO TO 230-END.

SN0406     IF (HR13F1-DIS-APR-CHP-D   > WS-SYSTEM-DATE-YMD)
SN0406     AND (HR13F1-DIS-APR-CHP-D  NOT = ZEROES)
SN0406         MOVE 208                        TO CRT-ERROR-NBR
SN0406         MOVE HR13F1-DIS-APR-CHP-D-FN    TO CRT-FIELD-NBR
SN0406         GO TO 230-END.

SN0406     IF (HR13F1-DIS-APR-CHP-D   > ZEROS)             
SN0406     AND (HR13F1-DIS-APR-CHP     = SPACES)
SN0406         MOVE 213                        TO CRT-ERROR-NBR
SN0406         MOVE HR13F1-DIS-APR-CHP-D-FN    TO CRT-FIELD-NBR
SN0406         GO TO 230-END.

AI0080 230-END.

015800
001800******************************************************************
001900 240-EDIT-DELETE.
002000******************************************************************
006200     MOVE HR13F1-EMD-COMPANY     TO DB-COMPANY.
006300     MOVE HR13F1-EMD-EMPLOYEE    TO DB-EMPLOYEE.
           MOVE ZEROES                 TO DB-PARTICIPNT.
P48786     MOVE HR13F1-EMD-SEQ-NBR     TO DB-DEPENDENT.
P48786     MOVE BNTSET2-DEPENDENT      TO WS-DB-BEG-RNG.
           PERFORM 850-KFIND-BEGRNG-BNTSET2.
           IF (BNTRANS-KFOUND)
009300         MOVE 152                             TO CRT-ERROR-NBR
009400         MOVE HR13F1-FC-FN                    TO CRT-FIELD-NBR
009500         GO TO 240-END.
015700 240-END.
015900******************************************************************
016000 400-PROCESS-TRAN.
016100******************************************************************
016200
016300     IF (HR13F1-FC = "A")
016400         PERFORM 410-ADD
016500         THRU    410-END
016600     ELSE
016700     IF (HR13F1-FC = "C")
016800         PERFORM 420-CHANGE
016900         THRU    420-END
017000     ELSE
017100     IF (HR13F1-FC = "D")
017200         PERFORM 440-DELETE
017300         THRU    440-END
017400     ELSE
017500     IF (HR13F1-FC = "I" OR "N" OR "P")
017600         PERFORM 480-INQUIRE
017700         THRU    480-END.
017800
017900 400-END.
018000
018100******************************************************************
018200 410-ADD.
018300******************************************************************
018400
018500     PERFORM 500-MOVE-DATA-TO-HRDEP
018600     THRU    500-END.
018700
018800     MOVE "A"                    TO HRDEP-FC.
018900
J16547     INITIALIZE EMDEPEND.

019000     PERFORM 5000-HR-DEPENDENT.
019100
019200     PERFORM 600-MOVE-HRDEP-TO-SCREEN
019300     THRU    600-END.
019400
021100 410-END.
021200
021300******************************************************************
021400 420-CHANGE.
021500******************************************************************
ACS004
ACS004     IF (HR13F1-EMD-SMOKER NOT = EMD-SMOKER)
ACS004         SET SMOKER-CHANGED      TO TRUE 
ACS004         MOVE HR13F1-EMD-SMOKER  TO WS-SMOKER-FLAG
ACS004     ELSE
ACS004     IF (HR13F1-EMD-SMOKER = "Y")
ACS004         MOVE SPACES             TO WS-SMOKER-FLAG
ACS004         SET SMOKER-CHANGED      TO TRUE.
ACS004
021600
021700     PERFORM 500-MOVE-DATA-TO-HRDEP
021800     THRU    500-END.
021900
022000     MOVE "C"                    TO HRDEP-FC.
022100
022200     PERFORM 5000-HR-DEPENDENT.
022300
022400     PERFORM 600-MOVE-HRDEP-TO-SCREEN
022500     THRU    600-END.
022600
           IF (HRDEP-CHANGE-COUNTER    NOT = ZEROES)
           OR (HRDEP-ADD-COUNTER       NOT = ZEROES)
      *------- Change Complete; {0} Dependent benefits updated
               MOVE "HRDEP"            TO CRT-ERROR-CAT
               MOVE 153                TO CRT-MSG-NBR

               COMPUTE HRWS-DEC-FIELD  = HRDEP-CHANGE-COUNTER
                                       + HRDEP-ADD-COUNTER
               MOVE 2                  TO HRWS-SIZE
               INITIALIZE HRWS-NBR-DECIMALS
               PERFORM 770-HR-FORMAT-DEC-FIELD
               MOVE HRWS-VALUE         TO CRT-ERR-VAR1.

024300 420-END.
024400
024500******************************************************************
024600 440-DELETE.
024700******************************************************************
024800
024900     PERFORM 500-MOVE-DATA-TO-HRDEP
025000     THRU    500-END.
025100
025200     MOVE "D"                    TO HRDEP-FC.
025300
025400     PERFORM 5000-HR-DEPENDENT.
025500
025600     MOVE CRT-SEND-MESSAGE       TO CRT-REQUEST.
025700
025800 440-END.
025900
026000******************************************************************
026100 480-INQUIRE.
026200******************************************************************
026300
026400     PERFORM 610-MOVE-EMD-TO-SCREEN
026500     THRU    610-END.
026600
026700     MOVE EMP-LAST-NAME          TO HRWS-LAST-NAME.
026800     MOVE EMP-FIRST-NAME         TO HRWS-FIRST-NAME.
026900     MOVE EMP-MIDDLE-INIT        TO HRWS-MIDDLE-INIT.
           MOVE EMP-NAME-SUFFIX        TO HRWS-NAME-SUFFIX.
           MOVE EMP-LAST-NAME-PRE      TO HRWS-LAST-NAME-PRE.
027000     PERFORM 750-HR-FORMAT-NAME.
027100     MOVE HRWS-FORMAT-NAME       TO HR13F1-EMP-FULL-NAME.
027200*    MOVE EMP-FICA-NBR           TO HR13F1-EMP-FICA-NBR.
           
           MOVE PRS-COMPANY               TO DB-COMPANY.
           MOVE SPACES                    TO DB-COUNTRY-CD-REQ
                                             DB-PROCESS-LEVEL.
      * 50 = Social Number
           MOVE HREMP-FICA-NBR-DN         TO DB-FLD-NBR.
           PERFORM 840-FIND-PASSET1.
           IF (PASCRTY-FOUND)
               MOVE PAS-SEC-LEVEL         TO HRWS-SEC-LEVEL
               PERFORM 730-HR-FIELD-SECURITY
               IF (HRWS-FLD-SECURED)
                   MOVE SPACES            TO HR13F1-EMP-FICA-NBR
               ELSE
                   MOVE EMP-FICA-NBR      TO HR13F1-EMP-FICA-NBR.

027300
027400     MOVE EMD-COMPANY            TO DB-COMPANY.
027500     MOVE "DP"                   TO DB-TYPE.
027600     MOVE EMD-REL-CODE           TO DB-CODE.
027700     PERFORM 840-FIND-PCOSET1.
027800     IF (PCODES-FOUND)
027900         MOVE PCO-DESCRIPTION    TO HR13F1-PCO-DESCRIPTION.
028000
028100     IF (EMD-PRIMARY-CARE NOT = SPACES)
028200         MOVE EMD-COMPANY            TO DB-COMPANY
028300         MOVE "PC"                   TO DB-TYPE
028400         MOVE EMD-PRIMARY-CARE       TO DB-CODE
028500         PERFORM 840-FIND-PCOSET1
028600         IF (PCODES-FOUND)
028700             MOVE PCO-DESCRIPTION    TO HR13F1-PCO-PC-DESCRIPTION
028800         ELSE
028900             MOVE PCO-DESCRIPTION    TO HR13F1-PCO-PC-DESCRIPTION
029000     ELSE
029100         MOVE SPACES                 TO HR13F1-PCO-PC-DESCRIPTION.
029200
029300     MOVE EMD-BIRTHDATE          TO HRDEP-EMD-BIRTHDATE.
029400     PERFORM 5500-AGE-FROM-BIRTHDATE.
029500     MOVE HRDEP-EMD-CUR-AGE      TO HR13F1-EMD-CUR-AGE.
029600
029700     MOVE CRT-INQ-COMPLETE       TO CRT-MESSAGE.
029800
029900 480-END.
030000
030100******************************************************************
030200 500-MOVE-DATA-TO-HRDEP.
030300******************************************************************
030400
           MOVE HR13F1-PT-XMIT-NBR1       TO HRDEP-PT-XMIT-NBR1.
           MOVE HR13F1-PT-XMIT-NBR2       TO HRDEP-PT-XMIT-NBR2.
           MOVE HR13F1-XMIT-D             TO HRDEP-XMIT-D.
030500     MOVE HR13F1-EMD-COMPANY        TO HRDEP-EMD-COMPANY.
030600     MOVE HR13F1-EMD-COMPANY-FN     TO HRDEP-EMD-COMPANY-FN.
030700     MOVE HR13F1-EMD-EMPLOYEE       TO HRDEP-EMD-EMPLOYEE.
030800     MOVE HR13F1-EMD-EMPLOYEE-FN    TO HRDEP-EMD-EMPLOYEE-FN.
030900     MOVE HR13F1-EMD-SEQ-NBR        TO HRDEP-EMD-SEQ-NBR.
031000     MOVE HR13F1-EMD-SEQ-NBR-FN     TO HRDEP-EMD-SEQ-NBR-FN.
031100     MOVE HR13F1-EMD-LAST-NAME      TO HRDEP-EMD-LAST-NAME.
031200     MOVE HR13F1-EMD-LAST-NAME-FN   TO HRDEP-EMD-LAST-NAME-FN.
031300     MOVE HR13F1-EMD-FIRST-NAME     TO HRDEP-EMD-FIRST-NAME.
031400     MOVE HR13F1-EMD-FIRST-NAME-FN  TO HRDEP-EMD-FIRST-NAME-FN.
031500     MOVE HR13F1-EMD-MIDDLE-INIT    TO HRDEP-EMD-MIDDLE-INIT.
031600     MOVE HR13F1-EMD-MIDDLE-INIT-FN TO HRDEP-EMD-MIDDLE-INIT-FN.
031500     MOVE HR13F1-EMD-LAST-NAME-PRE  TO HRDEP-EMD-LAST-NAME-PRE.
031600     MOVE HR13F1-EMD-LAST-NAME-PRE-FN 
                                          TO HRDEP-EMD-LAST-NAME-PRE-FN.
031500     MOVE HR13F1-EMD-NAME-SUFFIX    TO HRDEP-EMD-NAME-SUFFIX.
031600     MOVE HR13F1-EMD-NAME-SUFFIX-FN TO HRDEP-EMD-NAME-SUFFIX-FN.
031700     MOVE HR13F1-EMD-FICA-NBR       TO HRDEP-EMD-FICA-NBR.
031800     MOVE HR13F1-EMD-FICA-NBR-FN    TO HRDEP-EMD-FICA-NBR-FN.
031900     MOVE HR13F1-EMD-EMP-ADDRESS    TO HRDEP-EMD-EMP-ADDRESS.
032000     MOVE HR13F1-EMD-EMP-ADDRESS-FN TO HRDEP-EMD-EMP-ADDRESS-FN.
032100     MOVE HR13F1-EMD-REL-CODE       TO HRDEP-EMD-REL-CODE.
032200     MOVE HR13F1-EMD-REL-CODE-FN    TO HRDEP-EMD-REL-CODE-FN.
           MOVE HR13F1-EMD-CONSENT        TO HRDEP-EMD-CONSENT.
           MOVE HR13F1-EMD-CONSENT-FN     TO HRDEP-EMD-CONSENT-FN.
032300     MOVE HR13F1-EMD-DEP-TYPE       TO HRDEP-EMD-DEP-TYPE.
032400     MOVE HR13F1-EMD-DEP-TYPE-FN    TO HRDEP-EMD-DEP-TYPE-FN.
032500     MOVE HR13F1-EMD-BIRTHDATE      TO HRDEP-EMD-BIRTHDATE.
032600     MOVE HR13F1-EMD-BIRTHDATE-FN   TO HRDEP-EMD-BIRTHDATE-FN.
032700     MOVE HR13F1-EMD-ADOPTION-DATE  TO HRDEP-EMD-ADOPTION-DATE.
032800     MOVE HR13F1-EMD-ADOPTION-DATE-FN  
032900                                 TO HRDEP-EMD-ADOPTION-DATE-FN.
033000     MOVE HR13F1-EMD-PLACEMENT-DATE 
033100                                 TO HRDEP-EMD-PLACEMENT-DATE.
033200     MOVE HR13F1-EMD-PLACEMENT-DATE-FN
033300                                 TO HRDEP-EMD-PLACEMENT-DATE-FN.
033400     MOVE HR13F1-EMD-SEX            TO HRDEP-EMD-SEX.
033500     MOVE HR13F1-EMD-SEX-FN         TO HRDEP-EMD-SEX-FN.
033600     MOVE HR13F1-EMD-SMOKER         TO HRDEP-EMD-SMOKER.
033700     MOVE HR13F1-EMD-SMOKER-FN      TO HRDEP-EMD-SMOKER-FN.
033800     MOVE HR13F1-EMD-STUDENT        TO HRDEP-EMD-STUDENT.
033900     MOVE HR13F1-EMD-STUDENT-FN     TO HRDEP-EMD-STUDENT-FN.
034000     MOVE HR13F1-EMD-DISABLED       TO HRDEP-EMD-DISABLED.
034100     MOVE HR13F1-EMD-DISABLED-FN    TO HRDEP-EMD-DISABLED-FN.
034000     MOVE HR13F1-DECEASED           TO HRDEP-DECEASED.
034100     MOVE HR13F1-DECEASED-FN        TO HRDEP-DECEASED-FN.
           MOVE HR13F1-DATE-OF-DEATH      TO HRDEP-DEATH-DATE.   
           MOVE HR13F1-DATE-OF-DEATH-FN   TO HRDEP-DEATH-DATE-FN.
           MOVE HR13F1-PRIOR-MONTHS-COV   TO HRDEP-PRIOR-COV-MO.
           MOVE HR13F1-PRIOR-MONTHS-COV-FN TO HRDEP-PRIOR-COV-MO-FN.
           MOVE HR13F1-ESTAB-PATIENT      TO HRDEP-ESTAB-PATIENT.
           MOVE HR13F1-ESTAB-PATIENT-FN   TO HRDEP-ESTAB-PATIENT-FN.
034200     MOVE HR13F1-EMD-PRIMARY-CARE   TO HRDEP-EMD-PRIMARY-CARE.
034300     MOVE HR13F1-EMD-PRIMARY-CARE-FN TO HRDEP-EMD-PRIMARY-CARE-FN.
034400     MOVE HR13F1-EMD-HL-COV-FLAG    TO HRDEP-EMD-HL-COV-FLAG.
034500     MOVE HR13F1-EMD-HL-COV-FLAG-FN TO HRDEP-EMD-HL-COV-FLAG-FN.
034600     MOVE HR13F1-EMD-DN-COV-FLAG    TO HRDEP-EMD-DN-COV-FLAG.
034700     MOVE HR13F1-EMD-DN-COV-FLAG-FN TO HRDEP-EMD-DN-COV-FLAG-FN.
034800     MOVE HR13F1-EMD-DL-COV-FLAG    TO HRDEP-EMD-DL-COV-FLAG.
034900     MOVE HR13F1-EMD-DL-COV-FLAG-FN TO HRDEP-EMD-DL-COV-FLAG-FN.
HIPAA      MOVE HR13F1-EMD-MEDICARE-IND   TO HRDEP-EMD-MEDICARE-IND.
HIPAA      MOVE HR13F1-EMD-MEDICARE-IND-FN TO HRDEP-EMD-MEDICARE-IND-FN.
035000     MOVE HR13F1-EMD-ADDR1          TO HRDEP-EMD-ADDR1.
035100     MOVE HR13F1-EMD-ADDR1-FN       TO HRDEP-EMD-ADDR1-FN.
035200     MOVE HR13F1-EMD-ADDR2          TO HRDEP-EMD-ADDR2.
035300     MOVE HR13F1-EMD-ADDR2-FN       TO HRDEP-EMD-ADDR2-FN.
035400     MOVE HR13F1-EMD-ADDR3          TO HRDEP-EMD-ADDR3.
035500     MOVE HR13F1-EMD-ADDR3-FN       TO HRDEP-EMD-ADDR3-FN.
035600     MOVE HR13F1-EMD-ADDR4          TO HRDEP-EMD-ADDR4.
035700     MOVE HR13F1-EMD-ADDR4-FN       TO HRDEP-EMD-ADDR4-FN.
035800     MOVE HR13F1-EMD-CITY           TO HRDEP-EMD-CITY.
035900     MOVE HR13F1-EMD-CITY-FN        TO HRDEP-EMD-CITY-FN.
036000     MOVE HR13F1-EMD-STATE          TO HRDEP-EMD-STATE.
036100     MOVE HR13F1-EMD-STATE-FN       TO HRDEP-EMD-STATE-FN.
036200     MOVE HR13F1-EMD-ZIP            TO HRDEP-EMD-ZIP.
036300     MOVE HR13F1-EMD-ZIP-FN         TO HRDEP-EMD-ZIP-FN.
036400     MOVE HR13F1-EMD-COUNTRY-CODE   TO HRDEP-EMD-COUNTRY-CODE.
036500     MOVE HR13F1-EMD-COUNTRY-CODE-FN
036500                                    TO HRDEP-EMD-COUNTRY-CODE-FN.
036600     MOVE HR13F1-EMD-HM-PHONE-CNTRY TO HRDEP-EMD-HM-PHONE-CNTRY.
036700     MOVE HR13F1-EMD-HM-PHONE-CNTRY-FN
036800                                   TO HRDEP-EMD-HM-PHONE-CNTRY-FN.
036900     MOVE HR13F1-EMD-HM-PHONE-NBR    TO HRDEP-EMD-HM-PHONE-NBR.
037000     MOVE HR13F1-EMD-HM-PHONE-NBR-FN TO HRDEP-EMD-HM-PHONE-NBR-FN.
037100     MOVE HR13F1-EMD-WK-PHONE-CNTRY  TO HRDEP-EMD-WK-PHONE-CNTRY.
037200     MOVE HR13F1-EMD-WK-PHONE-CNTRY-FN
037300                                   TO HRDEP-EMD-WK-PHONE-CNTRY-FN.
037400     MOVE HR13F1-EMD-WK-PHONE-NBR    TO HRDEP-EMD-WK-PHONE-NBR.
037500     MOVE HR13F1-EMD-WK-PHONE-NBR-FN TO HRDEP-EMD-WK-PHONE-NBR-FN.
037600     MOVE HR13F1-EMD-WK-PHONE-EXT    TO HRDEP-EMD-WK-PHONE-EXT.
037700     MOVE HR13F1-EMD-WK-PHONE-EXT-FN TO HRDEP-EMD-WK-PHONE-EXT-FN.
J81460     MOVE HR13F1-EMD-EMAIL-PERSONAL  TO HRDEP-EMD-EMAIL-PERSONAL.
J81460     MOVE HR13F1-EMD-EMAIL-PERSONAL-FN 
J81460                                 TO HRDEP-EMD-EMAIL-PERSONAL-FN.
037600     MOVE HR13F1-EMD-ACTIVE-FLAG    TO HRDEP-EMD-ACTIVE-FLAG.
037700     MOVE HR13F1-EMD-ACTIVE-FLAG-FN TO HRDEP-EMD-ACTIVE-FLAG-FN.
037600     MOVE HR13F1-EFFECT-DATE        TO HRDEP-EFFECT-DATE.
037700     MOVE HR13F1-EFFECT-DATE-FN     TO HRDEP-EFFECT-DATE-FN.
037600     MOVE HR13F1-CREATE-TRANS       TO HRDEP-CREATE-TRANS.
037700     MOVE HR13F1-CREATE-TRANS-FN    TO HRDEP-CREATE-TRANS-FN.
J12776     MOVE HR13F1-EMD-HICN           TO HRDEP-EMD-HICN.
J12776     MOVE HR13F1-EMD-HICN-FN        TO HRDEP-EMD-HICN-FN.

037600     MOVE HR13F1-HIPAA-REASON       TO HRDEP-HIPAA-REASON.
037700     MOVE HR13F1-HIPAA-REASON-FN    TO HRDEP-HIPAA-REASON-FN.
037600     MOVE HR13F1-EFF-DT-XMIT        TO HRDEP-EFF-DT-XMIT.
J83494     MOVE HR13F1-VERIFY-DATE        TO HRDEP-VERIFY-DATE.
J83494     MOVE HR13F1-VERIFY-DATE-FN     TO HRDEP-VERIFY-DATE-FN.

J68319     IF (HR13F1-EMD-USER-ID NOT = SPACES)
J68319         MOVE HR13F1-EMD-USER-ID    TO HRDEP-EMD-USER-ID
J68319     ELSE
J68319         MOVE CRT-USER-NAME         TO HRDEP-EMD-USER-ID
J68319     END-IF.

AI0080     MOVE HR13F1-DIVORCE-DATE       TO HRDEP-DIVORCE-DATE.
AI0080     MOVE HR13F1-DIVORCE-DATE-FN    TO HRDEP-DIVORCE-DATE-FN.
AI0080     MOVE HR13F1-MARRIAGE-DATE      TO HRDEP-MARRIAGE-DATE.
AI0080     MOVE HR13F1-MARRIAGE-DATE-FN   TO HRDEP-MARRIAGE-DATE-FN.
AI0080     MOVE HR13F1-DISABLED-DATE      TO HRDEP-DISABLED-DATE.
AI0080     MOVE HR13F1-DISABLED-DATE-FN   TO HRDEP-DISABLED-DATE-FN.
AI0080     MOVE HR13F1-STUDENT-DATE       TO HRDEP-STUDENT-DATE.
AI0080     MOVE HR13F1-STUDENT-DATE-FN    TO HRDEP-STUDENT-DATE-FN.
SN0306     MOVE HR13F1-HIC-NUMBER         TO HRDEP-HIC-NUMBER.
SN0306     MOVE HR13F1-HIC-NUMBER-FN      TO HRDEP-HIC-NUMBER-FN.
SN0406     MOVE HR13F1-DIS-APR-CDSP       TO HRDEP-DIS-APR-CDSP.
SN0406     MOVE HR13F1-DIS-APR-CDSP-FN    TO HRDEP-DIS-APR-CDSP-FN.
SN0406     MOVE HR13F1-DIS-APR-CDSP-D     TO HRDEP-DIS-APR-CDSP-D.
SN0406     MOVE HR13F1-DIS-APR-CDSP-D-FN  TO HRDEP-DIS-APR-CDSP-D-FN.
SN0406     MOVE HR13F1-DIS-APR-CHP        TO HRDEP-DIS-APR-CHP.
SN0406     MOVE HR13F1-DIS-APR-CHP-FN     TO HRDEP-DIS-APR-CHP-FN.
SN0406     MOVE HR13F1-DIS-APR-CHP-D      TO HRDEP-DIS-APR-CHP-D.
SN0406     MOVE HR13F1-DIS-APR-CHP-D-FN   TO HRDEP-DIS-APR-CHP-D-FN.

037800 500-END.
037900
038000******************************************************************
038100 600-MOVE-HRDEP-TO-SCREEN.
038200******************************************************************
038300
           IF  (ERROR-FOUND)
               MOVE HRDEP-PT-XMIT-NBR1   TO HR13F1-PT-XMIT-NBR1
               MOVE HRDEP-XMIT-D         TO HR13F1-XMIT-D      
           ELSE
               INITIALIZE                   HR13F1-PT-XMIT-NBR1
                                            HR13F1-XMIT-D.     
           IF  (ERROR-FOUND)
               MOVE HRDEP-PT-XMIT-NBR2   TO HR13F1-PT-XMIT-NBR2
           ELSE
               INITIALIZE                   HR13F1-PT-XMIT-NBR2.
038400     MOVE HRDEP-EMD-COMPANY        TO HR13F1-EMD-COMPANY.
038500     MOVE HRDEP-EMD-EMPLOYEE       TO HR13F1-EMD-EMPLOYEE.
038600     MOVE HRDEP-EMD-SEQ-NBR        TO HR13F1-EMD-SEQ-NBR.
038700     MOVE HRDEP-EMD-LAST-NAME      TO HR13F1-EMD-LAST-NAME.
038800     MOVE HRDEP-EMD-FIRST-NAME     TO HR13F1-EMD-FIRST-NAME.
038900     MOVE HRDEP-EMD-MIDDLE-INIT    TO HR13F1-EMD-MIDDLE-INIT.
038900     MOVE HRDEP-EMD-LAST-NAME-PRE  TO HR13F1-EMD-LAST-NAME-PRE.
038900     MOVE HRDEP-EMD-NAME-SUFFIX    TO HR13F1-EMD-NAME-SUFFIX.
039000     MOVE HRDEP-EMD-FICA-NBR       TO HR13F1-EMD-FICA-NBR.
039100     MOVE HRDEP-EMD-EMP-ADDRESS    TO HR13F1-EMD-EMP-ADDRESS.
039200     MOVE HRDEP-EMD-REL-CODE       TO HR13F1-EMD-REL-CODE.
           MOVE HRDEP-EMD-CONSENT        TO HR13F1-EMD-CONSENT.
039300     MOVE HRDEP-EMD-DEP-TYPE       TO HR13F1-EMD-DEP-TYPE.
039400     MOVE HRDEP-EMD-BIRTHDATE      TO HR13F1-EMD-BIRTHDATE.
039500     MOVE HRDEP-EMD-ADOPTION-DATE  TO HR13F1-EMD-ADOPTION-DATE.
039600     MOVE HRDEP-EMD-CUR-AGE        TO HR13F1-EMD-CUR-AGE.
039700     MOVE HRDEP-EMD-PLACEMENT-DATE TO HR13F1-EMD-PLACEMENT-DATE.
039800     MOVE HRDEP-EMD-SEX            TO HR13F1-EMD-SEX.
039900     MOVE HRDEP-EMD-SMOKER         TO HR13F1-EMD-SMOKER.
040000     MOVE HRDEP-EMD-STUDENT        TO HR13F1-EMD-STUDENT.
040100     MOVE HRDEP-EMD-DISABLED       TO HR13F1-EMD-DISABLED.
           MOVE HRDEP-DECEASED           TO HR13F1-DECEASED.
           MOVE HRDEP-DEATH-DATE         TO HR13F1-DATE-OF-DEATH.
           MOVE HRDEP-PRIOR-COV-MO       TO HR13F1-PRIOR-MONTHS-COV.
           MOVE HRDEP-ESTAB-PATIENT      TO HR13F1-ESTAB-PATIENT.
040200     MOVE HRDEP-EMD-PRIMARY-CARE   TO HR13F1-EMD-PRIMARY-CARE.
040300     MOVE HRDEP-EMD-HL-COV-FLAG    TO HR13F1-EMD-HL-COV-FLAG.
040400     MOVE HRDEP-EMD-DN-COV-FLAG    TO HR13F1-EMD-DN-COV-FLAG.
040500     MOVE HRDEP-EMD-DL-COV-FLAG    TO HR13F1-EMD-DL-COV-FLAG.
HIPAA      MOVE HRDEP-EMD-MEDICARE-IND   TO HR13F1-EMD-MEDICARE-IND.
040600     MOVE HRDEP-EMD-ADDR1          TO HR13F1-EMD-ADDR1.
040700     MOVE HRDEP-EMD-ADDR2          TO HR13F1-EMD-ADDR2.
040800     MOVE HRDEP-EMD-ADDR3          TO HR13F1-EMD-ADDR3.
040900     MOVE HRDEP-EMD-ADDR4          TO HR13F1-EMD-ADDR4.
041000     MOVE HRDEP-EMD-CITY           TO HR13F1-EMD-CITY.
041100     MOVE HRDEP-EMD-STATE          TO HR13F1-EMD-STATE.
041200     MOVE HRDEP-EMD-ZIP            TO HR13F1-EMD-ZIP.
041300     MOVE HRDEP-EMD-COUNTRY-CODE   TO HR13F1-EMD-COUNTRY-CODE.
041400     MOVE HRDEP-EMD-HM-PHONE-CNTRY TO HR13F1-EMD-HM-PHONE-CNTRY.
041500     MOVE HRDEP-EMD-HM-PHONE-NBR   TO HR13F1-EMD-HM-PHONE-NBR.
041600     MOVE HRDEP-EMD-WK-PHONE-CNTRY TO HR13F1-EMD-WK-PHONE-CNTRY.
041700     MOVE HRDEP-EMD-WK-PHONE-NBR   TO HR13F1-EMD-WK-PHONE-NBR.
041800     MOVE HRDEP-EMD-WK-PHONE-EXT   TO HR13F1-EMD-WK-PHONE-EXT.
J81460     MOVE HRDEP-EMD-EMAIL-PERSONAL TO HR13F1-EMD-EMAIL-PERSONAL.
041800     MOVE HRDEP-EMD-ACTIVE-FLAG    TO HR13F1-EMD-ACTIVE-FLAG.
J12776     MOVE HRDEP-EMD-HICN           TO HR13F1-EMD-HICN.
           MOVE HRDEP-EFFECT-DATE        TO HR13F1-EFFECT-DATE.
           MOVE HRDEP-CREATE-TRANS       TO HR13F1-CREATE-TRANS.
           MOVE HRDEP-HIPAA-REASON       TO HR13F1-HIPAA-REASON.
037600     MOVE HRDEP-EFF-DT-XMIT        TO HR13F1-EFF-DT-XMIT.
J83494     MOVE HRDEP-VERIFY-DATE        TO HR13F1-VERIFY-DATE.
041900
           IF (NO-ERROR-FOUND)
               INITIALIZE HR13F1-EFFECT-DATE
                          HR13F1-CREATE-TRANS
                          HR13F1-HIPAA-REASON.
J68319     INITIALIZE                       HR13F1-EMD-USER-ID.

AI0080     MOVE HRDEP-DIVORCE-DATE       TO HR13F1-DIVORCE-DATE.
AI0080     MOVE HRDEP-DIVORCE-DATE-FN    TO HR13F1-DIVORCE-DATE-FN.
AI0080     MOVE HRDEP-MARRIAGE-DATE      TO HR13F1-MARRIAGE-DATE.
AI0080     MOVE HRDEP-MARRIAGE-DATE-FN   TO HR13F1-MARRIAGE-DATE-FN.
AI0080     MOVE HRDEP-DISABLED-DATE      TO HR13F1-DISABLED-DATE.
AI0080     MOVE HRDEP-DISABLED-DATE-FN   TO HR13F1-DISABLED-DATE-FN.
AI0080     MOVE HRDEP-STUDENT-DATE       TO HR13F1-STUDENT-DATE.
AI0080     MOVE HRDEP-STUDENT-DATE-FN    TO HR13F1-STUDENT-DATE-FN.
SN0306     MOVE HRDEP-HIC-NUMBER         TO HR13F1-HIC-NUMBER.
SN0306     MOVE HRDEP-HIC-NUMBER-FN      TO HR13F1-HIC-NUMBER-FN.
SN0406     MOVE HRDEP-DIS-APR-CDSP       TO HR13F1-DIS-APR-CDSP.
SN0406     MOVE HRDEP-DIS-APR-CDSP-FN    TO HR13F1-DIS-APR-CDSP-FN.
SN0406     MOVE HRDEP-DIS-APR-CDSP-D     TO HR13F1-DIS-APR-CDSP-D.
SN0406     MOVE HRDEP-DIS-APR-CDSP-D-FN  TO HR13F1-DIS-APR-CDSP-D-FN.
SN0406     MOVE HRDEP-DIS-APR-CHP        TO HR13F1-DIS-APR-CHP.
SN0406     MOVE HRDEP-DIS-APR-CHP-FN     TO HR13F1-DIS-APR-CHP-FN.
SN0406     MOVE HRDEP-DIS-APR-CHP-D      TO HR13F1-DIS-APR-CHP-D.
SN0406     MOVE HRDEP-DIS-APR-CHP-D-FN   TO HR13F1-DIS-APR-CHP-D-FN.
SN0406
042000 600-END.
042100
042200******************************************************************
042300 610-MOVE-EMD-TO-SCREEN.
042400******************************************************************
042500
           INITIALIZE                 HR13F1-PT-XMIT-NBR1.
           INITIALIZE                 HR13F1-PT-XMIT-NBR2.
           INITIALIZE                 HR13F1-XMIT-D.   
J68319     INITIALIZE                 HR13F1-EMD-USER-ID.
042600     MOVE EMD-COMPANY        TO HR13F1-EMD-COMPANY.
042700     MOVE EMD-EMPLOYEE       TO HR13F1-EMD-EMPLOYEE.
042800     MOVE EMD-SEQ-NBR        TO HR13F1-EMD-SEQ-NBR.
042900     MOVE EMD-LAST-NAME      TO HR13F1-EMD-LAST-NAME.
043000     MOVE EMD-FIRST-NAME     TO HR13F1-EMD-FIRST-NAME.
043100     MOVE EMD-MIDDLE-INIT    TO HR13F1-EMD-MIDDLE-INIT.
043100     MOVE EMD-LAST-NAME-PRE  TO HR13F1-EMD-LAST-NAME-PRE.
043100     MOVE EMD-NAME-SUFFIX    TO HR13F1-EMD-NAME-SUFFIX.
124002     MOVE EMD-LAST-NAME      TO HRWS-LAST-NAME.
124002     MOVE EMD-MIDDLE-INIT    TO HRWS-MIDDLE-INIT.
124002     MOVE EMD-NAME-SUFFIX    TO HRWS-NAME-SUFFIX.
124002     MOVE EMD-LAST-NAME-PRE  TO HRWS-LAST-NAME-PRE. 
124002     MOVE EMD-FIRST-NAME     TO HRWS-FIRST-NAME.
124002     PERFORM 750-HR-FORMAT-NAME.
124002     MOVE HRWS-FORMAT-NAME   TO HR13F1-EMD-SHORT-NAME.
043200     MOVE EMD-FICA-NBR       TO HR13F1-EMD-FICA-NBR.
043300     MOVE EMD-EMP-ADDRESS    TO HR13F1-EMD-EMP-ADDRESS.
043400     MOVE EMD-REL-CODE       TO HR13F1-EMD-REL-CODE.
           MOVE EMD-CONSENT        TO HR13F1-EMD-CONSENT.
043500     MOVE EMD-DEP-TYPE       TO HR13F1-EMD-DEP-TYPE.
043600     MOVE EMD-BIRTHDATE      TO HR13F1-EMD-BIRTHDATE.
043700     MOVE EMD-ADOPTION-DATE  TO HR13F1-EMD-ADOPTION-DATE.
043800     MOVE EMD-PLACEMENT-DATE TO HR13F1-EMD-PLACEMENT-DATE.
043900     MOVE EMD-SEX            TO HR13F1-EMD-SEX.
044000     MOVE EMD-SMOKER         TO HR13F1-EMD-SMOKER.
044100     MOVE EMD-STUDENT        TO HR13F1-EMD-STUDENT.
044200     MOVE EMD-DISABLED       TO HR13F1-EMD-DISABLED.
044200     MOVE EMD-DECEASED       TO HR13F1-DECEASED.
044300     MOVE EMD-PRIMARY-CARE   TO HR13F1-EMD-PRIMARY-CARE.
044400     MOVE EMD-HL-COV-FLAG    TO HR13F1-EMD-HL-COV-FLAG.
044500     MOVE EMD-DN-COV-FLAG    TO HR13F1-EMD-DN-COV-FLAG.
044600     MOVE EMD-DL-COV-FLAG    TO HR13F1-EMD-DL-COV-FLAG.
044700     MOVE EMD-ADDR1          TO HR13F1-EMD-ADDR1.
044800     MOVE EMD-ADDR2          TO HR13F1-EMD-ADDR2.
044900     MOVE EMD-ADDR3          TO HR13F1-EMD-ADDR3.
045000     MOVE EMD-ADDR4          TO HR13F1-EMD-ADDR4.
045100     MOVE EMD-CITY           TO HR13F1-EMD-CITY.
045200     MOVE EMD-STATE          TO HR13F1-EMD-STATE.
045300     MOVE EMD-ZIP            TO HR13F1-EMD-ZIP.
045400     MOVE EMD-COUNTRY-CODE   TO HR13F1-EMD-COUNTRY-CODE.
045500     MOVE EMD-HM-PHONE-CNTRY TO HR13F1-EMD-HM-PHONE-CNTRY.
045600     MOVE EMD-HM-PHONE-NBR   TO HR13F1-EMD-HM-PHONE-NBR.
045700     MOVE EMD-WK-PHONE-CNTRY TO HR13F1-EMD-WK-PHONE-CNTRY.
045800     MOVE EMD-WK-PHONE-NBR   TO HR13F1-EMD-WK-PHONE-NBR.
045900     MOVE EMD-WK-PHONE-EXT   TO HR13F1-EMD-WK-PHONE-EXT.
J81460     MOVE EMD-EMAIL-PERSONAL TO HR13F1-EMD-EMAIL-PERSONAL.
045900     MOVE EMD-ACTIVE-FLAG    TO HR13F1-EMD-ACTIVE-FLAG.
J12776     MOVE EMD-HICN           TO HR13F1-EMD-HICN.
046000
           INITIALIZE                 HR13F1-INT-COUNTRY-DESC.
           IF (EMD-COUNTRY-CODE NOT = SPACES)
               MOVE EMD-COUNTRY-CODE TO DB-COUNTRY-CODE
               PERFORM 840-FIND-INTSET1
               IF (INSTCTRYCD-FOUND)
                   MOVE INT-COUNTRY-DESC   TO HR13F1-INT-COUNTRY-DESC
               END-IF.

           MOVE EMD-DEATH-DATE      TO HR13F1-DATE-OF-DEATH.
           MOVE EMD-PRIOR-COV-MO    TO HR13F1-PRIOR-MONTHS-COV.
           MOVE EMD-ESTAB-PATIENT   TO HR13F1-ESTAB-PATIENT.
HIPAA      MOVE EMD-MEDICARE-IND    TO HR13F1-EMD-MEDICARE-IND.
       
090800     MOVE "HR13"                 TO CRT-ERROR-CAT.
090900     MOVE 153                    TO CRT-MSG-NBR.
091000     PERFORM 790-GET-MSG.
091100     MOVE CRT-MESSAGE            TO HR13F1-COMMENTS.
           INITIALIZE CRT-MESSAGE.

           INITIALIZE HR13F1-COMMENTS-FLAG.
           MOVE HR13F1-EMD-COMPANY     TO DB-COMPANY.
           MOVE HR13F1-PAC-EMP-APP     TO DB-EMP-APP.
           MOVE HR13F1-PAC-CMT-TYPE    TO DB-CMT-TYPE.
           MOVE HR13F1-EMD-EMPLOYEE    TO DB-EMPLOYEE.
           INITIALIZE DB-JOB-CODE
                      DB-ACTION-CODE.
           MOVE HR13F1-EMD-SEQ-NBR     TO DB-LN-NBR.
           MOVE PACSET1-LN-NBR         TO WS-DB-BEG-RNG.
           PERFORM 850-KFIND-BEGRNG-PACSET1.
           IF (PACOMMENTS-KFOUND)
               MOVE "*"                TO HR13F1-COMMENTS-FLAG.
               
J83494     MOVE EMD-STUDENT-VERIFY     TO HR13F1-VERIFY-DATE.               
115300
           INITIALIZE HR13F1-EFFECT-DATE
                      HR13F1-CREATE-TRANS
                      HR13F1-HIPAA-REASON.

AI0080     MOVE EMD-COMPANY        TO DB-COMPANY.
AI0080     MOVE EMD-EMPLOYEE       TO DB-EMPLOYEE.
AI0080     MOVE EMD-SEQ-NBR        TO DB-SEQ-NBR.
AI0080     PERFORM 840-FIND-WBDSET1.
AI0080     IF (WBPDEPEND-NOTFOUND)
AI0080         MOVE ZEROES              TO HR13F1-MARRIAGE-DATE
AI0080         MOVE ZEROES              TO HR13F1-DIVORCE-DATE  
AI0080         MOVE ZEROES              TO HR13F1-STUDENT-DATE  
AI0080         MOVE ZEROES              TO HR13F1-DISABLED-DATE 
SN0306         MOVE SPACES              TO HR13F1-HIC-NUMBER
SN0406         MOVE SPACES              TO HR13F1-DIS-APR-CDSP  
SN0406         MOVE SPACES              TO HR13F1-DIS-APR-CHP
SN0406         MOVE ZEROES              TO HR13F1-DIS-APR-CDSP-D
SN0406         MOVE ZEROES              TO HR13F1-DIS-APR-CHP-D
AI0080     ELSE
AI0080         MOVE WBD-MARRIAGE-DATE   TO HR13F1-MARRIAGE-DATE
AI0080         MOVE WBD-DIVORCE-DATE    TO HR13F1-DIVORCE-DATE  
AI0080         MOVE WBD-STUDENT-DATE    TO HR13F1-STUDENT-DATE  
AI0080         MOVE WBD-DISABLED-DATE   TO HR13F1-DISABLED-DATE
SN0306         MOVE WBD-HIC-NUMBER      TO HR13F1-HIC-NUMBER
SN0406         MOVE WBD-DIS-APR-CDSP    TO HR13F1-DIS-APR-CDSP
SN0406         MOVE WBD-DIS-APR-CDSP-D  TO HR13F1-DIS-APR-CDSP-D
SN0406         MOVE WBD-DIS-APR-CHP     TO HR13F1-DIS-APR-CHP
SN0406         MOVE WBD-DIS-APR-CHP-D   TO HR13F1-DIS-APR-CHP-D.
AI0080     
046100 610-END.
046200
046300******************************************************************
046400 HR13S1-TRANSACTION-END.
046500******************************************************************
ACS004
ACS004        IF (SMOKER-CHANGED)
ACS004           PERFORM 1000-OPEN-WORKFLOW-DB 
ACS004
ACS004           INITIALIZE WFAPI-I-SERVICE
ACS004
ACS004           MOVE HR13F1-EMD-COMPANY  TO WFAPI-I-CRITERION-1 
ACS004           MOVE "COMPANY"           TO WFAPI-I-VARIABLE-NAME (1)  
ACS004           MOVE HR13F1-EMD-COMPANY  TO WFAPI-I-VARIABLE-VAL  (1) 
ACS004           MOVE "S"                 TO WFAPI-I-VARIABLE-TYPE (1) 
ACS004           MOVE "EMPLOYEE"          TO WFAPI-I-VARIABLE-NAME (2)  
ACS004           MOVE HR13F1-EMD-EMPLOYEE TO WFAPI-I-VARIABLE-VAL  (2) 
ACS004           MOVE "S"                 TO WFAPI-I-VARIABLE-TYPE (2) 
ACS004           MOVE "DEPENDENT"         TO WFAPI-I-VARIABLE-NAME (3)  
ACS004           MOVE HR13F1-EMD-SEQ-NBR  TO WFAPI-I-VARIABLE-VAL  (3) 
ACS004           MOVE "S"                 TO WFAPI-I-VARIABLE-TYPE (3) 
ACS004           MOVE "DEPNAME"           TO WFAPI-I-VARIABLE-NAME (4)  
ACS004           MOVE HR13F1-EMD-SHORT-NAME
ACS004                                    TO WFAPI-I-VARIABLE-VAL  (4) 
ACS004           MOVE "S"                 TO WFAPI-I-VARIABLE-TYPE (4) 
ACS004           MOVE "SMOKERFLAG"        TO WFAPI-I-VARIABLE-NAME (5)  
ACS004           MOVE WS-SMOKER-FLAG       
ACS004                                    TO WFAPI-I-VARIABLE-VAL  (5) 
ACS004           MOVE "S"                 TO WFAPI-I-VARIABLE-TYPE (5) 
ACS004
ACS004           PERFORM 1000-PROCESS-FLOW.
ACS004
046600
052300******************************************************************
052400 HR13S2-TRANSACTION              SECTION 41.
052500******************************************************************
052600 HR13S2-START.
052700
171600     MOVE CRT-SCREEN-CODE        TO HR13F2-SCREEN-CODE.

052800     PERFORM 200-EDIT-TRAN
052900     THRU    200-END.
053000
053100     IF (NO-ERROR-FOUND)
053200         PERFORM 400-PROCESS-TRAN
053300         THRU    400-END.
053400
053500     GO TO HR13S2-TRANSACTION-END.
053600
053700******************************************************************
053800 200-EDIT-TRAN.
053900******************************************************************
054000
054100     PERFORM 210-EDIT-ACCESS
054200     THRU    210-END.
054300
054400     IF (ERROR-FOUND)
054500         GO TO 200-END.
054600
054700     IF (HR13F2-FC = "C")
054800         PERFORM 220-EDIT-DATA
054900         THRU    220-END
055000         GO TO 200-END.
055100
055200 200-END.
055300
055400******************************************************************
055500 210-EDIT-ACCESS.
055600******************************************************************
055700          
055800     MOVE HR13F2-EMD-COMPANY         TO DB-COMPANY. 
055900     PERFORM 840-FIND-BNCSET1.
056000     IF (BNCOMPANY-NOTFOUND)
056100         MOVE 150            TO CRT-ERROR-NBR
056200         MOVE HR13F2-FC-FN   TO CRT-FIELD-NBR
056300         GO TO 210-END.

           MOVE HR13F2-EMD-COMPANY     TO DB-COMPANY.
057300     MOVE HR13F2-EMD-EMPLOYEE    TO DB-EMPLOYEE.
057400     MOVE HR13F2-EMD-SEQ-NBR     TO DB-SEQ-NBR. 
057500     PERFORM 840-FIND-EMDSET1.
057600     IF (EMDEPEND-NOTFOUND)
057700     OR (EMD-COMPANY     NOT = DB-COMPANY)
057800     OR (EMD-EMPLOYEE    NOT = DB-EMPLOYEE)
057900     OR (EMD-SEQ-NBR     NOT = DB-SEQ-NBR)
058000     OR (EMD-BIRTHDATE       = ZEROES)
058100         MOVE 118            TO CRT-ERROR-NBR
058200         MOVE HR13F2-FC-FN   TO CRT-FIELD-NBR
058300         GO TO 210-END
J20175     ELSE
J20175        MOVE EMD-LAST-NAME          TO HRWS-LAST-NAME
J20175        MOVE EMD-FIRST-NAME         TO HRWS-FIRST-NAME
J20175        MOVE EMD-MIDDLE-INIT        TO HRWS-MIDDLE-INIT
J20175        PERFORM 750-HR-FORMAT-NAME
J20175        MOVE HRWS-FORMAT-NAME       TO HR13F2-EMD-SHORT-NAME
J20175     END-IF.
056400
056500     IF  (BNC-DEP-HEALTH        = "N") 
056600     AND (BNC-DEP-DENTAL        = "N") 
056700     AND (BNC-DEP-DEP-LIFE      = "N") 
056800          MOVE 150            TO CRT-ERROR-NBR
056900          MOVE HR13F2-FC-FN   TO CRT-FIELD-NBR
057000          GO TO 210-END.
058400
058500     IF (HR13F2-FC = "C")
058600         GO TO 210-END.
058700
058800     MOVE HR13F2-EMD-COMPANY     TO DB-COMPANY.
058900     MOVE HR13F2-EMD-EMPLOYEE    TO DB-EMPLOYEE.
059000
059100     IF (HR13F2-FC              = "I")
059200     OR (HR13F2-EMD-COMPANY     NOT = HR13F2-PT-EMD-COMPANY)
059300     OR (HR13F2-EMD-EMPLOYEE    NOT = HR13F2-PT-EMD-EMPLOYEE)
059400     OR (HR13F2-EMD-SEQ-NBR     NOT = HR13F2-PT-EMD-SEQ-NBR)
059500         MOVE "D"                        TO HR13WS-RECORD-ORIG
059600         MOVE HR13F2-EMD-SEQ-NBR         TO DB-DEPENDENT 
059700         MOVE SPACES                     TO DB-PLAN-TYPE 
059800         MOVE SPACES                     TO DB-PLAN-CODE 
059900         MOVE ZEROES                     TO DB-EMP-START
060000         MOVE ZEROES                     TO DB-START-DATE
060100     ELSE
060200         MOVE HR13F2-PT-RECORD-ORIG      TO HR13WS-RECORD-ORIG
060300         MOVE HR13F2-PT-HDB-PLAN-TYPE    TO DB-PLAN-TYPE
060400         MOVE HR13F2-PT-HDB-PLAN-CODE    TO DB-PLAN-CODE
060500         MOVE HR13F2-PT-HDB-EMP-START    TO DB-EMP-START 
060600         MOVE HR13F2-PT-HDB-START-DATE   TO DB-START-DATE.
060700
060800     IF (HR13F2-FC              = "-")
060900         MOVE HR13F2-HDB-PLAN-TYPE (1)   TO DB-PLAN-TYPE
061000         MOVE HR13F2-HDB-PLAN-CODE (1)   TO DB-PLAN-CODE
061100         MOVE HR13F2-HDB-EMP-START (1)   TO DB-EMP-START     
061200         MOVE HR13F2-RECORD-ORIG   (1)   TO HR13WS-RECORD-ORIG 
061300         IF (HR13F2-RECORD-ORIG (1) = "D")
061400             MOVE HR13F2-HDB-START-DATE (1)   TO DB-START-DATE    
061500         ELSE
061600         IF (HR13F2-RECORD-ORIG (1) = "B")
061700             MOVE HR13F2-BEN-START-DATE (1)   TO DB-START-DATE.   
061800
061900     IF (HR13WS-RECORD-ORIG = "D")
062000         PERFORM 850-FIND-NLT-HDBSET1
062100         IF (HR13F2-FC    = "-")
062200             PERFORM 870-FIND-PREV-HDBSET1
062300             IF (HRDEPBEN-NOTFOUND)
062400             OR (HDB-COMPANY     NOT = DB-COMPANY)
062500             OR (HDB-EMPLOYEE    NOT = DB-EMPLOYEE)
062600             OR (HDB-DEPENDENT   NOT = DB-DEPENDENT)
062700                 MOVE 150                     TO CRT-ERROR-NBR
062800                 MOVE HR13F2-FC-FN            TO CRT-FIELD-NBR
062900                 GO TO 210-END.
063000
063100     IF   (HR13WS-RECORD-ORIG = "D")
063200     AND ((HRDEPBEN-NOTFOUND)
063300     OR   (HDB-COMPANY      NOT = DB-COMPANY)
063400     OR   (HDB-EMPLOYEE     NOT = DB-EMPLOYEE) 
063500     OR   (HDB-DEPENDENT    NOT = DB-DEPENDENT))
063600           MOVE "B"                    TO HR13WS-RECORD-ORIG.
063700
063800     IF (HR13WS-RECORD-ORIG = "B")
063900         PERFORM 850-FIND-NLT-BENSET4
064000         IF (HR13F2-FC    = "-")
064100             PERFORM 870-FIND-PREV-BENSET4
064200         END-IF
064300         IF (BENEFIT-NOTFOUND)
064400         OR (BEN-COMPANY     NOT = DB-COMPANY)
064500         OR (BEN-EMPLOYEE    NOT = DB-EMPLOYEE)
064600             MOVE 150            TO CRT-ERROR-NBR
064700             MOVE HR13F2-FC-FN   TO CRT-FIELD-NBR
064800             GO TO 210-END.
064900
065000     IF   (HR13WS-RECORD-ORIG = "B")
065100     AND ((BENEFIT-NOTFOUND)
065200     OR   (BEN-COMPANY     NOT = DB-COMPANY)
065300     OR   (BEN-EMPLOYEE    NOT = DB-EMPLOYEE))
065400           MOVE "D"                    TO HR13WS-RECORD-ORIG.
065500
065600 210-END.
065700
065800******************************************************************
065900 220-EDIT-DATA.
066000******************************************************************
066100
066200     PERFORM 840-FIND-EMDSET1.
066300     IF (EMD-BIRTHDATE = ZEROES)
066400         MOVE 118                            TO CRT-ERROR-NBR
066500         GO TO 220-END.
066600
066700     PERFORM 222-EDIT-DTL-TRAN
066800     THRU    222-END
066900         VARYING I1 FROM 1 BY 1
067000         UNTIL  (I1 > 10)
067100         OR     (ERROR-FOUND).
067200*
067300     IF (ERROR-FOUND)
067400         GO TO 220-END.
067500              
067600 220-END.
067700
067800******************************************************************
067900 222-EDIT-DTL-TRAN.
068000******************************************************************
068100
068200     IF (HR13F2-LINE-FC (I1) = SPACES)
068300         GO TO 222-END.
068400
068500     PERFORM 520-MOVE-DTL-SCR-TO-WS
068600     THRU    520-END.
068700
068800     PERFORM 2000-HRHDB-EDIT-TRAN.
068900
069000     PERFORM 530-MOVE-DTL-WS-TO-SCR
069100     THRU    530-END.
069200
069300     IF (ERROR-FOUND)
069400         GO TO 222-END.
069500
069600     IF (HR13F2-LINE-FC (I1) = "A")
069700         MOVE HR13F2-EMD-COMPANY         TO DB-COMPANY  
069800         MOVE HR13F2-EMD-EMPLOYEE        TO DB-EMPLOYEE  
069900         MOVE HR13F2-EMD-SEQ-NBR         TO DB-DEPENDENT  
070000         MOVE HR13F2-HDB-PLAN-TYPE (I1)  TO DB-PLAN-TYPE  
070100         MOVE HR13F2-HDB-PLAN-CODE (I1)  TO DB-PLAN-CODE  
070200         MOVE HR13F2-HDB-EMP-START (I1)  TO DB-EMP-START 
070300         MOVE HR13F2-HDB-START-DATE (I1) TO DB-START-DATE 
070400         PERFORM 850-FIND-NLT-HDBSET1
071200         PERFORM 870-FIND-PREV-HDBSET1
071300         IF  (HRDEPBEN-FOUND)
071400         AND (HDB-COMPANY   = DB-COMPANY)
071500         AND (HDB-EMPLOYEE  = DB-EMPLOYEE)
071600         AND (HDB-DEPENDENT = DB-DEPENDENT)
071700         AND (HDB-PLAN-TYPE = DB-PLAN-TYPE)
071800         AND (HDB-PLAN-CODE = DB-PLAN-CODE)
071900         AND (HDB-EMP-START = DB-EMP-START)
072100             PERFORM 230-EDIT-EXISTING-PREV-HDB
072200             THRU    230-END
072300         END-IF
072500     END-IF.
072600     IF (ERROR-FOUND)
072700         GO TO 222-END.
072800
072900     MOVE HR13F2-EMD-COMPANY         TO DB-COMPANY.   
073000     MOVE HR13F2-EMD-EMPLOYEE        TO DB-EMPLOYEE. 
073100     MOVE HR13F2-EMD-SEQ-NBR         TO DB-DEPENDENT.  
073200     MOVE HR13F2-HDB-PLAN-TYPE (I1)  TO DB-PLAN-TYPE.  
073300     MOVE HR13F2-HDB-PLAN-CODE (I1)  TO DB-PLAN-CODE.  
073400     MOVE HR13F2-HDB-EMP-START (I1)  TO DB-EMP-START. 
073500     MOVE HR13F2-HDB-START-DATE (I1) TO DB-START-DATE. 
073600     PERFORM 850-FIND-NLT-HDBSET1.
073700     IF (HRDEPBEN-NOTFOUND)
073800     OR (HDB-COMPANY   NOT = DB-COMPANY)
073900     OR (HDB-EMPLOYEE  NOT = DB-EMPLOYEE)
074000     OR (HDB-DEPENDENT NOT = DB-DEPENDENT)
074100     OR (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE)
074200     OR (HDB-PLAN-CODE NOT = DB-PLAN-CODE)
074300     OR (HDB-EMP-START NOT = DB-EMP-START)
074400         PERFORM 870-FIND-PREV-HDBSET1
074500         IF  (HRDEPBEN-FOUND)
074600         AND (HDB-COMPANY   = DB-COMPANY)
074700         AND (HDB-EMPLOYEE  = DB-EMPLOYEE)
074800         AND (HDB-DEPENDENT = DB-DEPENDENT)
074900         AND (HDB-PLAN-TYPE = DB-PLAN-TYPE)
075000         AND (HDB-PLAN-CODE = DB-PLAN-CODE)
075100         AND (HDB-EMP-START = DB-EMP-START)
075200         AND (HDB-START-DATE = DB-START-DATE)
075300             IF  (HR13F2-LINE-FC (I1)         = "A")
075400             OR ((HR13F2-LINE-FC (I1)         = "C")
075500             AND (HR13F2-HDB-STOP-DATE (I1)   > HDB-STOP-DATE))
075600             OR ((HR13F2-LINE-FC (I1)         = "C")
075700             AND (HR13F2-HDB-STOP-DATE (I1)   NOT = HDB-STOP-DATE)
075800             AND (HR13F2-HDB-STOP-DATE (I1)   = ZEROS))
075900                 MOVE WS-FALSE       TO HR13WS-STOP-DFLT-SW
076000                 PERFORM 234-EDIT-EXISTING-POST-HDB
076100                 THRU    234-END
076200                     UNTIL  (ERROR-FOUND)
076300                     OR     (STOP-DEFAULTED)
076400                     OR     (HRDEPBEN-NOTFOUND)
076500                     OR     (HDB-COMPANY       NOT = DB-COMPANY)
076600                     OR     (HDB-EMPLOYEE      NOT = DB-EMPLOYEE)
                           OR     (HDB-DEPENDENT     NOT = DB-DEPENDENT)
076700                     OR     (HDB-PLAN-TYPE     NOT = DB-PLAN-TYPE)
076800                     OR     (HDB-PLAN-CODE     NOT = DB-PLAN-CODE)
076900                     OR     (HDB-EMP-START     NOT = DB-EMP-START)
077000                     OR   ((HR13F2-HDB-STOP-DATE (I1) NOT = ZEROS)
077100                     AND    (HR13F2-HDB-STOP-DATE (I1) <
077200                                                 HDB-START-DATE))
                   END-IF
               END-IF
           ELSE
           IF  (HRDEPBEN-FOUND)
           AND (HDB-COMPANY   = DB-COMPANY)
           AND (HDB-EMPLOYEE  = DB-EMPLOYEE)
           AND (HDB-DEPENDENT = DB-DEPENDENT)
           AND (HDB-PLAN-TYPE = DB-PLAN-TYPE)
           AND (HDB-PLAN-CODE = DB-PLAN-CODE)
           AND (HDB-EMP-START = DB-EMP-START)
               IF  (HR13F2-LINE-FC (I1)         = "A")
               OR ((HR13F2-LINE-FC (I1)         = "C")
               AND (HR13F2-HDB-STOP-DATE (I1)   > HDB-STOP-DATE))
               OR ((HR13F2-LINE-FC (I1)         = "C")
               AND (HR13F2-HDB-STOP-DATE (I1)   NOT = HDB-STOP-DATE)
               AND (HR13F2-HDB-STOP-DATE (I1)   = ZEROS))
                   MOVE WS-FALSE       TO HR13WS-STOP-DFLT-SW
                   PERFORM 234-EDIT-EXISTING-POST-HDB
                   THRU    234-END
                       UNTIL  (ERROR-FOUND)
                       OR     (STOP-DEFAULTED)
                       OR     (HRDEPBEN-NOTFOUND)
                       OR     (HDB-COMPANY       NOT = DB-COMPANY)
                       OR     (HDB-EMPLOYEE      NOT = DB-EMPLOYEE)
                       OR     (HDB-DEPENDENT     NOT = DB-DEPENDENT)
                       OR     (HDB-PLAN-TYPE     NOT = DB-PLAN-TYPE)
                       OR     (HDB-PLAN-CODE     NOT = DB-PLAN-CODE)
                       OR     (HDB-EMP-START     NOT = DB-EMP-START)
                       OR   ((HR13F2-HDB-STOP-DATE (I1) NOT = ZEROS)
                       AND    (HR13F2-HDB-STOP-DATE (I1) <
                                                   HDB-START-DATE))
               END-IF
           END-IF.
077300                 
077400     IF (ERROR-FOUND)
077500         GO TO 222-END.
077600
077700 222-END.
077800
077900******************************************************************
078000 230-EDIT-EXISTING-PREV-HDB.
078100******************************************************************
078200
078300     MOVE WS-FALSE               TO HR13WS-DEL-HDB-SW
078400                                    HR13WS-CHG-HDB-SW.
078500
078600     PERFORM 232-EDIT-CHANGING-HDB
078700     THRU    232-END
078800         VARYING I2 FROM 1 BY 1
078900         UNTIL  (I2 = I1)
079000         OR     (DELETING-HDB)
079100         OR     (CHANGING-HDB).
079200
079300     IF  (NOT-CHANGING-HDB)
079400     AND ((HDB-STOP-DATE     NOT = ZEROS)
079500     AND (HDB-STOP-DATE      NOT < HR13F2-HDB-START-DATE (I1))
           OR  (HDB-STOP-DATE      = ZEROS))
079600         MOVE 124                            TO CRT-ERROR-NBR
079700         MOVE HR13F2-LINE-FC-FN (I1)         TO CRT-FIELD-NBR
079800         GO TO 230-END.
079900
080000 230-END.
080100
080200******************************************************************
080300 232-EDIT-CHANGING-HDB.
080400******************************************************************
080500
080600     IF  (HR13F2-LINE-FC (I2)        = "D")
080700     AND (HR13F2-HDB-PLAN-TYPE (I2)  = HDB-PLAN-TYPE)
080800     AND (HR13F2-HDB-PLAN-CODE (I2)  = HDB-PLAN-CODE)
080900     AND (HR13F2-HDB-EMP-START  (I2) = HDB-EMP-START)
081000     AND (HR13F2-HDB-START-DATE (I2) = HDB-START-DATE)
081100         MOVE WS-TRUE                TO HR13WS-DEL-HDB-SW
081200         GO TO 232-END.
081300
081400     IF  (HR13F2-LINE-FC (I2)            = "C")
081500     AND (HR13F2-HDB-PLAN-TYPE (I2)      = HDB-PLAN-TYPE)
081600     AND (HR13F2-HDB-PLAN-CODE (I2)      = HDB-PLAN-CODE)
081700     AND (HR13F2-HDB-EMP-START (I2)      = HDB-EMP-START)
081800     AND (HR13F2-HDB-START-DATE (I2)     = HDB-START-DATE)
081900         IF  (HR13F2-HDB-STOP-DATE (I2)  NOT = ZEROS)
082000         AND (HR13F2-HDB-STOP-DATE (I2)  NOT < 
082100                                       HR13F2-HDB-START-DATE (I1))
082200             MOVE WS-TRUE                    TO HR13WS-CHG-HDB-SW
082300             MOVE 124                        TO CRT-ERROR-NBR
082400             MOVE HR13F2-LINE-FC-FN (I1)     TO CRT-FIELD-NBR
082500             GO TO 232-END
082600         ELSE
082700             MOVE WS-TRUE                TO HR13WS-CHG-HDB-SW
082800             GO TO 232-END.
082900
083000 232-END.
083100
083200******************************************************************
083300 234-EDIT-EXISTING-POST-HDB.
083400******************************************************************
083500
083600     IF (HDB-START-DATE          = HR13F2-HDB-START-DATE (I1))
083700         GO TO 234-FIND-NEXT-HDBSET1.
083800
083900     MOVE WS-FALSE               TO HR13WS-DEL-HDB-SW.
084000
084100     PERFORM 236-EDIT-DELETING-POST-HDB
084200     THRU    236-END
084300         VARYING I2 FROM 1 BY 1
084400         UNTIL  (DELETING-HDB)
084500         OR     (I2 > 10).                     
084600
084700     IF (DELETING-HDB)
084800         GO TO 234-FIND-NEXT-HDBSET1.
084900
085000     IF (NOT-DELETING-HDB)
085100         IF  (HR13F2-HDB-STOP-DATE (I1) = ZEROS)
               AND (HDB-START-DATE > HR13F2-HDB-START-DATE (I1))
085200             MOVE WS-TRUE            TO HR13WS-STOP-DFLT-SW
085300             MOVE HDB-START-DATE     TO WSDR-FR-DATE
085400             PERFORM 900-DATE-TO-JULIAN
085500             SUBTRACT 1              FROM WSDR-JULIAN-DAYS
085600             PERFORM 900-JULIAN-TO-DATE
085700             MOVE WSDR-FR-DATE       TO HR13F2-HDB-STOP-DATE (I1)
085800         ELSE
085900             IF (HR13F2-HDB-STOP-DATE (I1) NOT < HDB-START-DATE)
086000                 MOVE 124                     TO CRT-ERROR-NBR
086100                 MOVE HR13F2-LINE-FC-FN (I1)  TO CRT-FIELD-NBR
086200                 GO TO 234-END.
086300
086400 234-FIND-NEXT-HDBSET1.
086500
086600     PERFORM 860-FIND-NEXT-HDBSET1.
086700
086800 234-END.
086900
087000******************************************************************
087100 236-EDIT-DELETING-POST-HDB.
087200******************************************************************
087300
087400     IF  (HR13F2-HDB-PLAN-CODE (I2)   = HDB-PLAN-CODE)
087500     AND (HR13F2-HDB-START-DATE (I2)  = HDB-START-DATE)
087600     AND (HR13F2-LINE-FC (I2)     = "D")
087700         MOVE WS-TRUE            TO HR13WS-DEL-HDB-SW.
087800
087900 236-END.
088000
088100******************************************************************
088200 400-PROCESS-TRAN.
088300******************************************************************
088400
088500     IF (HR13F2-FC = "I" OR "+" OR "-")
088600         PERFORM 480-INQUIRE
088700         THRU    480-END
088800         GO TO 400-END.
088900
089000     IF (HR13F2-FC = "C")
089100         PERFORM 910-AUDIT-BEGIN
089200
089300         IF (DMS-ABORTED)
089400             GO TO 400-END
089500         END-IF
089600
089700         PERFORM 420-PROCESS-DETAIL
089800         THRU    420-END
089900             VARYING I1 FROM 1 BY 1
090000             UNTIL  (I1 > 10)
090100
090200         PERFORM 920-AUDIT-END
090300
090400     END-IF.
090500
090600     IF (HR13F2-FC = "C")
090700         MOVE SPACES                 TO HR13F2-FC 
090800         MOVE CRT-CHG-COMPLETE       TO CRT-MESSAGE.
090900
091000 400-END.
091100
091200******************************************************************
091300 420-PROCESS-DETAIL.          
091400******************************************************************
091500
091600     IF (HR13F2-LINE-FC (I1) = SPACES)
091700         GO TO 420-END.
091800
091900     PERFORM 520-MOVE-DTL-SCR-TO-WS
092000     THRU    520-END.
092100
092200     PERFORM 3000-HRHDB-PROCESS-TRAN.
092300
092400     IF (HR13F2-LINE-FC (I1) = "A" OR "C")
092500         PERFORM 530-MOVE-DTL-WS-TO-SCR
092600         THRU    530-END
092700         INITIALIZE HR13F2-LINE-FC (I1)

               MOVE PLN-CREATE-TRANS   TO HR13F2-BNT-CREATE-TRANS (I1)

015500         INITIALIZE HR13F2-BNT-REASON (I1)
015500                    HR13F2-BNT-MEMBER-ID (I1)
092800     ELSE
092900         INITIALIZE HR13F2-DETAIL-LINE (I1).
093000
093100 420-END.
093200
093300******************************************************************
093400 480-INQUIRE.
093500******************************************************************
093600
093700     IF (HR13F2-FC = "I")
093800         MOVE "+"                TO HR13F2-FC.
093900
           INITIALIZE                  HR13WS-SAVED-DATA.

094000     IF (HR13F2-FC = "-")
094100         MOVE HR13F2-HDB-PLAN-TYPE (1) TO HR13F2-PT-HDB-PLAN-TYPE
094200         MOVE HR13F2-HDB-PLAN-CODE (1) TO HR13F2-PT-HDB-PLAN-CODE 
094300         MOVE HR13F2-HDB-EMP-START (1) TO HR13F2-PT-HDB-EMP-START
094400         MOVE HR13F2-HDB-START-DATE(1) TO HR13F2-PT-HDB-START-DATE
094500         MOVE HR13F2-RECORD-ORIG   (1) TO HR13F2-PT-RECORD-ORIG
094600         INITIALIZE HR13F2-DETAIL-GROUP
094700         MOVE 10                       TO I1
094800         IF (HR13WS-RECORD-ORIG = "B")
094900             PERFORM 490-MOVE-BENEFIT
095000             THRU    490-END
095100                 UNTIL (I1 < 1)
095200                 OR    (BENEFIT-NOTFOUND)
095300                 OR    (BEN-COMPANY   NOT = DB-COMPANY)
095400                 OR    (BEN-EMPLOYEE  NOT = DB-EMPLOYEE) 
095500
095600             IF (BENEFIT-NOTFOUND)
095700             OR (BEN-COMPANY   NOT = DB-COMPANY)
095800             OR (BEN-EMPLOYEE  NOT = DB-EMPLOYEE) 
095900                 MOVE "D"            TO HR13WS-RECORD-ORIG
096000                 MOVE HIGH-VALUES    TO DB-PLAN-TYPE
096100                 MOVE HIGH-VALUES    TO DB-PLAN-CODE
096200                 MOVE WS-HIGH-VALUES TO DB-EMP-START
096300                 MOVE WS-HIGH-VALUES TO DB-START-DATE
096400                 PERFORM 850-FIND-NLT-HDBSET1
096500                 PERFORM 870-FIND-PREV-HDBSET1
096600             END-IF
096700         END-IF
096800         IF (HR13WS-RECORD-ORIG = "D")
096900             PERFORM 485-MOVE-HRDEPBEN
097000             THRU    485-END
097100                 UNTIL (I1 < 1)
097200                 OR    (HRDEPBEN-NOTFOUND)
097300                 OR    (HDB-COMPANY   NOT = DB-COMPANY)
097400                 OR    (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
097500                 OR    (HDB-DEPENDENT NOT = DB-DEPENDENT) 
097600         END-IF
097700         IF (I1 NOT < 1)
097800             COMPUTE I9              = I1 + 1
097900             MOVE 1                  TO I1
098000             PERFORM
098100                 UNTIL (I9 > 10)
098200                     MOVE HR13F2-DETAIL-LINE (I9)
098300                                     TO HR13F2-DETAIL-LINE (I1)
098400                     INITIALIZE         HR13F2-DETAIL-LINE (I9)
098500                     ADD 1           TO I1
098600                                        I9
098700             END-PERFORM
098800             MOVE "+"                TO HR13F2-FC
098900             MOVE HR13F2-PT-RECORD-ORIG      TO HR13WS-RECORD-ORIG
099000             MOVE HR13F2-PT-HDB-PLAN-TYPE    TO DB-PLAN-TYPE
099100             MOVE HR13F2-PT-HDB-PLAN-CODE    TO DB-PLAN-CODE
099200             MOVE HR13F2-PT-HDB-EMP-START    TO DB-EMP-START 
099300             MOVE HR13F2-PT-HDB-START-DATE   TO DB-START-DATE
099400             IF (HR13WS-RECORD-ORIG = "D")
099500                 PERFORM 850-FIND-NLT-HDBSET1
099600                 IF   (HR13WS-RECORD-ORIG = "D")
099700                 AND ((HRDEPBEN-NOTFOUND)
099800                 OR   (HDB-COMPANY      NOT = DB-COMPANY)
099900                 OR   (HDB-EMPLOYEE     NOT = DB-EMPLOYEE) 
100000                 OR   (HDB-DEPENDENT    NOT = DB-DEPENDENT))
100100                       MOVE "B"              TO HR13WS-RECORD-ORIG
100200                 END-IF
100300             END-IF
100400             IF (HR13WS-RECORD-ORIG = "B")
100500                 PERFORM 850-FIND-NLT-BENSET4
100600             END-IF
100700             MOVE 1                        TO I1
100800             IF (HR13WS-RECORD-ORIG = "D")
100900                 PERFORM 485-MOVE-HRDEPBEN
101000                 THRU    485-END
101100                     UNTIL (I1 > 10)
101200                     OR    (HRDEPBEN-NOTFOUND)
101300                     OR    (HDB-COMPANY   NOT = DB-COMPANY)
101400                     OR    (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
101500                     OR    (HDB-DEPENDENT NOT = DB-DEPENDENT) 
101600                 IF (HRDEPBEN-NOTFOUND)
101700                 OR (HDB-COMPANY   NOT = DB-COMPANY)
101800                 OR (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
101900                 OR (HDB-DEPENDENT NOT = DB-DEPENDENT) 
102000                     MOVE "B"            TO HR13WS-RECORD-ORIG
102100                     MOVE SPACES         TO DB-PLAN-TYPE
102200                     MOVE SPACES         TO DB-PLAN-CODE
102300                     MOVE ZEROES         TO DB-START-DATE
102400                     PERFORM 850-FIND-NLT-BENSET4
102500                 ELSE
102600                     MOVE "D"            TO HR13F2-PT-RECORD-ORIG
102700                     MOVE HDB-PLAN-TYPE  TO
102800                                         HR13F2-PT-HDB-PLAN-TYPE
102900                     MOVE HDB-PLAN-CODE  TO
103000                                         HR13F2-PT-HDB-PLAN-CODE
103100                     MOVE HDB-EMP-START  TO
103200                                         HR13F2-PT-HDB-EMP-START
103300                     MOVE HDB-START-DATE TO
103400                                         HR13F2-PT-HDB-START-DATE
103500                     MOVE CRT-MORE-RECS  TO CRT-MESSAGE
103600                 END-IF
103700             END-IF
103800             IF (HR13WS-RECORD-ORIG = "B")
103900                 PERFORM 490-MOVE-BENEFIT  
104000                 THRU    490-END
104100                     UNTIL (I1 > 10)
104200                     OR    (BENEFIT-NOTFOUND)
104300                     OR    (BEN-COMPANY   NOT = DB-COMPANY)
104400                     OR    (BEN-EMPLOYEE  NOT = DB-EMPLOYEE)
104500                 IF (BENEFIT-NOTFOUND)
104600                 OR (BEN-COMPANY   NOT = DB-COMPANY)
104700                 OR (BEN-EMPLOYEE  NOT = DB-EMPLOYEE) 
104800                     MOVE "D"          TO HR13F2-PT-RECORD-ORIG
104900                     MOVE SPACES       TO HR13F2-PT-HDB-PLAN-TYPE
105000                     MOVE SPACES       TO HR13F2-PT-HDB-PLAN-CODE
105100                     MOVE ZEROES       TO HR13F2-PT-HDB-EMP-START
105200                     MOVE ZEROES       TO HR13F2-PT-HDB-START-DATE
105300                     MOVE CRT-INQ-COMPLETE TO CRT-MESSAGE
105400                 ELSE
105500                     MOVE "B"             TO HR13F2-PT-RECORD-ORIG
105600                     MOVE BEN-PLAN-TYPE TO HR13F2-PT-HDB-PLAN-TYPE
105700                     MOVE BEN-PLAN-CODE TO HR13F2-PT-HDB-PLAN-CODE
105800                     MOVE BEN-START-DATE   TO
105900                                          HR13F2-PT-HDB-START-DATE
106000                     MOVE CRT-MORE-RECS    TO CRT-MESSAGE
106100                 END-IF
106200             END-IF
106300         END-IF
106400     ELSE
106500         INITIALIZE HR13F2-DETAIL-GROUP
106600         MOVE 1                        TO I1
106700         IF (HR13WS-RECORD-ORIG = "D")
106800             PERFORM 485-MOVE-HRDEPBEN
106900             THRU    485-END
107000                 UNTIL (I1 > 10)
107100                 OR    (HRDEPBEN-NOTFOUND)
107200                 OR    (HDB-COMPANY   NOT = DB-COMPANY)
107300                 OR    (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
107400                 OR    (HDB-DEPENDENT NOT = DB-DEPENDENT) 
107500             IF (HRDEPBEN-NOTFOUND)
107600             OR (HDB-COMPANY   NOT = DB-COMPANY)
107700             OR (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
107800             OR (HDB-DEPENDENT NOT = DB-DEPENDENT) 
107900                 MOVE "B"            TO HR13WS-RECORD-ORIG
108000                 MOVE SPACES         TO DB-PLAN-TYPE
108100                 MOVE SPACES         TO DB-PLAN-CODE
108200                 MOVE ZEROES         TO DB-START-DATE
108300                 PERFORM 850-FIND-NLT-BENSET4
108400             ELSE
108500                 MOVE "D"            TO HR13F2-PT-RECORD-ORIG
108600                 MOVE HDB-PLAN-TYPE  TO HR13F2-PT-HDB-PLAN-TYPE
108700                 MOVE HDB-PLAN-CODE  TO HR13F2-PT-HDB-PLAN-CODE
108800                 MOVE HDB-EMP-START  TO HR13F2-PT-HDB-EMP-START
108900                 MOVE HDB-START-DATE TO HR13F2-PT-HDB-START-DATE
109000                 MOVE CRT-MORE-RECS  TO CRT-MESSAGE
109100             END-IF
109200         END-IF
109300         IF (HR13WS-RECORD-ORIG = "B")
109400             PERFORM 490-MOVE-BENEFIT  
109500             THRU    490-END
109600                 UNTIL (I1 > 10)
109700                 OR    (BENEFIT-NOTFOUND)
109800                 OR    (BEN-COMPANY   NOT = DB-COMPANY)
109900                 OR    (BEN-EMPLOYEE  NOT = DB-EMPLOYEE)
110000             IF (BENEFIT-NOTFOUND)
110100             OR (BEN-COMPANY   NOT = DB-COMPANY)
110200             OR (BEN-EMPLOYEE  NOT = DB-EMPLOYEE) 
110300                 MOVE "D"              TO HR13F2-PT-RECORD-ORIG
110400                 MOVE SPACES           TO HR13F2-PT-HDB-PLAN-TYPE
110500                 MOVE SPACES           TO HR13F2-PT-HDB-PLAN-CODE
110600                 MOVE ZEROES           TO HR13F2-PT-HDB-EMP-START
110700                 MOVE ZEROES           TO HR13F2-PT-HDB-START-DATE
110800                 MOVE CRT-INQ-COMPLETE TO CRT-MESSAGE
110900             ELSE
111000                 MOVE "B"              TO HR13F2-PT-RECORD-ORIG
111100                 MOVE BEN-PLAN-TYPE    TO HR13F2-PT-HDB-PLAN-TYPE
111200                 MOVE BEN-PLAN-CODE    TO HR13F2-PT-HDB-PLAN-CODE
111300                 MOVE BEN-START-DATE   TO HR13F2-PT-HDB-START-DATE
111400                 MOVE CRT-MORE-RECS    TO CRT-MESSAGE
111500             END-IF
111600         END-IF.
111700 480-END.
111800
111900******************************************************************
112000 485-MOVE-HRDEPBEN.
112100******************************************************************
112200
112300     PERFORM 600-MOVE-HDB-TO-SCREEN
112400     THRU    600-END.
112500      
112600      IF (HR13F2-FC = "-")
112700          SUBTRACT 1              FROM I1
112800      ELSE 
112900          ADD 1                   TO I1.
113000
113100 485-NEXT-HRDEPBEN.
113200     IF (HR13F2-FC = "-")
113300         PERFORM 870-FIND-PREV-HDBSET1
113400     ELSE
113500         PERFORM 860-FIND-NEXT-HDBSET1.
113600
113700 485-END.
113800
113900******************************************************************
114000 490-MOVE-BENEFIT.
114100******************************************************************
114200
           IF (EMD-ACTIVE-FLAG         = "I")
               GO TO 490-NEXT-BENEFIT.

114300     MOVE HR13F2-EMD-COMPANY     TO DB-COMPANY. 
114400     PERFORM 840-FIND-BNCSET1. 
114500     IF  (BNC-DEP-HEALTH         = "N") 
114600     AND (BEN-PLAN-TYPE          = "HL")
114700         GO TO 490-NEXT-BENEFIT.
114800
114900     IF  (BNC-DEP-DENTAL         = "N") 
115000     AND (BEN-PLAN-TYPE          = "DN")
115100         GO TO 490-NEXT-BENEFIT.
115200
115300     IF  (BNC-DEP-DEP-LIFE       = "N") 
115400     AND (BEN-PLAN-TYPE          = "DL")
115500         GO TO 490-NEXT-BENEFIT.
115600
115700     IF (BEN-PLAN-TYPE  NOT = "HL" AND "DN" AND "EL" AND "DL")
115800         GO TO 490-NEXT-BENEFIT.
115900
116000     MOVE BEN-COMPANY                TO DB-COMPANY.
116100     MOVE BEN-PLAN-TYPE              TO DB-PLAN-TYPE.
116200     MOVE BEN-PLAN-CODE              TO DB-PLAN-CODE.
116300     PERFORM 840-FIND-PLNSET1.
116400
116500     IF (PLN-COVERAGE-TYPE           = "0")
116600         GO TO 490-NEXT-BENEFIT.
116700
           MOVE "E"                        TO HRDEP-COVER-TYPE.
           INITIALIZE HRDEP-CALC-AGE-DATE.
           PERFORM 5110-FIND-DEP-N-STUD-AGE.
           IF (ERROR-FOUND)
               GO TO 490-NEXT-BENEFIT
           ELSE
118400         MOVE HRDEP-DEP-AGE          TO HR13WS-DEP-AGE
118500         MOVE HRDEP-STUD-AGE         TO HR13WS-STUDENT-AGE
118600         MOVE HRDEP-COV-DEPS         TO HR13WS-COV-DEPENDENTS
               MOVE HRDEP-PT-STUDENTS      TO HR13WS-PT-STUDENTS
118600         MOVE HRDEP-TERM-OPT         TO HR13WS-TERM-OPT.
118700
118800     IF  ((PLN-COVERAGE-TYPE  = "1")
118900     AND  (COP-COV-DEPENDENTS = "N"))
119000     OR  ((PLN-COVERAGE-TYPE  = "2")
119100     AND  (CVR-LIFE-ADD-FLG   = "E"))
119200         GO TO 490-NEXT-BENEFIT.
119300
J07540* (E)MPLOYEE, (N)ONE, (S)POUSE, (P)ARTNER, SPOUSE (O)R PARTNER
119400     IF  (EMD-DEP-TYPE               = "D")
J07540     AND (HR13WS-COV-DEPENDENTS      = "E" OR "N" OR "S" OR 
J07540          "P" OR "O")
119600         GO TO 490-NEXT-BENEFIT.
119700
J45689* SEPERATING SPOUSE AND DOM PARTNER ELIGIBILITY CRITERIA
J45689*
J07540* (E)MPLOYEE, (N)ONE, (D)EPENDENTS, PA(R)TNER DEPS
J07540*    IF  (EMD-DEP-TYPE               = "S" OR "P") 
J07540*    AND (HR13WS-COV-DEPENDENTS      = "E" OR "N" OR "D" OR "R")
120000*        GO TO 490-NEXT-BENEFIT.
J45689*
J45689* (E)MPLOYEE, (N)ONE, (D)EPENDENTS, PA(R)TNER DEPS, (P)ARTNER, 
J45689* (C)PARTNER AND DEPENDENTS
J45689     IF  (EMD-DEP-TYPE               = "S")
J45689     AND (HR13WS-COV-DEPENDENTS      = "E" OR "N" OR "D" OR "R"
J45689                                    OR "P" OR "C")
J45689         GO TO 490-NEXT-BENEFIT
J45689     END-IF.
J45689*
J45689* (E)MPLOYEE, (N)ONE, (D)EPENDENTS, PA(R)TNER DEPS, (S)POUSE, 
J45689* (B)SPOUSE AND DEPENDENTS
J45689     IF  (EMD-DEP-TYPE               = "P")
J45689     AND (HR13WS-COV-DEPENDENTS      = "E" OR "N" OR "D" OR "R"
J45689                                    OR "B" OR "S")
J45689         GO TO 490-NEXT-BENEFIT
J45689     END-IF.
120100
P96883     IF (PLN-COVERAGE-TYPE       NOT = "0")
120200         PERFORM 610-MOVE-BEN-TO-SCREEN
120300         THRU    610-END.
120400      
120500      IF (HR13F2-FC = "-")
120600          SUBTRACT 1              FROM I1
120700      ELSE 
120800          ADD 1                   TO I1.
120900
121000 490-NEXT-BENEFIT.
121100     IF (HR13F2-FC = "-")
121200         PERFORM 870-FIND-PREV-BENSET4
121300     ELSE
121400         PERFORM 860-FIND-NEXT-BENSET4.
121500
121600 490-END.
121700
121800******************************************************************
121900 520-MOVE-DTL-SCR-TO-WS.
122000******************************************************************
122100
122200     INITIALIZE HRHDB-SCR-FIELDS.    
122300
122400     MOVE HR13F2-EMD-COMPANY         TO HRHDB-COMPANY.   
122500     MOVE HR13F2-EMD-COMPANY-FN      TO HRHDB-COMPANY-FN. 
122600     MOVE HR13F2-EMD-EMPLOYEE        TO HRHDB-EMPLOYEE. 
122700     MOVE HR13F2-EMD-EMPLOYEE-FN     TO HRHDB-EMPLOYEE-FN.
122800     MOVE HR13F2-EMD-SEQ-NBR         TO HRHDB-DEPENDENT.
122900     MOVE HR13F2-EMD-SEQ-NBR-FN      TO HRHDB-DEPENDENT-FN.
123000     MOVE HR13F2-LINE-FC (I1)        TO HRHDB-FC.
123100     MOVE HR13F2-LINE-FC-FN (I1)     TO HRHDB-FC-FN.
123200     MOVE HR13F2-HDB-START-DATE (I1) TO HRHDB-START-DATE. 
123300     MOVE HR13F2-HDB-START-DATE-FN (I1)
123400                                     TO HRHDB-START-DATE-FN.
123500     MOVE HR13F2-HDB-STOP-DATE (I1)  TO HRHDB-STOP-DATE. 
123600     MOVE HR13F2-HDB-STOP-DATE-FN (I1)
123700                                     TO HRHDB-STOP-DATE-FN.
123800     MOVE HR13F2-HDB-PLAN-TYPE (I1)  TO HRHDB-PLAN-TYPE. 
123900     MOVE HR13F2-HDB-PLAN-TYPE-FN (I1)
124000                                     TO HRHDB-PLAN-TYPE-FN.
124100     MOVE HR13F2-HDB-PLAN-CODE (I1)  TO HRHDB-PLAN-CODE. 
124200     MOVE HR13F2-HDB-PLAN-CODE-FN (I1)
124300                                     TO HRHDB-PLAN-CODE-FN.
124400     MOVE HR13F2-BEN-START-DATE (I1) TO HRHDB-BEN-START-DATE. 
124500     MOVE HR13F2-BEN-START-DATE-FN (I1)
124600                                     TO HRHDB-BEN-START-DATE-FN.
124700     MOVE HR13F2-BEN-STOP-DATE (I1)  TO HRHDB-BEN-STOP-DATE. 
124800     MOVE HR13F2-BEN-STOP-DATE-FN (I1)
124900                                     TO HRHDB-BEN-STOP-DATE-FN.
125000     MOVE HR13F2-HDB-EMP-START (I1)  TO HRHDB-EMP-START. 
125100     MOVE HR13F2-HDB-EMP-START-FN (I1)
125200                                     TO HRHDB-EMP-START-FN.
125300
125400     MOVE HR13F2-HILIGHT (I1)        TO HRHDB-HILIGHT.   
125500     MOVE "E"                        TO HRHDB-COVER-TYPE.   
125600
015500     MOVE HR13F2-BNT-CREATE-TRANS (I1)   TO HRHDB-CREATE-TRANS.
015600     MOVE HR13F2-BNT-CREATE-TRANS-FN(I1) TO HRHDB-CREATE-TRANS-FN.
015500     MOVE HR13F2-BNT-REASON (I1)         TO HRHDB-REASON.
015600     MOVE HR13F2-BNT-REASON-FN (I1)      TO HRHDB-REASON-FN.
015500     MOVE HR13F2-BNT-MEMBER-ID (I1)      TO HRHDB-MEMBER-ID.
015600     MOVE HR13F2-BNT-MEMBER-ID-FN (I1)   TO HRHDB-MEMBER-ID-FN.

125700 520-END.
125800
125900******************************************************************
126000 530-MOVE-DTL-WS-TO-SCR.
126100******************************************************************
126200
126300     MOVE HRHDB-START-DATE       TO HR13F2-HDB-START-DATE (I1).
126400     MOVE HRHDB-START-DATE-FN    TO HR13F2-HDB-START-DATE-FN (I1).
126500     MOVE HRHDB-STOP-DATE        TO HR13F2-HDB-STOP-DATE (I1).
126600     MOVE HRHDB-STOP-DATE-FN     TO HR13F2-HDB-STOP-DATE-FN (I1).
126700     MOVE HRHDB-PLAN-TYPE        TO HR13F2-HDB-PLAN-TYPE (I1). 
126800     MOVE HRHDB-PLAN-TYPE-FN     TO HR13F2-HDB-PLAN-TYPE-FN (I1). 
126900     MOVE HRHDB-PLAN-CODE        TO HR13F2-HDB-PLAN-CODE (I1). 
127000     MOVE HRHDB-PLAN-CODE-FN     TO HR13F2-HDB-PLAN-CODE-FN (I1). 
127100     MOVE HRHDB-BEN-START-DATE   TO HR13F2-BEN-START-DATE (I1).
127200     MOVE HRHDB-BEN-START-DATE-FN 
127300                                 TO HR13F2-BEN-START-DATE-FN (I1).
127400     MOVE HRHDB-BEN-STOP-DATE    TO HR13F2-BEN-STOP-DATE (I1).
127500     MOVE HRHDB-BEN-STOP-DATE-FN TO HR13F2-BEN-STOP-DATE-FN (I1).
127600     MOVE HRHDB-EMP-START        TO HR13F2-HDB-EMP-START (I1). 
127700     MOVE HRHDB-EMP-START-FN     TO HR13F2-HDB-EMP-START-FN (I1). 
127800
127900     MOVE HRHDB-HILIGHT          TO HR13F2-HILIGHT (I1).   
128000
128100     IF (HR13F2-HDB-PLAN-CODE (I1) NOT = SPACES)
128200         MOVE HR13F2-EMD-COMPANY        TO DB-COMPANY
128300         MOVE HR13F2-HDB-PLAN-TYPE (I1) TO DB-PLAN-TYPE
128400         MOVE HR13F2-HDB-PLAN-CODE (I1) TO DB-PLAN-CODE
128500         PERFORM 840-FIND-PLNSET1
128600         IF (PLAN-FOUND)
128700             MOVE PLN-DESC           TO HR13F2-PLN-DESC (I1)
128800         ELSE
128900             MOVE SPACES             TO HR13F2-PLN-DESC (I1)
129000     ELSE
129100         MOVE SPACES                 TO HR13F2-PLN-DESC (I1).
129200
           MOVE HRHDB-CREATE-TRANS     TO HR13F2-BNT-CREATE-TRANS (I1).
           MOVE HRHDB-REASON           TO HR13F2-BNT-REASON (I1).
           MOVE HRHDB-MEMBER-ID        TO HR13F2-BNT-MEMBER-ID (I1).

129300 530-END.
129400
129500******************************************************************
129600 600-MOVE-HDB-TO-SCREEN.
129700******************************************************************
129800
129900     MOVE CRT-HILITE             TO HR13F2-HILIGHT (I1)
                                          HR13WS-SAVED-HILITE (I1).
130000
130100     MOVE HDB-PLAN-TYPE          TO HR13F2-RECORD-PLAN-TYPE (I1).
130200
130300     MOVE "D"                    TO HR13F2-RECORD-ORIG (I1).
130400     MOVE HDB-START-DATE         TO HR13F2-HDB-START-DATE (I1)
                                          HR13WS-SAVED-START-DATE (I1).
130500     MOVE HDB-STOP-DATE          TO HR13F2-HDB-STOP-DATE (I1).
130600     MOVE HDB-PLAN-TYPE          TO HR13F2-HDB-PLAN-TYPE (I1)
                                          HR13WS-SAVED-PLAN-TYPE (I1).
130700     MOVE HDB-PLAN-CODE          TO HR13F2-HDB-PLAN-CODE (I1)
                                          HR13WS-SAVED-PLAN-CODE (I1).
130800     MOVE HDB-EMP-START          TO HR13F2-HDB-EMP-START (I1)
                                          HR13WS-SV-EMP-START-DT (I1).
130900     
131000     MOVE HDB-COMPANY            TO DB-COMPANY.
131100     MOVE HDB-PLAN-TYPE          TO DB-PLAN-TYPE.
131200     MOVE HDB-PLAN-CODE          TO DB-PLAN-CODE.
131300     PERFORM 840-FIND-PLNSET1.
131400     IF (PLAN-FOUND)
131500         MOVE PLN-DESC           TO HR13F2-PLN-DESC (I1)
131600     ELSE
131700         MOVE SPACES             TO HR13F2-PLN-DESC (I1).
131800
131900     MOVE HDB-COMPANY            TO DB-COMPANY. 
132000     MOVE HDB-EMPLOYEE           TO DB-EMPLOYEE. 
132100     MOVE HDB-PLAN-TYPE          TO DB-PLAN-TYPE. 
132200     MOVE HDB-PLAN-CODE          TO DB-PLAN-CODE.
132300     MOVE HDB-EMP-START          TO DB-START-DATE.
132400     PERFORM 840-FIND-BENSET4.
132500     IF (BENEFIT-FOUND)
132600         MOVE BEN-START-DATE     TO HR13F2-BEN-START-DATE (I1)
132700         MOVE BEN-STOP-DATE      TO HR13F2-BEN-STOP-DATE (I1)
132800     ELSE
132900         MOVE ZEROES             TO HR13F2-BEN-START-DATE (I1)
133000         MOVE ZEROES             TO HR13F2-BEN-STOP-DATE (I1).
133100
           MOVE PLN-CREATE-TRANS       TO HR13F2-BNT-CREATE-TRANS (I1).
           IF (PLN-CREATE-TRANS        = "Y")
195200         MOVE 106                TO CRT-MSG-NBR
091500         MOVE "HRCOM"            TO CRT-ERROR-CAT
091700         PERFORM 790-GET-MSG
091800         MOVE CRT-MESSAGE        TO HR13F2-HIPAA (I1).

133200 600-END.
133300
133400******************************************************************
133500 610-MOVE-BEN-TO-SCREEN.
133600******************************************************************
133700
133800     MOVE BEN-PLAN-TYPE          TO HR13F2-RECORD-PLAN-TYPE (I1).
133900
134000     MOVE "B"                    TO HR13F2-RECORD-ORIG (I1).
134100
J07540     IF (EMD-DEP-TYPE            = "S" OR "P")
134300         MOVE BEN-START-DATE     TO HR13F2-HDB-START-DATE (I1)
134400         MOVE BEN-STOP-DATE      TO HR13F2-HDB-STOP-DATE (I1).
134500
134600     IF (EMD-DEP-TYPE            = "D")
134700         IF (BEN-START-DATE      >= EMD-BIRTHDATE)
134800             MOVE BEN-START-DATE TO HR13F2-HDB-START-DATE (I1)
134900         ELSE
135000             MOVE EMD-BIRTHDATE  TO HR13F2-HDB-START-DATE (I1)
135100         END-IF
135200     END-IF.
135300
           COMPUTE I4 = (I1 - 1).
           PERFORM
               UNTIL   (I4  = 0)
               OR     ((HR13WS-SAVED-PLAN-TYPE (I4) = BEN-PLAN-TYPE)
               AND     (HR13WS-SAVED-PLAN-CODE (I4) = BEN-PLAN-CODE)
               AND     (HR13WS-SV-EMP-START-DT (I4) = BEN-START-DATE))
                       SUBTRACT 1 FROM I4
           END-PERFORM.

           IF  (I4 NOT = ZEROS)
           AND (HR13WS-SAVED-HILITE (I4)    = "*")
           AND (HR13WS-SAVED-PLAN-TYPE (I4) = BEN-PLAN-TYPE)
           AND (HR13WS-SAVED-PLAN-CODE (I4) = BEN-PLAN-CODE)
           AND (HR13WS-SV-EMP-START-DT (I4) = BEN-START-DATE)
               MOVE HR13WS-SAVED-START-DATE (I4)
                                       TO HR13F2-HDB-START-DATE (I1).
137500
J07540     IF (EMD-DEP-TYPE            = "S" OR "P")
           OR (EMD-DISABLED            = "Y")
               CONTINUE
           ELSE
               INITIALIZE HRDEP-CALC-DEP-N-STUD
               PERFORM 5200-DEP-AGE-DATE-CALC

               MOVE HRDEP-DEP-END-DATE     TO HR13WS-DEP-END-DATE

138600         IF (HR13WS-DEP-END-DATE     < BEN-START-DATE)
138700             INITIALIZE HR13F2-HDB-START-DATE (I1)
138800                    HR13F2-HDB-STOP-DATE (I1)
138900         ELSE
139000         IF (HR13WS-DEP-END-DATE     NOT < BEN-START-DATE)
139100             IF (HR13WS-DEP-END-DATE < BEN-STOP-DATE)
139200                 MOVE HR13WS-DEP-END-DATE 
139300                                     TO HR13F2-HDB-STOP-DATE (I1)
139400             ELSE
139500             IF (BEN-STOP-DATE       NOT = ZEROES)
139600                 MOVE BEN-STOP-DATE  TO HR13F2-HDB-STOP-DATE (I1)
139800             ELSE
139900                 MOVE HR13WS-DEP-END-DATE  
140000                                     TO HR13F2-HDB-STOP-DATE (I1).
140100
142800     IF  (EMD-DEP-TYPE = "D")
142900     AND (EMD-DISABLED = "Y")
143000         MOVE BEN-STOP-DATE      TO HR13F2-HDB-STOP-DATE (I1).
143100
143200     MOVE HR13F2-EMD-COMPANY         TO DB-COMPANY. 
143300     MOVE HR13F2-EMD-EMPLOYEE        TO DB-EMPLOYEE. 
143400     MOVE HR13F2-EMD-SEQ-NBR         TO DB-DEPENDENT. 
143500     MOVE BEN-PLAN-TYPE              TO DB-PLAN-TYPE. 
143600     MOVE BEN-PLAN-CODE              TO DB-PLAN-CODE. 
143700     MOVE BEN-START-DATE             TO DB-EMP-START.
143800     MOVE HR13F2-HDB-START-DATE (I1) TO DB-START-DATE
                                              HRDEP-HDB-START-DATE.
312700     MOVE HR13F2-HDB-STOP-DATE (I1)  TO HRDEP-HDB-STOP-DATE.
           MOVE BEN-STOP-DATE              TO HRDEP-BEN-STOP-DATE.
           PERFORM 5300-GET-NEW-HDB-START-N-STOP.

           MOVE HRDEP-HDB-START-DATE   TO HR13F2-HDB-START-DATE (I1).
           MOVE HRDEP-HDB-STOP-DATE    TO HR13F2-HDB-STOP-DATE (I1).

145000     MOVE BEN-START-DATE         TO HR13F2-BEN-START-DATE (I1) 
145100                                    HR13F2-HDB-EMP-START (I1).
145200     MOVE BEN-STOP-DATE          TO HR13F2-BEN-STOP-DATE (I1). 
145300     MOVE BEN-PLAN-TYPE          TO HR13F2-HDB-PLAN-TYPE (I1). 
145400     MOVE BEN-PLAN-CODE          TO HR13F2-HDB-PLAN-CODE (I1).
145500
145600     IF (HR13F2-HDB-PLAN-CODE (I1) NOT = SPACES)
145700         MOVE HR13F2-EMD-COMPANY        TO DB-COMPANY
145800         MOVE HR13F2-HDB-PLAN-TYPE (I1) TO DB-PLAN-TYPE
145900         MOVE HR13F2-HDB-PLAN-CODE (I1) TO DB-PLAN-CODE
146000         PERFORM 840-FIND-PLNSET1
146100         IF (PLAN-FOUND)
146200             MOVE PLN-DESC           TO HR13F2-PLN-DESC (I1)
146300         ELSE
146400             MOVE SPACES             TO HR13F2-PLN-DESC (I1)
146500     ELSE
146600         MOVE SPACES                 TO HR13F2-PLN-DESC (I1).
146700
           MOVE PLN-CREATE-TRANS       TO HR13F2-BNT-CREATE-TRANS (I1).
           IF (PLN-CREATE-TRANS        = "Y")
195200         MOVE 106                TO CRT-MSG-NBR
091500         MOVE "HRCOM"            TO CRT-ERROR-CAT
091700         PERFORM 790-GET-MSG
091800         MOVE CRT-MESSAGE        TO HR13F2-HIPAA (I1).

146800 610-END.             
146900
147000******************************************************************
147100 HR13S2-TRANSACTION-END.
147200******************************************************************
147300
147400******************************************************************
147500 HR13S3-TRANSACTION              SECTION 41.
147600******************************************************************
147700 HR13S3-START.
147800
171600     MOVE CRT-SCREEN-CODE        TO HR13F3-SCREEN-CODE.

147900     PERFORM 200-EDIT-TRAN
148000     THRU    200-END.
148100
148200     IF (NO-ERROR-FOUND)
148300         PERFORM 400-PROCESS-TRAN
148400         THRU    400-END.
148500
148600     GO TO HR13S3-TRANSACTION-END.
148700
148800******************************************************************
148900 200-EDIT-TRAN.
149000******************************************************************
149100
149200     MOVE HR13F3-BEN-COMPANY         TO DB-COMPANY.
149300     MOVE HR13F3-BEN-PLAN-TYPE       TO DB-PLAN-TYPE.
149400     MOVE HR13F3-BEN-PLAN-CODE       TO DB-PLAN-CODE.
149500     PERFORM 840-FIND-PLNSET1.
149600     MOVE PLN-DESC               TO HR13F3-PLN-DESC.
149700
149800     MOVE HR13F3-BEN-START-DATE  TO HR13F3-BEN-START.
149900     MOVE HR13F3-BEN-STOP-DATE   TO HR13F3-BEN-STOP.
150000
150100     MOVE HR13F3-BEN-COMPANY         TO DB-COMPANY. 
150200     PERFORM 840-FIND-BNCSET1. 
150300     IF  (HR13F3-BEN-PLAN-TYPE = "HL")
150400     AND (BNC-DEP-HEALTH   = "N") 
150500         MOVE 125                            TO CRT-ERROR-NBR
150600         GO TO 200-END
150700     END-IF.
150800     IF  (HR13F3-BEN-PLAN-TYPE = "DN")
150900     AND (BNC-DEP-DENTAL   = "N") 
151000         MOVE 125                            TO CRT-ERROR-NBR
151100         GO TO 200-END
151200     END-IF.
151300     IF  (HR13F3-BEN-PLAN-TYPE = "DL")
151400     AND (BNC-DEP-DEP-LIFE = "N") 
151500         MOVE 125                            TO CRT-ERROR-NBR
151600         GO TO 200-END
151700     END-IF.
151800
152000     MOVE HR13F3-BEN-COMPANY                 TO DB-COMPANY.
152200     MOVE HR13F3-BEN-PLAN-TYPE               TO DB-PLAN-TYPE.
152100     MOVE HR13F3-BEN-EMPLOYEE                TO DB-EMPLOYEE.
152400     MOVE HR13F3-BEN-START-DATE              TO DB-START-DATE.
152300     MOVE HR13F3-BEN-PLAN-CODE               TO DB-PLAN-CODE.
152500     PERFORM 840-FIND-BENSET1.
           IF (BENEFIT-NOTFOUND)
               MOVE 202                            TO CRT-ERROR-NBR
               MOVE HR13F3-FC-FN                   TO CRT-FIELD-NBR
               GO TO 200-END.

151900     IF  (HR13F3-BEN-PLAN-TYPE = "HL" OR "DN") 
152000         MOVE HR13F3-BEN-COMPANY             TO DB-COMPANY
152100         MOVE HR13F3-BEN-EMPLOYEE            TO DB-EMPLOYEE
152200         MOVE HR13F3-BEN-PLAN-TYPE           TO DB-PLAN-TYPE
152300         MOVE HR13F3-BEN-PLAN-CODE           TO DB-PLAN-CODE
152400         MOVE HR13F3-BEN-START-DATE          TO DB-START-DATE
152500         PERFORM 840-FIND-BENSET4
152600         IF (BENEFIT-FOUND)
152700             IF (PLN-COVERAGE-TYPE = "1")
152800                 MOVE BEN-COV-OPTION      TO DB-COVERAGE-OPT
152900                 PERFORM 840-FIND-COPSET1
153000             ELSE
153100                 MOVE BEN-COV-UPD-DT      TO DB-START-DATE
153200                 MOVE BEN-COV-GROUP       TO DB-GROUP-NAME
153300                 PERFORM 840-FIND-CVRSET1
153400             END-IF
153500             IF  ((PLN-COVERAGE-TYPE  = "1")
153600             AND  (COP-COV-DEPENDENTS = "N"))
153700             OR  ((PLN-COVERAGE-TYPE  = "2")
153800             AND  (CVR-LIFE-ADD-FLG   = "E"))
153900                 MOVE 126                    TO CRT-ERROR-NBR
154000                 GO TO 200-END
154100             END-IF
154200         END-IF.
154300
154400     PERFORM 210-EDIT-ACCESS
154500     THRU    210-END.
154600
154700     IF (ERROR-FOUND)
154800         GO TO 200-END.
154900
155000     IF (HR13F3-FC = "C")
155100         PERFORM 220-EDIT-DATA
155200         THRU    220-END
155300         GO TO 200-END.
155400
155500 200-END.
155600
155700******************************************************************
155800 210-EDIT-ACCESS.
155900******************************************************************
156000
156100     IF (HR13F3-FC = "C")
156200         GO TO 210-END.
156300
156400     IF  (HR13F3-FC              = "I")
156500     OR  (HR13F3-BEN-COMPANY     NOT = HR13F3-PT-BEN-COMPANY)
156600     OR  (HR13F3-BEN-PLAN-TYPE   NOT = HR13F3-PT-BEN-PLAN-TYPE)
156700     OR  (HR13F3-BEN-PLAN-CODE   NOT = HR13F3-PT-BEN-PLAN-CODE)
156800     OR  (HR13F3-BEN-EMPLOYEE    NOT = HR13F3-PT-BEN-EMPLOYEE)
156900     OR ((HR13F3-FC              = "+")
157000     AND (HR13F3-PT-RECORD-ORIG  = "E"))
157100         MOVE "D"                         TO HR13WS-RECORD-ORIG
157200         MOVE HR13F3-BEN-COMPANY          TO DB-COMPANY  
157300         MOVE HR13F3-BEN-EMPLOYEE         TO DB-EMPLOYEE 
157400         MOVE HR13F3-BEN-PLAN-TYPE        TO DB-PLAN-TYPE
157500         MOVE HR13F3-BEN-PLAN-CODE        TO DB-PLAN-CODE
157600         MOVE HR13F3-BEN-START-DATE       TO DB-EMP-START
157700         MOVE ZEROES                      TO DB-DEPENDENT
157800         MOVE ZEROES                      TO DB-START-DATE
157900     ELSE
158000     IF (HR13F3-FC = "+")
158100         MOVE HR13F3-PT-HDB-DEPENDENT     TO DB-DEPENDENT
158200         MOVE HR13F3-PT-HDB-DEPENDENT     TO DB-SEQ-NBR  
158300         MOVE HR13F3-PT-HDB-EMP-START     TO DB-EMP-START
158400         MOVE HR13F3-PT-HDB-START-DATE    TO DB-START-DATE
158500         MOVE HR13F3-PT-RECORD-ORIG       TO HR13WS-RECORD-ORIG
158600     ELSE
158700         MOVE HR13F3-HDB-DEPENDENT (1)    TO DB-DEPENDENT
158800         MOVE HR13F3-HDB-DEPENDENT (1)    TO DB-SEQ-NBR  
158900         MOVE HR13F3-HDB-EMP-START (1)    TO DB-EMP-START
159000         MOVE HR13F3-HDB-START-DATE (1)   TO DB-START-DATE    
159100         MOVE HR13F3-RECORD-ORIG (1)      TO HR13WS-RECORD-ORIG.
159200
159300     IF (HR13WS-RECORD-ORIG = "D")
159400         PERFORM 850-FIND-NLT-HDBSET3
159500         IF (HR13F3-FC    = "-")
159600             PERFORM 870-FIND-PREV-HDBSET3
159700             IF (HRDEPBEN-NOTFOUND)
159800             OR (HDB-COMPANY     NOT = DB-COMPANY)
159900             OR (HDB-EMPLOYEE    NOT = DB-EMPLOYEE)
160000             OR (HDB-PLAN-TYPE   NOT = DB-PLAN-TYPE)
160100             OR (HDB-PLAN-CODE   NOT = DB-PLAN-CODE)
160200             OR (HDB-EMP-START   NOT = DB-EMP-START)
160300                 MOVE 151                     TO CRT-ERROR-NBR
160400                 MOVE HR13F3-FC-FN            TO CRT-FIELD-NBR
160500                 GO TO 210-END.
160600
160700     IF   (HR13WS-RECORD-ORIG = "D")
160800     AND ((HRDEPBEN-NOTFOUND)
160900     OR   (HDB-COMPANY      NOT = DB-COMPANY)
161000     OR   (HDB-EMPLOYEE     NOT = DB-EMPLOYEE) 
161100     OR   (HDB-PLAN-TYPE    NOT = DB-PLAN-TYPE) 
161200     OR   (HDB-PLAN-CODE    NOT = DB-PLAN-CODE)
161300     OR   (HDB-EMP-START    NOT = DB-EMP-START))
161400             MOVE "B"                    TO HR13WS-RECORD-ORIG
161500             MOVE HR13F3-BEN-COMPANY     TO DB-COMPANY   
161600             MOVE HR13F3-BEN-EMPLOYEE    TO DB-EMPLOYEE 
161700             MOVE ZEROES                 TO DB-SEQ-NBR.   
161800
161900     IF (HR13WS-RECORD-ORIG = "B")
162000         PERFORM 850-FIND-NLT-EMDSET1
162100         IF (HR13F3-FC    = "-")
162200             PERFORM 870-FIND-PREV-EMDSET1
162300             IF (EMDEPEND-NOTFOUND)
162400             OR (EMD-COMPANY     NOT = DB-COMPANY)
162500             OR (EMD-EMPLOYEE    NOT = DB-EMPLOYEE)
162600                 MOVE WS-HIGH-VALUES      TO DB-SEQ-NBR   
162700                 PERFORM 850-FIND-NLT-EMDSET1
162800                 PERFORM 870-FIND-PREV-EMDSET1
162900                 MOVE "D"                 TO HR13WS-RECORD-ORIG
163000                 IF (EMDEPEND-NOTFOUND)
163100                 OR (EMD-COMPANY     NOT = DB-COMPANY)
163200                 OR (EMD-EMPLOYEE    NOT = DB-EMPLOYEE)
163300                     MOVE 151                     TO CRT-ERROR-NBR
163400                     MOVE HR13F3-FC-FN            TO CRT-FIELD-NBR
163500                     GO TO 210-END.
163600
163700 210-END.
163800
163900******************************************************************
164000 220-EDIT-DATA.
164100******************************************************************
164200
164300     PERFORM 222-EDIT-DTL-TRAN
164400     THRU    222-END
164500         VARYING I1 FROM 1 BY 1
164600         UNTIL  (I1 > 10)
164700         OR     (ERROR-FOUND).
164800*
164900     IF (ERROR-FOUND)
165000         GO TO 220-END.
165100              
165200 220-END.
165300
165400******************************************************************
165500 222-EDIT-DTL-TRAN.
165600******************************************************************
165700
165800     IF (HR13F3-LINE-FC (I1) = SPACES)
165900         GO TO 222-END.
166000
166100     PERFORM 520-MOVE-DTL-SCR-TO-WS
166200     THRU    520-END.
166300
166400     PERFORM 2000-HRHDB-EDIT-TRAN.
166500
166600     PERFORM 530-MOVE-DTL-WS-TO-SCR
166700     THRU    530-END.
166800
166900     IF (ERROR-FOUND)
167000         GO TO 222-END.
167100
167200     IF (HR13F3-LINE-FC (I1) = "A")
167300         MOVE HR13F3-BEN-COMPANY         TO DB-COMPANY  
167400         MOVE HR13F3-BEN-EMPLOYEE        TO DB-EMPLOYEE  
167500         MOVE HR13F3-HDB-DEPENDENT (I1)  TO DB-DEPENDENT  
167600         MOVE HR13F3-BEN-PLAN-TYPE       TO DB-PLAN-TYPE  
167700         MOVE HR13F3-BEN-PLAN-CODE       TO DB-PLAN-CODE  
167800         MOVE HR13F3-BEN-START-DATE      TO DB-EMP-START 
167900         MOVE HR13F3-HDB-START-DATE (I1) TO DB-START-DATE 
168000         PERFORM 850-FIND-NLT-HDBSET1
168800         PERFORM 870-FIND-PREV-HDBSET1
168900         IF  (HRDEPBEN-FOUND)
169000         AND (HDB-COMPANY   = DB-COMPANY)
169100         AND (HDB-EMPLOYEE  = DB-EMPLOYEE)
169200         AND (HDB-DEPENDENT = DB-DEPENDENT)
169300         AND (HDB-PLAN-TYPE = DB-PLAN-TYPE)
169400         AND (HDB-PLAN-CODE = DB-PLAN-CODE)
169500         AND (HDB-EMP-START = DB-EMP-START)
169700             PERFORM 230-EDIT-EXISTING-PREV-HDB
169800             THRU    230-END
170000         END-IF
170100     END-IF.
170200     IF (ERROR-FOUND)
170300         GO TO 222-END.
170400
170500     MOVE HR13F3-BEN-COMPANY         TO DB-COMPANY.   
170600     MOVE HR13F3-BEN-EMPLOYEE        TO DB-EMPLOYEE. 
170700     MOVE HR13F3-HDB-DEPENDENT (I1)  TO DB-DEPENDENT.  
170800     MOVE HR13F3-BEN-PLAN-TYPE       TO DB-PLAN-TYPE.  
170900     MOVE HR13F3-BEN-PLAN-CODE       TO DB-PLAN-CODE.  
171000     MOVE HR13F3-BEN-START-DATE      TO DB-EMP-START. 
171100     MOVE HR13F3-HDB-START-DATE (I1) TO DB-START-DATE. 
171200     PERFORM 850-FIND-NLT-HDBSET1.
171300     IF (HRDEPBEN-NOTFOUND)
171400     OR (HDB-COMPANY   NOT = DB-COMPANY)
171500     OR (HDB-EMPLOYEE  NOT = DB-EMPLOYEE)
171600     OR (HDB-DEPENDENT NOT = DB-DEPENDENT)
171700     OR (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE)
171800     OR (HDB-PLAN-CODE NOT = DB-PLAN-CODE)
171900     OR (HDB-EMP-START NOT = DB-EMP-START)
172000         PERFORM 870-FIND-PREV-HDBSET1
172100         IF  (HRDEPBEN-FOUND)
172200         AND (HDB-COMPANY   = DB-COMPANY)
172300         AND (HDB-EMPLOYEE  = DB-EMPLOYEE)
172400         AND (HDB-DEPENDENT = DB-DEPENDENT)
172500         AND (HDB-PLAN-TYPE = DB-PLAN-TYPE)
172600         AND (HDB-PLAN-CODE = DB-PLAN-CODE)
172700         AND (HDB-EMP-START = DB-EMP-START)
172800         AND (HDB-START-DATE = DB-START-DATE)
172900             IF  (HR13F3-LINE-FC (I1)         = "A")
173000             OR ((HR13F3-LINE-FC (I1)         = "C")
173100             AND (HR13F3-HDB-STOP-DATE (I1)   > HDB-STOP-DATE))
173200             OR ((HR13F3-LINE-FC (I1)         = "C")
173300             AND (HR13F3-HDB-STOP-DATE (I1)   NOT = HDB-STOP-DATE)
173400             AND (HR13F3-HDB-STOP-DATE (I1)   = ZEROS))
173500                 MOVE WS-FALSE       TO HR13WS-STOP-DFLT-SW
173600                 PERFORM 234-EDIT-EXISTING-POST-HDB
173700                 THRU    234-END
173800                     UNTIL  (ERROR-FOUND)
173900                     OR     (STOP-DEFAULTED)
174000                     OR     (HRDEPBEN-NOTFOUND)
174100                     OR     (HDB-COMPANY       NOT = DB-COMPANY)
174200                     OR     (HDB-EMPLOYEE      NOT = DB-EMPLOYEE)
                           OR     (HDB-DEPENDENT     NOT = DB-DEPENDENT)
174300                     OR     (HDB-PLAN-TYPE     NOT = DB-PLAN-TYPE)
174400                     OR     (HDB-PLAN-CODE     NOT = DB-PLAN-CODE)
174500                     OR     (HDB-EMP-START     NOT = DB-EMP-START)
174600                     OR   ((HR13F3-HDB-STOP-DATE (I1) NOT = ZEROS)
174700                     AND    (HR13F3-HDB-STOP-DATE (I1) <
174800                                                  HDB-START-DATE))
                   END-IF
               END-IF
           ELSE
           IF  (HRDEPBEN-FOUND)
           AND (HDB-COMPANY   = DB-COMPANY)
           AND (HDB-EMPLOYEE  = DB-EMPLOYEE)
           AND (HDB-DEPENDENT = DB-DEPENDENT)
           AND (HDB-PLAN-TYPE = DB-PLAN-TYPE)
           AND (HDB-PLAN-CODE = DB-PLAN-CODE)
           AND (HDB-EMP-START = DB-EMP-START)
               IF  (HR13F3-LINE-FC (I1)         = "A")
               OR ((HR13F3-LINE-FC (I1)         = "C")
               AND (HR13F3-HDB-STOP-DATE (I1)   > HDB-STOP-DATE))
               OR ((HR13F3-LINE-FC (I1)         = "C")
               AND (HR13F3-HDB-STOP-DATE (I1)   NOT = HDB-STOP-DATE)
               AND (HR13F3-HDB-STOP-DATE (I1)   = ZEROS))
                   MOVE WS-FALSE       TO HR13WS-STOP-DFLT-SW
                   PERFORM 234-EDIT-EXISTING-POST-HDB
                   THRU    234-END
                       UNTIL  (ERROR-FOUND)
                       OR     (STOP-DEFAULTED)
                       OR     (HRDEPBEN-NOTFOUND)
                       OR     (HDB-COMPANY       NOT = DB-COMPANY)
                       OR     (HDB-EMPLOYEE      NOT = DB-EMPLOYEE)
                       OR     (HDB-DEPENDENT     NOT = DB-DEPENDENT)
                       OR     (HDB-PLAN-TYPE     NOT = DB-PLAN-TYPE)
                       OR     (HDB-PLAN-CODE     NOT = DB-PLAN-CODE)
                       OR     (HDB-EMP-START     NOT = DB-EMP-START)
                       OR   ((HR13F3-HDB-STOP-DATE (I1) NOT = ZEROS)
                       AND    (HR13F3-HDB-STOP-DATE (I1) <
                                                    HDB-START-DATE))
               END-IF
           END-IF.
174900     IF (ERROR-FOUND)
175000         GO TO 222-END.

J10377     IF (HR13F3-LINE-FC (I1) = "C" OR "D")
J10377         IF (HR13F3-HDB-PARTICIPNT (I1) NOT = ZEROS)
J10377             MOVE 127                        TO CRT-ERROR-NBR
J10377             MOVE HR13F3-LINE-FC-FN (I1)     TO CRT-FIELD-NBR
J10377             GO TO 222-END
J10377         END-IF
J10377     END-IF.
J10377     IF (ERROR-FOUND)
J10377         GO TO 222-END
           END-IF.   
175100
175200 222-END.
175300
175400******************************************************************
175500 230-EDIT-EXISTING-PREV-HDB.
175600******************************************************************
175700
175800     MOVE WS-FALSE               TO HR13WS-DEL-HDB-SW
175900                                    HR13WS-CHG-HDB-SW.
176000
176100     PERFORM 232-EDIT-CHANGING-HDB
176200     THRU    232-END
176300         VARYING I2 FROM 1 BY 1
176400         UNTIL  (I2 = I1)
176500         OR     (DELETING-HDB)
176600         OR     (CHANGING-HDB).
176700
176800     IF  (NOT-CHANGING-HDB)
176900     AND ((HDB-STOP-DATE         NOT = ZEROS)
177000     AND (HDB-STOP-DATE          NOT < HR13F3-HDB-START-DATE (I1))
           OR  (HDB-STOP-DATE          = ZEROS))
177100         MOVE 124                            TO CRT-ERROR-NBR
177200         MOVE HR13F3-LINE-FC-FN (I1)         TO CRT-FIELD-NBR
177300         GO TO 230-END.
177400
177500 230-END.
177600
177700******************************************************************
177800 232-EDIT-CHANGING-HDB.
177900******************************************************************
178000
178100     IF  (HR13F3-LINE-FC (I2)        = "D")
178200     AND (HR13F3-BEN-PLAN-TYPE       = HDB-PLAN-TYPE)
178300     AND (HR13F3-BEN-PLAN-CODE       = HDB-PLAN-CODE)
178400     AND (HR13F3-BEN-START-DATE      = HDB-EMP-START)
178500     AND (HR13F3-HDB-START-DATE (I2) = HDB-START-DATE)
178600         MOVE WS-TRUE                TO HR13WS-DEL-HDB-SW
178700         GO TO 232-END.
178800
178900     IF  (HR13F3-LINE-FC (I2)            = "C")
179000     AND (HR13F3-BEN-PLAN-TYPE           = HDB-PLAN-TYPE)
179100     AND (HR13F3-BEN-PLAN-CODE           = HDB-PLAN-CODE)
179200     AND (HR13F3-BEN-START-DATE          = HDB-EMP-START)
179300     AND (HR13F3-HDB-START-DATE (I2)     = HDB-START-DATE)
179400         IF  (HR13F3-HDB-STOP-DATE (I2)  NOT = ZEROS)
179500         AND (HR13F3-HDB-STOP-DATE (I2)  NOT < 
179600                                       HR13F3-HDB-START-DATE (I1))
179700             MOVE WS-TRUE                    TO HR13WS-CHG-HDB-SW
179800             MOVE 124                        TO CRT-ERROR-NBR
179900             MOVE HR13F3-LINE-FC-FN (I1)     TO CRT-FIELD-NBR
180000             GO TO 232-END
180100         ELSE
180200             MOVE WS-TRUE                TO HR13WS-CHG-HDB-SW
180300             GO TO 232-END.
180400
180500 232-END.
180600
180700******************************************************************
180800 234-EDIT-EXISTING-POST-HDB.
180900******************************************************************
181000
181100     IF (HDB-START-DATE          = HR13F3-HDB-START-DATE (I1))
181200         GO TO 234-FIND-NEXT-HDBSET1.
181300
181400     MOVE WS-FALSE               TO HR13WS-DEL-HDB-SW.
181500
181600     PERFORM 236-EDIT-DELETING-POST-HDB
181700     THRU    236-END
181800         VARYING I2 FROM 1 BY 1
181900         UNTIL  (DELETING-HDB)
182000         OR     (I2 > 10).
182100
182200     IF (DELETING-HDB)
182300         GO TO 234-FIND-NEXT-HDBSET1.
182400
182500     IF (NOT-DELETING-HDB)
182600         IF  (HR13F3-HDB-STOP-DATE (I1) = ZEROS)
               AND (HDB-START-DATE > HR13F3-HDB-START-DATE (I1))
182700             MOVE WS-TRUE            TO HR13WS-STOP-DFLT-SW
182800             MOVE HDB-START-DATE     TO WSDR-FR-DATE
182900             PERFORM 900-DATE-TO-JULIAN
183000             SUBTRACT 1              FROM WSDR-JULIAN-DAYS
183100             PERFORM 900-JULIAN-TO-DATE
183200             MOVE WSDR-FR-DATE       TO HR13F3-HDB-STOP-DATE (I1)
183300         ELSE
183400             IF (HR13F3-HDB-STOP-DATE (I1) NOT < HDB-START-DATE)
183500                 MOVE 124                     TO CRT-ERROR-NBR
183600                 MOVE HR13F3-LINE-FC-FN (I1)  TO CRT-FIELD-NBR
183700                 GO TO 234-END.
183800
183900 234-FIND-NEXT-HDBSET1.
184000
184100     PERFORM 860-FIND-NEXT-HDBSET1.
184200
184300 234-END.
184400
184500******************************************************************
184600 236-EDIT-DELETING-POST-HDB.
184700******************************************************************
184800
184900     IF  (HR13F3-BEN-PLAN-CODE        = HDB-PLAN-CODE)
185000     AND (HR13F3-HDB-START-DATE (I2)  = HDB-START-DATE)
185100     AND (HR13F3-LINE-FC (I2)     = "D")
185200         MOVE WS-TRUE            TO HR13WS-DEL-HDB-SW.
185300
185400 236-END.
185500
185600******************************************************************
185700 400-PROCESS-TRAN.
185800******************************************************************
185900
186000     IF (HR13F3-FC = "I" OR "+" OR "-")
186100         PERFORM 480-INQUIRE
186200         THRU    480-END.
186300
186400     IF (HR13F3-FC = "C")
186500
186600         PERFORM 910-AUDIT-BEGIN
186700
186800         IF (DMS-ABORTED)
186900             GO TO 400-END
187000         END-IF
187100
187200         PERFORM 420-PROCESS-DETAIL
187300         THRU    420-END
187400             VARYING I1 FROM 1 BY 1
187500             UNTIL  (I1 > 10)
187600
187700         PERFORM 920-AUDIT-END
187800
187900     END-IF.
188000
188100     IF (HR13F3-FC = "C")
188200         MOVE SPACES                 TO HR13F3-FC 
188300         MOVE CRT-CHG-COMPLETE       TO CRT-MESSAGE.
188400
188500 400-END.
188600
188700******************************************************************
188800 420-PROCESS-DETAIL.          
188900******************************************************************
189000
189100     IF (HR13F3-LINE-FC (I1) = SPACES)
189200         GO TO 420-END.

J10377     MOVE ZEROS                 TO HRHDB-PARTICIPNT.
189400     PERFORM 520-MOVE-DTL-SCR-TO-WS
189500     THRU    520-END.
189600
189700     PERFORM 3000-HRHDB-PROCESS-TRAN.
189800
189900     IF (HR13F3-LINE-FC (I1) = "A" OR "C")
190000         PERFORM 530-MOVE-DTL-WS-TO-SCR
190100         THRU    530-END
190200         INITIALIZE HR13F3-LINE-FC (I1)

               MOVE PLN-CREATE-TRANS   TO HR13F3-BNT-CREATE-TRANS (I1)

015500         INITIALIZE HR13F3-BNT-REASON (I1)
015500                    HR13F3-BNT-MEMBER-ID (I1)
190300     ELSE
190400         INITIALIZE HR13F3-DETAIL-LINE (I1).
190500
190600 420-END.
190700
190800******************************************************************
190900 480-INQUIRE.
191000******************************************************************
191100
191200     IF (HR13F3-FC = "I")
191300         MOVE "+"                TO HR13F3-FC.
191400
           INITIALIZE                  HR13WS-SAVED-DATA.

191500     IF (HR13F3-FC = "-")
191600         MOVE 11                 TO I1 
191700     ELSE
191800         MOVE ZEROS              TO I1.
191900
192000     IF (HR13F3-FC = "-")
192100         MOVE HR13F3-HDB-DEPENDENT (1) TO HR13F3-PT-HDB-DEPENDENT
192200         MOVE HR13F3-HDB-EMP-START (1) TO HR13F3-PT-HDB-EMP-START
192300         MOVE HR13F3-HDB-START-DATE(1) TO HR13F3-PT-HDB-START-DATE
192400         MOVE HR13F3-RECORD-ORIG (1)   TO HR13F3-PT-RECORD-ORIG
192500         INITIALIZE HR13F3-DETAIL-GROUP
192600         IF (HR13WS-RECORD-ORIG = "B")
192700             PERFORM 610-MOVE-EMD-TO-SCREEN
192800             THRU    610-END
192900                 UNTIL  (I1            = 1)
193000                 OR     (EMDEPEND-NOTFOUND)
193100                 OR     (EMD-COMPANY    NOT = DB-COMPANY)
193200                 OR     (EMD-EMPLOYEE   NOT = DB-EMPLOYEE)  
193300             MOVE CRT-MORE-RECS              TO CRT-MESSAGE
193400             IF (EMDEPEND-NOTFOUND)
193500             OR (EMD-COMPANY   NOT = DB-COMPANY)
193600             OR (EMD-EMPLOYEE  NOT = DB-EMPLOYEE)
193700                 MOVE WS-HIGH-VALUES TO DB-DEPENDENT
193800                 MOVE WS-HIGH-VALUES TO DB-START-DATE
193900                 PERFORM 850-FIND-NLT-HDBSET3
194000                 PERFORM 870-FIND-PREV-HDBSET3
194100                 MOVE "D"            TO HR13WS-RECORD-ORIG.
194200
194300     IF  (HR13F3-FC          = "-")
194400     AND (HR13WS-RECORD-ORIG = "D")
194500     AND (I1               > 1)
194600         PERFORM 600-MOVE-HDB-TO-SCREEN
194700         THRU    600-END
194800             UNTIL (I1 = 1)
194900             OR    (HRDEPBEN-NOTFOUND)
195000             OR    (HDB-COMPANY   NOT = DB-COMPANY) 
195100             OR    (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
195200             OR    (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE) 
195300             OR    (HDB-PLAN-CODE NOT = DB-PLAN-CODE) 
195400             OR    (HDB-EMP-START NOT = DB-EMP-START) 
195500         IF (HRDEPBEN-NOTFOUND)
195600         OR (HDB-COMPANY   NOT = DB-COMPANY) 
195700         OR (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
195800         OR (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE) 
195900         OR (HDB-PLAN-CODE NOT = DB-PLAN-CODE) 
196000         OR (HDB-EMP-START NOT = DB-EMP-START) 
196100             MOVE CRT-INQ-COMPLETE           TO CRT-MESSAGE
196200             IF (HR13F3-RECORD-ORIG (1) = SPACES)
196300                 MOVE "E"        TO HR13F3-PT-RECORD-ORIG
196400             ELSE
196500                 NEXT SENTENCE
196600         ELSE
196700             MOVE CRT-MORE-RECS              TO CRT-MESSAGE.
196800
196900     IF (HR13F3-FC NOT = "-")
197000         INITIALIZE HR13F3-DETAIL-GROUP
197100         IF (HR13WS-RECORD-ORIG = "D")
197200             PERFORM 600-MOVE-HDB-TO-SCREEN
197300             THRU    600-END
197400                 UNTIL  (I1 = 10)                 
197500                 OR     (HRDEPBEN-NOTFOUND)
197600                 OR     (HDB-COMPANY   NOT = DB-COMPANY) 
197700                 OR     (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
197800                 OR     (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE) 
197900                 OR     (HDB-PLAN-CODE NOT = DB-PLAN-CODE) 
198000                 OR     (HDB-EMP-START NOT = DB-EMP-START) 
198100             IF (HRDEPBEN-NOTFOUND)
198200             OR (HDB-COMPANY   NOT = DB-COMPANY) 
198300             OR (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
198400             OR (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE) 
198500             OR (HDB-PLAN-CODE NOT = DB-PLAN-CODE) 
198600             OR (HDB-EMP-START NOT = DB-EMP-START) 
198700                 MOVE ZEROES         TO DB-SEQ-NBR   
198800                 PERFORM 850-FIND-NLT-EMDSET1
198900                 MOVE "B"            TO HR13WS-RECORD-ORIG
199000             ELSE
199100                 MOVE "D"            TO HR13F3-PT-RECORD-ORIG
199200                 MOVE HDB-DEPENDENT  TO HR13F3-PT-HDB-DEPENDENT
199300                 MOVE HDB-START-DATE TO HR13F3-PT-HDB-START-DATE
P35671                 MOVE HDB-EMP-START  TO HR13F3-PT-HDB-EMP-START
199400                 MOVE CRT-MORE-RECS          TO CRT-MESSAGE.
199500
199600     IF  (HR13F3-FC          NOT = "-")
199700     AND (HR13WS-RECORD-ORIG = "B")
199800     AND (I1               NOT > 10)                   
199900         PERFORM 610-MOVE-EMD-TO-SCREEN
200000         THRU    610-END
200100             UNTIL (I1 = 10)              
200200             OR    (EMDEPEND-NOTFOUND)
200300             OR    (EMD-COMPANY   NOT = DB-COMPANY)
200400             OR    (EMD-EMPLOYEE  NOT = DB-EMPLOYEE)
200500         IF (EMDEPEND-NOTFOUND)
200600         OR (EMD-COMPANY   NOT = DB-COMPANY)
200700         OR (EMD-EMPLOYEE  NOT = DB-EMPLOYEE)
200800             MOVE "E"            TO HR13F3-PT-RECORD-ORIG
200900             MOVE ZEROS          TO HR13F3-PT-HDB-DEPENDENT
201000             MOVE ZEROS          TO HR13F3-PT-HDB-START-DATE    
201100             MOVE CRT-INQ-COMPLETE           TO CRT-MESSAGE
201200             IF (HR13F3-HDB-DEPENDENT (1) = ZEROS)
201300                 MOVE WS-HIGH-VALUES TO HR13F3-PT-HDB-DEPENDENT
201400                 MOVE "B"            TO HR13F3-PT-RECORD-ORIG
201500             ELSE
201600                 NEXT SENTENCE
201700         ELSE
201800             MOVE "B"            TO HR13F3-PT-RECORD-ORIG
201900             MOVE EMD-SEQ-NBR    TO HR13F3-PT-HDB-DEPENDENT
202000             MOVE CRT-MORE-RECS              TO CRT-MESSAGE.
202100
202200 480-END.
202300
202400******************************************************************
202500 520-MOVE-DTL-SCR-TO-WS.
202600******************************************************************
202700
202800     INITIALIZE HRHDB-SCR-FIELDS.    
202900
203000     MOVE HR13F3-BEN-COMPANY         TO HRHDB-COMPANY.   
203100     MOVE HR13F3-BEN-COMPANY-FN      TO HRHDB-COMPANY-FN. 
203200     MOVE HR13F3-BEN-EMPLOYEE        TO HRHDB-EMPLOYEE. 
203300     MOVE HR13F3-BEN-EMPLOYEE-FN     TO HRHDB-EMPLOYEE-FN.
203400     MOVE HR13F3-BEN-PLAN-TYPE       TO HRHDB-PLAN-TYPE. 
203500     MOVE HR13F3-BEN-PLAN-TYPE-FN    TO HRHDB-PLAN-TYPE-FN.
203600     MOVE HR13F3-BEN-PLAN-CODE       TO HRHDB-PLAN-CODE. 
203700     MOVE HR13F3-BEN-PLAN-CODE-FN    TO HRHDB-PLAN-CODE-FN.
203800     MOVE HR13F3-BEN-START-DATE      TO HRHDB-BEN-START-DATE. 
203900     MOVE HR13F3-BEN-START-DATE-FN   TO HRHDB-BEN-START-DATE-FN.
204000     MOVE HR13F3-BEN-STOP-DATE       TO HRHDB-BEN-STOP-DATE. 
204100     MOVE HR13F3-BEN-STOP-DATE-FN    TO HRHDB-BEN-STOP-DATE-FN.
204200     MOVE HR13F3-LINE-FC (I1)        TO HRHDB-FC.
204300     MOVE HR13F3-LINE-FC-FN (I1)     TO HRHDB-FC-FN.
204400     MOVE HR13F3-HDB-START-DATE (I1) TO HRHDB-START-DATE. 
204500     MOVE HR13F3-HDB-START-DATE-FN (I1)
204600                                     TO HRHDB-START-DATE-FN.
204700     MOVE HR13F3-HDB-STOP-DATE (I1)  TO HRHDB-STOP-DATE. 
204800     MOVE HR13F3-HDB-STOP-DATE-FN (I1)
204900                                     TO HRHDB-STOP-DATE-FN.
205000     MOVE HR13F3-HDB-DEPENDENT (I1)  TO HRHDB-DEPENDENT.
205100     MOVE HR13F3-HDB-DEPENDENT-FN (I1) TO HRHDB-DEPENDENT-FN.
205200     MOVE HR13F3-HDB-EMP-START (I1)  TO HRHDB-EMP-START. 
205300     MOVE HR13F3-HDB-EMP-START-FN (I1)
205400                                     TO HRHDB-EMP-START-FN.
205500
P64667     MOVE HR13F3-HDB-USER-ID (I1)    TO HRHDB-USER-ID.
J10377     MOVE HR13F3-HDB-PARTICIPNT (I1) TO HRHDB-PARTICIPNT.
J10377*     MOVE HR13F3-HDB-PARTICIPNT-FN (I1) 
J10377*                                     TO HRHDB-PARTICIPNT-FN.
205600     MOVE HR13F3-HILIGHT (I1)        TO HRHDB-HILIGHT.   
205700     MOVE "E"                        TO HRHDB-COVER-TYPE.   
205800
015500     MOVE HR13F3-BNT-CREATE-TRANS (I1)   TO HRHDB-CREATE-TRANS.
015600     MOVE HR13F3-BNT-CREATE-TRANS-FN(I1) TO HRHDB-CREATE-TRANS-FN.
015500     MOVE HR13F3-BNT-REASON (I1)         TO HRHDB-REASON.
015600     MOVE HR13F3-BNT-REASON-FN (I1)      TO HRHDB-REASON-FN.
015500     MOVE HR13F3-BNT-MEMBER-ID (I1)      TO HRHDB-MEMBER-ID.
015600     MOVE HR13F3-BNT-MEMBER-ID-FN (I1)   TO HRHDB-MEMBER-ID-FN.

205900 520-END.
206000
206100******************************************************************
206200 530-MOVE-DTL-WS-TO-SCR.
206300******************************************************************
206400
206500     MOVE HRHDB-START-DATE       TO HR13F3-HDB-START-DATE (I1).
206600     MOVE HRHDB-START-DATE-FN    TO HR13F3-HDB-START-DATE-FN (I1).
206700     MOVE HRHDB-STOP-DATE        TO HR13F3-HDB-STOP-DATE (I1).
206800     MOVE HRHDB-STOP-DATE-FN     TO HR13F3-HDB-STOP-DATE-FN (I1).
206900     MOVE HRHDB-DEPENDENT        TO HR13F3-HDB-DEPENDENT (I1).
207000     MOVE HRHDB-DEPENDENT-FN     TO HR13F3-HDB-DEPENDENT-FN (I1).
207100     MOVE HRHDB-PLAN-TYPE        TO HR13F3-BEN-PLAN-TYPE.       
207200     MOVE HRHDB-PLAN-TYPE-FN     TO HR13F3-BEN-PLAN-TYPE-FN.      
207300     MOVE HRHDB-PLAN-CODE        TO HR13F3-BEN-PLAN-CODE.       
207400     MOVE HRHDB-PLAN-CODE-FN     TO HR13F3-BEN-PLAN-CODE-FN.     
207500     MOVE HRHDB-BEN-START-DATE   TO HR13F3-BEN-START-DATE.       
207600     MOVE HRHDB-BEN-START-DATE-FN 
207700                                 TO HR13F3-BEN-START-DATE-FN.    
207800     MOVE HRHDB-BEN-STOP-DATE    TO HR13F3-BEN-STOP-DATE.     
207900     MOVE HRHDB-BEN-STOP-DATE-FN TO HR13F3-BEN-STOP-DATE-FN.     
208000     MOVE HRHDB-EMP-START        TO HR13F3-HDB-EMP-START (I1). 
208100     MOVE HRHDB-EMP-START-FN     TO HR13F3-HDB-EMP-START-FN (I1). 
208200
208300     MOVE HRHDB-HILIGHT          TO HR13F3-HILIGHT (I1).   
208400
           MOVE HRHDB-CREATE-TRANS     TO HR13F3-BNT-CREATE-TRANS (I1).
           MOVE HRHDB-REASON           TO HR13F3-BNT-REASON (I1).
           MOVE HRHDB-MEMBER-ID        TO HR13F3-BNT-MEMBER-ID (I1).

J10377     MOVE HRHDB-PARTICIPNT       TO HR13F3-HDB-PARTICIPNT (I1).           

208500 530-END.
208600
208700******************************************************************
208800 600-MOVE-HDB-TO-SCREEN.
208900******************************************************************
209000
209100     IF (HDB-COMPANY    NOT = DB-COMPANY)
209200     OR (HDB-EMPLOYEE   NOT = DB-EMPLOYEE) 
209300     OR (HDB-PLAN-TYPE  NOT = DB-PLAN-TYPE) 
209400     OR (HDB-PLAN-CODE  NOT = DB-PLAN-CODE) 
209500     OR (HDB-EMP-START  NOT = DB-EMP-START)
209600        GO TO 600-FIND-NEXT-HRDEPBEN.
209700
209800     IF (HR13F3-FC = "-")
209900         SUBTRACT 1              FROM I1
210000     ELSE
210100         ADD 1                   TO I1.
210200
210300     MOVE CRT-HILITE             TO HR13F3-HILIGHT (I1)
                                          HR13WS-SAVED-HILITE (I1).
210400
210500     MOVE "D"                    TO HR13F3-RECORD-ORIG (I1).
210600     MOVE HDB-START-DATE         TO HR13F3-HDB-START-DATE (I1)
                                          HR13WS-SAVED-START-DATE (I1). 
210700     MOVE HDB-STOP-DATE          TO HR13F3-HDB-STOP-DATE (I1).
210800     MOVE HDB-DEPENDENT          TO HR13F3-HDB-DEPENDENT (I1)
                                          HR13WS-SAVED-DEPENDENT (I1).
210900     MOVE HDB-EMP-START          TO HR13F3-HDB-EMP-START (I1).
J10377     MOVE HDB-PARTICIPNT         TO HR13F3-HDB-PARTICIPNT (I1).
           MOVE HDB-PLAN-TYPE          TO HR13WS-SAVED-PLAN-TYPE (I1).
           MOVE HDB-PLAN-CODE          TO HR13WS-SAVED-PLAN-CODE (I1).
211000     
211100     MOVE HDB-COMPANY                TO DB-COMPANY.
211200     MOVE HDB-EMPLOYEE               TO DB-EMPLOYEE.
211300     MOVE HDB-DEPENDENT              TO DB-SEQ-NBR. 
211400     PERFORM 840-FIND-EMDSET1.
211500     IF (EMDEPEND-FOUND)
211600         MOVE EMD-SEQ-NBR        TO HR13F3-HDB-DEPENDENT (I1)
211700         MOVE EMD-DEP-TYPE       TO HR13F3-EMD-DEP-TYPE (I1)
211800         MOVE EMD-REL-CODE       TO HR13F3-EMD-REL-CODE (I1)
211900         MOVE EMD-STUDENT        TO HR13F3-EMD-STUDENT (I1)
212000         MOVE EMD-DISABLED       TO HR13F3-EMD-DISABLED (I1)
212100     ELSE
J10377         INITIALIZE                 HR13F3-HDB-PARTICIPNT (I1)
212200         MOVE ZEROES             TO HR13F3-HDB-DEPENDENT (I1)
212300         MOVE SPACES             TO HR13F3-EMD-DEP-TYPE (I1)
212400                                    HR13F3-EMD-REL-CODE (I1)
212500                                    HR13F3-EMD-STUDENT (I1)
212600                                    HR13F3-EMD-DISABLED (I1).
212700
212800     MOVE EMD-LAST-NAME          TO HRWS-LAST-NAME.
212900     MOVE EMD-FIRST-NAME         TO HRWS-FIRST-NAME.
213000     MOVE EMD-MIDDLE-INIT        TO HRWS-MIDDLE-INIT.
213100     PERFORM 750-HR-FORMAT-NAME.
213200     MOVE HRWS-FORMAT-NAME       TO HR13F3-EMD-FULL-NAME (I1).
213300
           MOVE PLN-CREATE-TRANS       TO HR13F3-BNT-CREATE-TRANS (I1).
           IF (PLN-CREATE-TRANS        = "Y")
195200         MOVE 106                TO CRT-MSG-NBR
091500         MOVE "HRCOM"            TO CRT-ERROR-CAT
091700         PERFORM 790-GET-MSG
091800         MOVE CRT-MESSAGE        TO HR13F3-HIPAA (I1).

213400 600-FIND-NEXT-HRDEPBEN.
213500
213600     IF (HR13F3-FC = "-")
213700         PERFORM 870-FIND-PREV-HDBSET3
213800     ELSE
213900         PERFORM 860-FIND-NEXT-HDBSET3.
214000
214100 600-END.
214200
214300******************************************************************
214400 610-MOVE-EMD-TO-SCREEN.
214500******************************************************************
214600
           IF (EMD-ACTIVE-FLAG             = "I")
215900         GO TO 610-FIND-NEXT-EMDEPEND.

214700     MOVE HR13F3-BEN-COMPANY         TO DB-COMPANY.
214800     MOVE HR13F3-BEN-PLAN-TYPE       TO DB-PLAN-TYPE.
214900     MOVE HR13F3-BEN-PLAN-CODE       TO DB-PLAN-CODE.
215000     PERFORM 840-FIND-PLNSET1.
215100     IF (PLAN-NOTFOUND)
215200         GO TO 610-FIND-NEXT-EMDEPEND.
215300
215400     MOVE HR13F3-BEN-EMPLOYEE        TO DB-EMPLOYEE.
215500     MOVE HR13F3-BEN-START-DATE      TO DB-START-DATE.
215600     PERFORM 840-FIND-BENSET1.
215700
215800     IF (PLN-COVERAGE-TYPE           = "0")
215900         GO TO 610-FIND-NEXT-EMDEPEND.
216000
           MOVE "E"                        TO HRDEP-COVER-TYPE.
           INITIALIZE HRDEP-CALC-AGE-DATE.
           PERFORM 5110-FIND-DEP-N-STUD-AGE.
           IF (ERROR-FOUND)
               GO TO 610-FIND-NEXT-EMDEPEND
           ELSE
118400         MOVE HRDEP-DEP-AGE          TO HR13WS-DEP-AGE
118500         MOVE HRDEP-STUD-AGE         TO HR13WS-STUDENT-AGE
118600         MOVE HRDEP-COV-DEPS         TO HR13WS-COV-DEPENDENTS
               MOVE HRDEP-PT-STUDENTS      TO HR13WS-PT-STUDENTS
118600         MOVE HRDEP-TERM-OPT         TO HR13WS-TERM-OPT.
217900
J07540* (E)MPLOYEE, (S)POUSE, (P)ARTNER, SPOUSE (O)R PARTNER
218000     IF  (EMD-DEP-TYPE               = "D")
J07540     AND (HR13WS-COV-DEPENDENTS      = "E" OR "S" OR "P" OR "O")
218200         GO TO 610-FIND-NEXT-EMDEPEND.
218300
J45689* SEPERATING SPOUSE AND DOM PARTNER ELIGIBILITY CRITERIA
J45689*
J07540* (E)MPLOYEE, (D)EPENDENTS, PA(R)TNER DEPS
J07540*    IF  (EMD-DEP-TYPE               = "S" OR "P") 
J07540*    AND (HR13WS-COV-DEPENDENTS      = "E" OR "D" OR "R")
218600*        GO TO 610-FIND-NEXT-EMDEPEND.
J45689*
J45689* (E)MPLOYEE, (D)EPENDENTS, PA(R)TNER DEPS, (P)ARTNER,
J45689* (C)PARTNER AND DEPENDENTS
J45689     IF  (EMD-DEP-TYPE               = "S")
J45689     AND (HR13WS-COV-DEPENDENTS      = "E" OR "D" OR "R"
J45689                                    OR "P" OR "C")
J45689         GO TO 610-FIND-NEXT-EMDEPEND
J45689     END-IF.
J45689*
J45689* (E)MPLOYEE, (D)EPENDENTS, PA(R)TNER DEPS, (B)SPOUSE & DEPENDENTS
J45689* (S)POUSE
J45689     IF  (EMD-DEP-TYPE               = "P")
J45689     AND (HR13WS-COV-DEPENDENTS      = "E" OR "D" OR "R"
J45689                                    OR "B" OR "S")
J45689         GO TO 610-FIND-NEXT-EMDEPEND
J45689     END-IF.
218700
218800     IF (HR13F3-FC = "-")
218900         SUBTRACT 1              FROM I1
219000     ELSE
219100         ADD 1                   TO I1.
219200
219300     MOVE "B"                        TO HR13F3-RECORD-ORIG (I1).
219400
J07540     IF (EMD-DEP-TYPE = "S" OR "P")
219600         MOVE HR13F3-BEN-START-DATE  TO HR13F3-HDB-START-DATE (I1)
219700         MOVE HR13F3-BEN-STOP-DATE   TO HR13F3-HDB-STOP-DATE (I1).
219800
219900     IF (EMD-DEP-TYPE = "D")
220000         IF (HR13F3-BEN-START-DATE >= EMD-BIRTHDATE)
220100             MOVE HR13F3-BEN-START-DATE 
220200                                     TO HR13F3-HDB-START-DATE (I1)
220300         ELSE
220400             MOVE EMD-BIRTHDATE  TO HR13F3-HDB-START-DATE (I1)
220500         END-IF
220600     END-IF.
220700
           COMPUTE I4 = (I1 - 1).
           PERFORM
               UNTIL   (I4  = 0)
               OR     ((HR13WS-SAVED-PLAN-TYPE (I4) = BEN-PLAN-TYPE)
               AND     (HR13WS-SAVED-PLAN-CODE (I4) = BEN-PLAN-CODE)
               AND     (HR13WS-SAVED-DEPENDENT (I4) = EMD-SEQ-NBR))
                       SUBTRACT 1 FROM I4
            END-PERFORM.

           IF  (I4 NOT = ZEROS)
           AND (HR13WS-SAVED-HILITE (I4)    = "*")
           AND (HR13WS-SAVED-PLAN-TYPE (I4) = BEN-PLAN-TYPE)
           AND (HR13WS-SAVED-PLAN-CODE (I4) = BEN-PLAN-CODE)
           AND (HR13WS-SAVED-DEPENDENT (I4) = EMD-SEQ-NBR)
               MOVE HR13WS-SAVED-START-DATE (I4)
                                       TO HR13F3-HDB-START-DATE (I1).

J07540     IF (EMD-DEP-TYPE            = "S" OR "P")
           OR (EMD-DISABLED            = "Y")
               CONTINUE
           ELSE
               INITIALIZE HRDEP-CALC-DEP-N-STUD
               PERFORM 5200-DEP-AGE-DATE-CALC

               MOVE HRDEP-DEP-END-DATE     TO HR13WS-DEP-END-DATE

221800         IF (HR13WS-DEP-END-DATE     < HR13F3-BEN-START-DATE)
221900             INITIALIZE HR13F3-HDB-START-DATE (I1)
222000                        HR13F3-HDB-STOP-DATE (I1)
222100         ELSE
222200         IF (HR13WS-DEP-END-DATE     NOT < HR13F3-BEN-START-DATE)
222300             IF (HR13WS-DEP-END-DATE < HR13F3-BEN-STOP-DATE)
222400                 MOVE HR13WS-DEP-END-DATE 
222500                                     TO HR13F3-HDB-STOP-DATE (I1)
222600             ELSE
222700             IF (HR13F3-BEN-STOP-DATE NOT = ZEROES)
222800                 MOVE HR13F3-BEN-STOP-DATE 
222900                                     TO HR13F3-HDB-STOP-DATE (I1)
223000             ELSE
223100                 MOVE HR13WS-DEP-END-DATE  
223200                                     TO HR13F3-HDB-STOP-DATE (I1).
226100
226200     IF  (EMD-DEP-TYPE = "D")
226300     AND (EMD-DISABLED = "Y")
226400         MOVE HR13F3-BEN-STOP-DATE   TO HR13F3-HDB-STOP-DATE (I1).
226500
226600     MOVE HR13F3-BEN-COMPANY         TO DB-COMPANY. 
226700     MOVE HR13F3-BEN-EMPLOYEE        TO DB-EMPLOYEE. 
226800     MOVE EMD-SEQ-NBR                TO DB-DEPENDENT. 
226900     MOVE HR13F3-BEN-PLAN-TYPE       TO DB-PLAN-TYPE. 
227000     MOVE HR13F3-BEN-PLAN-CODE       TO DB-PLAN-CODE. 
227100     MOVE HR13F3-BEN-START-DATE      TO DB-EMP-START.
227200     MOVE HR13F3-HDB-START-DATE (I1) TO DB-START-DATE
                                              HRDEP-HDB-START-DATE.
312700     MOVE HR13F3-HDB-STOP-DATE (I1)  TO HRDEP-HDB-STOP-DATE.
           MOVE BEN-STOP-DATE              TO HRDEP-BEN-STOP-DATE.
           PERFORM 5300-GET-NEW-HDB-START-N-STOP.

           MOVE HRDEP-HDB-START-DATE   TO HR13F3-HDB-START-DATE (I1).
           MOVE HRDEP-HDB-STOP-DATE    TO HR13F3-HDB-STOP-DATE (I1).

228400     MOVE HR13F3-BEN-START-DATE      TO HR13F3-HDB-EMP-START (I1).
228500
228600     MOVE EMD-SEQ-NBR        TO HR13F3-HDB-DEPENDENT (I1).
228700     MOVE EMD-DEP-TYPE       TO HR13F3-EMD-DEP-TYPE (I1).
228800     MOVE EMD-REL-CODE       TO HR13F3-EMD-REL-CODE (I1).
228900     MOVE EMD-STUDENT        TO HR13F3-EMD-STUDENT (I1).
229000     MOVE EMD-DISABLED       TO HR13F3-EMD-DISABLED (I1).
229100
229200     MOVE EMD-LAST-NAME          TO HRWS-LAST-NAME.
229300     MOVE EMD-FIRST-NAME         TO HRWS-FIRST-NAME.
229400     MOVE EMD-MIDDLE-INIT        TO HRWS-MIDDLE-INIT.
229500     PERFORM 750-HR-FORMAT-NAME.
229600     MOVE HRWS-FORMAT-NAME       TO HR13F3-EMD-FULL-NAME (I1).
229700
           MOVE PLN-CREATE-TRANS       TO HR13F3-BNT-CREATE-TRANS (I1).
           IF (PLN-CREATE-TRANS        = "Y")
195200         MOVE 106                TO CRT-MSG-NBR
091500         MOVE "HRCOM"            TO CRT-ERROR-CAT
091700         PERFORM 790-GET-MSG
091800         MOVE CRT-MESSAGE        TO HR13F3-HIPAA (I1).

229800 610-FIND-NEXT-EMDEPEND.
229900
230000     IF (HR13F3-FC = "-")
230100         PERFORM 870-FIND-PREV-EMDSET1
230200     ELSE
230300         PERFORM 860-FIND-NEXT-EMDSET1.
230400
230500 610-END.             
230600
230700******************************************************************
230800 HR13S3-TRANSACTION-END.
230900******************************************************************
231000******************************************************************
231100 HR13S4-TRANSACTION              SECTION 41.
231200******************************************************************
231300 HR13S4-START.
231400
171600     MOVE CRT-SCREEN-CODE        TO HR13F4-SCREEN-CODE.

231500     PERFORM 200-EDIT-TRAN
231600     THRU    200-END.
231700
231800     IF (NO-ERROR-FOUND)
231900         PERFORM 400-PROCESS-TRAN
232000         THRU    400-END.
232100
232200     GO TO HR13S4-TRANSACTION-END.
232300
232400******************************************************************
232500 200-EDIT-TRAN.
232600******************************************************************
232700
232800     MOVE HR13F4-PTB-COMPANY         TO DB-COMPANY.
232900     MOVE HR13F4-PTB-PLAN-TYPE       TO DB-PLAN-TYPE.
233000     MOVE HR13F4-PTB-PLAN-CODE       TO DB-PLAN-CODE.
233100     PERFORM 840-FIND-PLNSET1.
233200     MOVE PLN-DESC               TO HR13F4-PLN-DESC.
233300
233400     MOVE HR13F4-PTB-START-DATE  TO HR13F4-PTB-START.
233500     MOVE HR13F4-PTB-STOP-DATE   TO HR13F4-PTB-STOP.
233600
233700     MOVE HR13F4-PTB-COMPANY         TO DB-COMPANY. 
233800     PERFORM 840-FIND-BNCSET1. 
233900     IF  (HR13F4-PTB-PLAN-TYPE = "HL")
234000     AND (BNC-DEP-HEALTH   = "N") 
234100         MOVE 125                            TO CRT-ERROR-NBR
234200         GO TO 200-END
234300     END-IF.
234400     IF  (HR13F4-PTB-PLAN-TYPE = "DN")
234500     AND (BNC-DEP-DENTAL   = "N") 
234600         MOVE 125                            TO CRT-ERROR-NBR
234700         GO TO 200-END
234800     END-IF.
234900     IF  (HR13F4-PTB-PLAN-TYPE = "DL")
235000     AND (BNC-DEP-DEP-LIFE = "N") 
235100         MOVE 125                            TO CRT-ERROR-NBR
235200         GO TO 200-END
235300     END-IF.
235400
235500     IF (HR13F4-PTB-PLAN-TYPE = "HL" OR "DN" OR "DL") 
235600         IF (HR13F4-PTB-PARTICIPNT = ZEROES)
235700             MOVE HR13F4-PTB-COMPANY             TO DB-COMPANY
235800             MOVE HR13F4-PTB-EMPLOYEE            TO DB-EMPLOYEE
235900             MOVE ZEROES                         TO DB-PARTICIPNT
236000             MOVE HR13F4-PTB-PLAN-TYPE           TO DB-PLAN-TYPE
236100             MOVE HR13F4-PTB-PLAN-CODE           TO DB-PLAN-CODE
236200             MOVE HR13F4-PTB-START-DATE          TO DB-START-DATE
236300         ELSE
236400             MOVE HR13F4-PTB-COMPANY             TO DB-COMPANY
236500*             MOVE HR13F4-PTB-EMPLOYEE            TO DB-EMPLOYEE
236600             MOVE ZEROES                         TO DB-EMPLOYEE
236700             MOVE HR13F4-PTB-PARTICIPNT          TO DB-PARTICIPNT
236800             MOVE HR13F4-PTB-PLAN-TYPE           TO DB-PLAN-TYPE
236900             MOVE HR13F4-PTB-PLAN-CODE           TO DB-PLAN-CODE
237000             MOVE HR13F4-PTB-START-DATE          TO DB-START-DATE
237100         END-IF
237200
237300         PERFORM 840-FIND-PTBSET5
237400         IF (PARTBEN-FOUND)
237500             IF (PLN-COVERAGE-TYPE = "1")
237600                 MOVE PTB-COV-OPTION        TO DB-COVERAGE-OPT
237700                 PERFORM 840-FIND-COPSET1
237800             ELSE
237900                 MOVE PTB-COV-UPD-DT        TO DB-START-DATE
238000                 MOVE PTB-COV-GROUP         TO DB-GROUP-NAME
238100                 PERFORM 840-FIND-CVRSET1
238200             END-IF
238300             IF  ((PLN-COVERAGE-TYPE  = "1")
238400             AND  (COP-COV-DEPENDENTS = "N"))
238500             OR  ((PLN-COVERAGE-TYPE  = "2")
J07540             AND  (CVR-LIFE-ADD-FLG   = "E"))
238700                 MOVE 126                    TO CRT-ERROR-NBR
238800                 GO TO 200-END
238900             END-IF
239000         END-IF.
239100
236500     MOVE HR13F4-PTB-EMPLOYEE        TO DB-EMPLOYEE.

239200     PERFORM 210-EDIT-ACCESS
239300     THRU    210-END.
239400
239500     IF (ERROR-FOUND)
239600         GO TO 200-END.
239700
239800     IF (HR13F4-FC = "C")
239900         PERFORM 220-EDIT-DATA
240000         THRU    220-END
240100         GO TO 200-END.
240200
240300 200-END.
240400
240500******************************************************************
240600 210-EDIT-ACCESS.
240700******************************************************************
240800
240900     IF (HR13F4-FC = "C")
241000         GO TO 210-END.
241100
241200     IF  (HR13F4-FC              = "I")
241300     OR  (HR13F4-PTB-COMPANY     NOT = HR13F4-PT-PTB-COMPANY)
241400     OR  (HR13F4-PTB-PLAN-TYPE   NOT = HR13F4-PT-PTB-PLAN-TYPE)
241500     OR  (HR13F4-PTB-PLAN-CODE   NOT = HR13F4-PT-PTB-PLAN-CODE)
241600     OR  (HR13F4-PTB-EMPLOYEE    NOT = HR13F4-PT-PTB-EMPLOYEE)
241700     OR ((HR13F4-FC              = "+")
241800     AND (HR13F4-PT-RECORD-ORIG  = "E"))
241900         MOVE "D"                         TO HR13WS-RECORD-ORIG
242000         MOVE HR13F4-PTB-COMPANY          TO DB-COMPANY  
242100         MOVE HR13F4-PTB-EMPLOYEE         TO DB-EMPLOYEE 
242200         MOVE HR13F4-PTB-PLAN-TYPE        TO DB-PLAN-TYPE
242300         MOVE HR13F4-PTB-PLAN-CODE        TO DB-PLAN-CODE
242400         MOVE HR13F4-PTB-START-DATE       TO DB-EMP-START
242500         MOVE ZEROES                      TO DB-DEPENDENT
242600         MOVE ZEROES                      TO DB-START-DATE
242700     ELSE
242800     IF (HR13F4-FC = "+")
242900         MOVE HR13F4-PT-HDB-DEPENDENT     TO DB-DEPENDENT
243000         MOVE HR13F4-PT-HDB-DEPENDENT     TO DB-SEQ-NBR  
243100         MOVE HR13F4-PT-HDB-EMP-START     TO DB-EMP-START
243200         MOVE HR13F4-PT-HDB-START-DATE    TO DB-START-DATE
243300         MOVE HR13F4-PT-RECORD-ORIG       TO HR13WS-RECORD-ORIG
243400     ELSE
243500         MOVE HR13F4-HDB-DEPENDENT (1)    TO DB-DEPENDENT
243600         MOVE HR13F4-HDB-DEPENDENT (1)    TO DB-SEQ-NBR  
243700         MOVE HR13F4-HDB-EMP-START (1)    TO DB-EMP-START
243800         MOVE HR13F4-HDB-START-DATE (1)   TO DB-START-DATE    
243900         MOVE HR13F4-RECORD-ORIG (1)      TO HR13WS-RECORD-ORIG.
244000
244100     IF (HR13WS-RECORD-ORIG = "D")
244200         PERFORM 850-FIND-NLT-HDBSET3
244300         IF (HR13F4-FC    = "-")
244400             PERFORM 870-FIND-PREV-HDBSET3
244500             IF (HRDEPBEN-NOTFOUND)
244600             OR (HDB-COMPANY     NOT = DB-COMPANY)
244700             OR (HDB-EMPLOYEE    NOT = DB-EMPLOYEE)
244800             OR (HDB-PLAN-TYPE   NOT = DB-PLAN-TYPE)
244900             OR (HDB-PLAN-CODE   NOT = DB-PLAN-CODE)
245000             OR (HDB-EMP-START   NOT = DB-EMP-START)
245100                 MOVE 151                     TO CRT-ERROR-NBR
245200                 MOVE HR13F4-FC-FN            TO CRT-FIELD-NBR
245300                 GO TO 210-END.
245400
245500     IF   (HR13WS-RECORD-ORIG = "D")
245600     AND ((HRDEPBEN-NOTFOUND)
245700     OR   (HDB-COMPANY      NOT = DB-COMPANY)
245800     OR   (HDB-EMPLOYEE     NOT = DB-EMPLOYEE) 
245900     OR   (HDB-PLAN-TYPE    NOT = DB-PLAN-TYPE) 
246000     OR   (HDB-PLAN-CODE    NOT = DB-PLAN-CODE)
246100     OR   (HDB-EMP-START    NOT = DB-EMP-START))
246200             MOVE "B"                    TO HR13WS-RECORD-ORIG
246300             MOVE HR13F4-PTB-COMPANY     TO DB-COMPANY   
246400             MOVE HR13F4-PTB-EMPLOYEE    TO DB-EMPLOYEE 
246500             MOVE ZEROES                 TO DB-SEQ-NBR.   
246600
246700     IF (HR13WS-RECORD-ORIG = "B")
246800         PERFORM 850-FIND-NLT-EMDSET1
246900         IF (HR13F4-FC    = "-")
247000             PERFORM 870-FIND-PREV-EMDSET1
247100             IF (EMDEPEND-NOTFOUND)
247200             OR (EMD-COMPANY     NOT = DB-COMPANY)
247300             OR (EMD-EMPLOYEE    NOT = DB-EMPLOYEE)
247400                 MOVE WS-HIGH-VALUES      TO DB-SEQ-NBR   
247500                 PERFORM 850-FIND-NLT-EMDSET1
247600                 PERFORM 870-FIND-PREV-EMDSET1
247700                 MOVE "D"                 TO HR13WS-RECORD-ORIG
247800                 IF (EMDEPEND-NOTFOUND)
247900                 OR (EMD-COMPANY     NOT = DB-COMPANY)
248000                 OR (EMD-EMPLOYEE    NOT = DB-EMPLOYEE)
248100                     MOVE 151                     TO CRT-ERROR-NBR
248200                     MOVE HR13F4-FC-FN            TO CRT-FIELD-NBR
248300                     GO TO 210-END.
248400
248500 210-END.
248600
248700******************************************************************
248800 220-EDIT-DATA.
248900******************************************************************
249000
249100     PERFORM 222-EDIT-DTL-TRAN
249200     THRU    222-END
249300         VARYING I1 FROM 1 BY 1
249400         UNTIL  (I1 > 10)
249500         OR     (ERROR-FOUND).
249600*
249700     IF (ERROR-FOUND)
249800         GO TO 220-END.
249900              
250000 220-END.
250100
250200******************************************************************
250300 222-EDIT-DTL-TRAN.
250400******************************************************************
250500
250600     IF (HR13F4-LINE-FC (I1) = SPACES)
250700         GO TO 222-END.
250800
250900     PERFORM 520-MOVE-DTL-SCR-TO-WS
251000     THRU    520-END.
251100
251200     PERFORM 2000-HRHDB-EDIT-TRAN.
251300
251400     PERFORM 530-MOVE-DTL-WS-TO-SCR
251500     THRU    530-END.
251600
251700     IF (ERROR-FOUND)
251800         GO TO 222-END.
251900
252000     IF (HR13F4-LINE-FC (I1) = "A")
252100         MOVE HR13F4-PTB-COMPANY         TO DB-COMPANY  
252200         MOVE HR13F4-PTB-EMPLOYEE        TO DB-EMPLOYEE  
252300         MOVE HR13F4-HDB-DEPENDENT (I1)  TO DB-DEPENDENT  
252400         MOVE HR13F4-PTB-PLAN-TYPE       TO DB-PLAN-TYPE  
252500         MOVE HR13F4-PTB-PLAN-CODE       TO DB-PLAN-CODE  
252600         MOVE HR13F4-PTB-START-DATE      TO DB-EMP-START 
252700         MOVE HR13F4-HDB-START-DATE (I1) TO DB-START-DATE 
252800         PERFORM 850-FIND-NLT-HDBSET1
253600         PERFORM 870-FIND-PREV-HDBSET1
253700         IF  (HRDEPBEN-FOUND)
253800         AND (HDB-COMPANY   = DB-COMPANY)
253900         AND (HDB-EMPLOYEE  = DB-EMPLOYEE)
254000         AND (HDB-DEPENDENT = DB-DEPENDENT)
254100         AND (HDB-PLAN-TYPE = DB-PLAN-TYPE)
254200         AND (HDB-PLAN-CODE = DB-PLAN-CODE)
254300         AND (HDB-EMP-START = DB-EMP-START)
254500             PERFORM 230-EDIT-EXISTING-PREV-HDB
254600             THRU    230-END
254800         END-IF
254900     END-IF.
255000     IF (ERROR-FOUND)
255100         GO TO 222-END.
255200
255300     MOVE HR13F4-PTB-COMPANY         TO DB-COMPANY.   
255400     MOVE HR13F4-PTB-EMPLOYEE        TO DB-EMPLOYEE. 
255500     MOVE HR13F4-HDB-DEPENDENT (I1)  TO DB-DEPENDENT.  
255600     MOVE HR13F4-PTB-PLAN-TYPE       TO DB-PLAN-TYPE.  
255700     MOVE HR13F4-PTB-PLAN-CODE       TO DB-PLAN-CODE.  
255800     MOVE HR13F4-PTB-START-DATE      TO DB-EMP-START. 
255900     MOVE HR13F4-HDB-START-DATE (I1) TO DB-START-DATE. 
256000     PERFORM 850-FIND-NLT-HDBSET1.
256100     IF (HRDEPBEN-NOTFOUND)
256200     OR (HDB-COMPANY   NOT = DB-COMPANY)
256300     OR (HDB-EMPLOYEE  NOT = DB-EMPLOYEE)
256400     OR (HDB-DEPENDENT NOT = DB-DEPENDENT)
256500     OR (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE)
256600     OR (HDB-PLAN-CODE NOT = DB-PLAN-CODE)
256700     OR (HDB-EMP-START NOT = DB-EMP-START)
256800         PERFORM 870-FIND-PREV-HDBSET1
256900         IF  (HRDEPBEN-FOUND)
257000         AND (HDB-COMPANY   = DB-COMPANY)
257100         AND (HDB-EMPLOYEE  = DB-EMPLOYEE)
257200         AND (HDB-DEPENDENT = DB-DEPENDENT)
257300         AND (HDB-PLAN-TYPE = DB-PLAN-TYPE)
257400         AND (HDB-PLAN-CODE = DB-PLAN-CODE)
257500         AND (HDB-EMP-START = DB-EMP-START)
257600         AND (HDB-START-DATE = DB-START-DATE)
257700             IF  (HR13F4-LINE-FC (I1)          = "A")
257800             OR ((HR13F4-LINE-FC (I1)         = "C")
257900             AND (HR13F4-HDB-STOP-DATE (I1)   > HDB-STOP-DATE))
258000             OR ((HR13F4-LINE-FC (I1)         = "C")
258100             AND (HR13F4-HDB-STOP-DATE (I1)   NOT = HDB-STOP-DATE)
258200             AND (HR13F4-HDB-STOP-DATE (I1)   = ZEROS))
258300                 MOVE WS-FALSE       TO HR13WS-STOP-DFLT-SW
258400                 PERFORM 234-EDIT-EXISTING-POST-HDB
258500                 THRU    234-END
258600                     UNTIL  (ERROR-FOUND)
258700                     OR     (STOP-DEFAULTED)
258800                     OR     (HRDEPBEN-NOTFOUND)
258900                     OR     (HDB-COMPANY       NOT = DB-COMPANY)
259000                     OR     (HDB-EMPLOYEE      NOT = DB-EMPLOYEE)
                           OR     (HDB-DEPENDENT     NOT = DB-DEPENDENT)
259100                     OR     (HDB-PLAN-TYPE     NOT = DB-PLAN-TYPE)
259200                     OR     (HDB-PLAN-CODE     NOT = DB-PLAN-CODE)
259300                     OR     (HDB-EMP-START     NOT = DB-EMP-START)
259400                     OR   ((HR13F4-HDB-STOP-DATE (I1) NOT = ZEROS)
259500                     AND    (HR13F4-HDB-STOP-DATE (I1) <
259600                                                  HDB-START-DATE))
                   END-IF
               END-IF
           ELSE
           IF  (HRDEPBEN-FOUND)
           AND (HDB-COMPANY   = DB-COMPANY)
           AND (HDB-EMPLOYEE  = DB-EMPLOYEE)
           AND (HDB-DEPENDENT = DB-DEPENDENT)
           AND (HDB-PLAN-TYPE = DB-PLAN-TYPE)
           AND (HDB-PLAN-CODE = DB-PLAN-CODE)
           AND (HDB-EMP-START = DB-EMP-START)
               IF  (HR13F4-LINE-FC (I1)          = "A")
               OR ((HR13F4-LINE-FC (I1)         = "C")
               AND (HR13F4-HDB-STOP-DATE (I1)   > HDB-STOP-DATE))
               OR ((HR13F4-LINE-FC (I1)         = "C")
               AND (HR13F4-HDB-STOP-DATE (I1)   NOT = HDB-STOP-DATE)
               AND (HR13F4-HDB-STOP-DATE (I1)   = ZEROS))
                   MOVE WS-FALSE       TO HR13WS-STOP-DFLT-SW
                   PERFORM 234-EDIT-EXISTING-POST-HDB
                   THRU    234-END
                       UNTIL  (ERROR-FOUND)
                       OR     (STOP-DEFAULTED)
                       OR     (HRDEPBEN-NOTFOUND)
                       OR     (HDB-COMPANY       NOT = DB-COMPANY)
                       OR     (HDB-EMPLOYEE      NOT = DB-EMPLOYEE)
                       OR     (HDB-DEPENDENT     NOT = DB-DEPENDENT)
                       OR     (HDB-PLAN-TYPE     NOT = DB-PLAN-TYPE)
                       OR     (HDB-PLAN-CODE     NOT = DB-PLAN-CODE)
                       OR     (HDB-EMP-START     NOT = DB-EMP-START)
                       OR   ((HR13F4-HDB-STOP-DATE (I1) NOT = ZEROS)
                       AND    (HR13F4-HDB-STOP-DATE (I1) <
                                                    HDB-START-DATE))
               END-IF
           END-IF.
259700     IF (ERROR-FOUND)
259800         GO TO 222-END.

J10377     IF (HR13F4-LINE-FC (I1) = "C" OR "D")
J10377         IF  (HR13F4-PTB-PARTICIPNT NOT = ZEROS)
J10377         AND (HR13F4-HDB-PARTICIPNT (I1) = ZEROS)
J10377*            Use employee benefit entry
J10377             MOVE 128                        TO CRT-ERROR-NBR
J10377             MOVE HR13F4-LINE-FC-FN (I1)     TO CRT-FIELD-NBR
J10377             GO TO 222-END     
J10377         END-IF

J10377         IF   (HR13F4-PTB-PARTICIPNT NOT = ZEROS)
J10377         AND  (HR13F4-HDB-PARTICIPNT (I1) NOT = 
J10377               HR13F4-PTB-PARTICIPNT) 
J10377*            Use COBRA benefit entry
J10377             MOVE 129                        TO CRT-ERROR-NBR
J10377             MOVE HR13F4-HDB-PARTICIPNT (I1) TO CRT-ERR-VAR1
J10377             MOVE HR13F4-LINE-FC-FN (I1)     TO CRT-FIELD-NBR
J10377             GO TO 222-END     
J10377         END-IF

J10377         IF  (HR13F4-PTB-PARTICIPNT = ZEROS)
J10377         AND (HR13F4-HDB-PARTICIPNT (I1) NOT = ZEROS)
J10377*            Use COBRA benefit entry
J10377             MOVE 127                        TO CRT-ERROR-NBR
J10377             MOVE HR13F4-LINE-FC-FN (I1)     TO CRT-FIELD-NBR
J10377             GO TO 222-END
J10377         END-IF
J10377     END-IF.
           
J10377     IF (ERROR-FOUND)
J10377         GO TO 222-END
J10377     END-IF.           
259900
260000 222-END.
260100
260200******************************************************************
260300 230-EDIT-EXISTING-PREV-HDB.
260400******************************************************************
260500
260600     MOVE WS-FALSE               TO HR13WS-DEL-HDB-SW
260700                                    HR13WS-CHG-HDB-SW.
260800
260900     PERFORM 232-EDIT-CHANGING-HDB
261000     THRU    232-END
261100         VARYING I2 FROM 1 BY 1
261200         UNTIL  (I2 = I1)
261300         OR     (DELETING-HDB)
261400         OR     (CHANGING-HDB).
261500
261600     IF  (NOT-CHANGING-HDB)
261700     AND ((HDB-STOP-DATE         NOT = ZEROS)
261800     AND (HDB-STOP-DATE          NOT < HR13F4-HDB-START-DATE (I1))
           OR  (HDB-STOP-DATE          = ZEROS))
261900         MOVE 124                            TO CRT-ERROR-NBR
262000         MOVE HR13F4-LINE-FC-FN (I1)         TO CRT-FIELD-NBR
262100         GO TO 230-END.
262200
262300 230-END.
262400
262500******************************************************************
262600 232-EDIT-CHANGING-HDB.
262700******************************************************************
262800
262900     IF  (HR13F4-LINE-FC (I2)        = "D")
263000     AND (HR13F4-PTB-PLAN-TYPE       = HDB-PLAN-TYPE)
263100     AND (HR13F4-PTB-PLAN-CODE       = HDB-PLAN-CODE)
263200     AND (HR13F4-PTB-START-DATE      = HDB-EMP-START)
263300     AND (HR13F4-HDB-START-DATE (I2) = HDB-START-DATE)
263400         MOVE WS-TRUE                TO HR13WS-DEL-HDB-SW
263500         GO TO 232-END.
263600
263700     IF  (HR13F4-LINE-FC (I2)            = "C")
263800     AND (HR13F4-PTB-PLAN-TYPE           = HDB-PLAN-TYPE)
263900     AND (HR13F4-PTB-PLAN-CODE           = HDB-PLAN-CODE)
264000     AND (HR13F4-PTB-START-DATE          = HDB-EMP-START)
264100     AND (HR13F4-HDB-START-DATE (I2)     = HDB-START-DATE)
264200         IF  (HR13F4-HDB-STOP-DATE (I2)  NOT = ZEROS)
264300         AND (HR13F4-HDB-STOP-DATE (I2)  NOT < 
264400                                       HR13F4-HDB-START-DATE (I1))
264500             MOVE WS-TRUE                    TO HR13WS-CHG-HDB-SW
264600             MOVE 124                        TO CRT-ERROR-NBR
264700             MOVE HR13F4-LINE-FC-FN (I1)     TO CRT-FIELD-NBR
264800             GO TO 232-END
264900         ELSE
265000             MOVE WS-TRUE                TO HR13WS-CHG-HDB-SW
265100             GO TO 232-END.
265200
265300 232-END.
265400
265500******************************************************************
265600 234-EDIT-EXISTING-POST-HDB.
265700******************************************************************
265800
265900     IF (HDB-START-DATE          = HR13F4-HDB-START-DATE (I1))
266000         GO TO 234-FIND-NEXT-HDBSET1.
266100
266200     MOVE WS-FALSE               TO HR13WS-DEL-HDB-SW.
266300
266400     PERFORM 236-EDIT-DELETING-POST-HDB
266500     THRU    236-END
266600         VARYING I2 FROM 1 BY 1
266700         UNTIL  (DELETING-HDB)
266800         OR     (I2 > 10).                     
266900
267000     IF (DELETING-HDB)
267100         GO TO 234-FIND-NEXT-HDBSET1.
267200
267300     IF (NOT-DELETING-HDB)
267400         IF  (HR13F4-HDB-STOP-DATE (I1) = ZEROS)
               AND (HDB-START-DATE > HR13F4-HDB-START-DATE (I1))
267500             MOVE WS-TRUE            TO HR13WS-STOP-DFLT-SW
267600             MOVE HDB-START-DATE     TO WSDR-FR-DATE
267700             PERFORM 900-DATE-TO-JULIAN
267800             SUBTRACT 1              FROM WSDR-JULIAN-DAYS
267900             PERFORM 900-JULIAN-TO-DATE
268000             MOVE WSDR-FR-DATE       TO HR13F4-HDB-STOP-DATE (I1)
268100         ELSE
268200             IF (HR13F4-HDB-STOP-DATE (I1) NOT < HDB-START-DATE)
268300                 MOVE 124                     TO CRT-ERROR-NBR
268400                 MOVE HR13F4-LINE-FC-FN (I1)  TO CRT-FIELD-NBR
268500                 GO TO 234-END.
268600
268700 234-FIND-NEXT-HDBSET1.
268800
268900     PERFORM 860-FIND-NEXT-HDBSET1.
269000
269100 234-END.
269200
269300******************************************************************
269400 236-EDIT-DELETING-POST-HDB.
269500******************************************************************
269600
269700     IF  (HR13F4-PTB-PLAN-CODE        = HDB-PLAN-CODE)
269800     AND (HR13F4-HDB-START-DATE (I2)  = HDB-START-DATE)
269900     AND (HR13F4-LINE-FC (I2)     = "D")
270000         MOVE WS-TRUE            TO HR13WS-DEL-HDB-SW.
270100
270200 236-END.
270300
270400******************************************************************
270500 400-PROCESS-TRAN.
270600******************************************************************
270700
270800     IF (HR13F4-FC = "I" OR "+" OR "-")
270900         PERFORM 480-INQUIRE
271000         THRU    480-END.
271100
271200     IF (HR13F4-FC = "C")
271300
271400         PERFORM 910-AUDIT-BEGIN
271500
271600         IF (DMS-ABORTED)
271700             GO TO 400-END
271800         END-IF
271900
272000         PERFORM 420-PROCESS-DETAIL
272100         THRU    420-END
272200             VARYING I1 FROM 1 BY 1
272300             UNTIL  (I1 > 10)
272400
272500         PERFORM 920-AUDIT-END
272600
272700     END-IF.
272800
272900     IF (HR13F4-FC = "C")
273000         MOVE SPACES                 TO HR13F4-FC 
273100         MOVE CRT-CHG-COMPLETE       TO CRT-MESSAGE.
273200
273300 400-END.
273400
273500******************************************************************
273600 420-PROCESS-DETAIL.          
273700******************************************************************
273800
273900     IF (HR13F4-LINE-FC (I1) = SPACES)
274000         GO TO 420-END.
274100
274200     PERFORM 520-MOVE-DTL-SCR-TO-WS
274300     THRU    520-END.
274400
274500     PERFORM 3000-HRHDB-PROCESS-TRAN.
274600
274700     IF (HR13F4-LINE-FC (I1) = "A" OR "C")
274800         PERFORM 530-MOVE-DTL-WS-TO-SCR
274900         THRU    530-END
275000         INITIALIZE HR13F4-LINE-FC (I1)

               MOVE PLN-CREATE-TRANS   TO HR13F4-BNT-CREATE-TRANS (I1)

015500         INITIALIZE HR13F4-BNT-REASON (I1)
015500                    HR13F4-BNT-MEMBER-ID (I1)
275100     ELSE
275200         INITIALIZE HR13F4-DETAIL-LINE (I1).
275300
275400 420-END.
275500
275600******************************************************************
275700 480-INQUIRE.
275800******************************************************************
275900
276000     IF (HR13F4-FC = "I")
276100         MOVE "+"                TO HR13F4-FC.
276200
           INITIALIZE                  HR13WS-SAVED-DATA.

276300     IF (HR13F4-FC = "-")
276400         MOVE 11                 TO I1 
276500     ELSE
276600         MOVE ZEROS              TO I1.
276700
276800     IF (HR13F4-FC = "-")
276900         MOVE HR13F4-HDB-DEPENDENT (1) TO HR13F4-PT-HDB-DEPENDENT
277000         MOVE HR13F4-HDB-EMP-START (1) TO HR13F4-PT-HDB-EMP-START
277100         MOVE HR13F4-HDB-START-DATE(1) TO HR13F4-PT-HDB-START-DATE
277200         MOVE HR13F4-RECORD-ORIG (1)   TO HR13F4-PT-RECORD-ORIG
277300         INITIALIZE HR13F4-DETAIL-GROUP
277400         IF (HR13WS-RECORD-ORIG = "B")
277500             PERFORM 610-MOVE-EMD-TO-SCREEN
277600             THRU    610-END
277700                 UNTIL  (I1            = 1)
277800                 OR     (EMDEPEND-NOTFOUND)
277900                 OR     (EMD-COMPANY    NOT = DB-COMPANY)
278000                 OR     (EMD-EMPLOYEE   NOT = DB-EMPLOYEE)  
278100             MOVE CRT-MORE-RECS              TO CRT-MESSAGE
278200             IF (EMDEPEND-NOTFOUND)
278300             OR (EMD-COMPANY   NOT = DB-COMPANY)
278400             OR (EMD-EMPLOYEE  NOT = DB-EMPLOYEE)
278500                 MOVE WS-HIGH-VALUES TO DB-DEPENDENT
278600                 MOVE WS-HIGH-VALUES TO DB-START-DATE
278700                 PERFORM 850-FIND-NLT-HDBSET3
278800                 PERFORM 870-FIND-PREV-HDBSET3
278900                 MOVE "D"            TO HR13WS-RECORD-ORIG.
279000
279100     IF  (HR13F4-FC          = "-")
279200     AND (HR13WS-RECORD-ORIG = "D")
279300     AND (I1               > 1)
279400         PERFORM 600-MOVE-HDB-TO-SCREEN
279500         THRU    600-END
279600             UNTIL (I1 = 1)
279700             OR    (HRDEPBEN-NOTFOUND)
279800             OR    (HDB-COMPANY   NOT = DB-COMPANY) 
279900             OR    (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
280000             OR    (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE) 
280100             OR    (HDB-PLAN-CODE NOT = DB-PLAN-CODE) 
280200             OR    (HDB-EMP-START NOT = DB-EMP-START) 
280300         IF (HRDEPBEN-NOTFOUND)
280400         OR (HDB-COMPANY   NOT = DB-COMPANY) 
280500         OR (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
280600         OR (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE) 
280700         OR (HDB-PLAN-CODE NOT = DB-PLAN-CODE) 
280800         OR (HDB-EMP-START NOT = DB-EMP-START) 
280900             MOVE CRT-INQ-COMPLETE           TO CRT-MESSAGE
281000             IF (HR13F4-RECORD-ORIG (1) = SPACES)
281100                 MOVE "E"        TO HR13F4-PT-RECORD-ORIG
281200             ELSE
281300                 NEXT SENTENCE
281400         ELSE
281500             MOVE CRT-MORE-RECS              TO CRT-MESSAGE.
281600
281700     IF (HR13F4-FC NOT = "-")
281800         INITIALIZE HR13F4-DETAIL-GROUP
281900         IF (HR13WS-RECORD-ORIG = "D")
282000             PERFORM 600-MOVE-HDB-TO-SCREEN
282100             THRU    600-END
282200                 UNTIL  (I1 = 10)                 
282300                 OR     (HRDEPBEN-NOTFOUND)
282400                 OR     (HDB-COMPANY   NOT = DB-COMPANY) 
282500                 OR     (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
282600                 OR     (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE) 
282700                 OR     (HDB-PLAN-CODE NOT = DB-PLAN-CODE) 
282800                 OR     (HDB-EMP-START NOT = DB-EMP-START) 
282900             IF (HRDEPBEN-NOTFOUND)
283000             OR (HDB-COMPANY   NOT = DB-COMPANY) 
283100             OR (HDB-EMPLOYEE  NOT = DB-EMPLOYEE) 
283200             OR (HDB-PLAN-TYPE NOT = DB-PLAN-TYPE) 
283300             OR (HDB-PLAN-CODE NOT = DB-PLAN-CODE) 
283400             OR (HDB-EMP-START NOT = DB-EMP-START) 
283500                 MOVE ZEROES         TO DB-SEQ-NBR   
283600                 PERFORM 850-FIND-NLT-EMDSET1
283700                 MOVE "B"            TO HR13WS-RECORD-ORIG
283800             ELSE
283900                 MOVE "D"            TO HR13F4-PT-RECORD-ORIG
284000                 MOVE HDB-DEPENDENT  TO HR13F4-PT-HDB-DEPENDENT
284100                 MOVE HDB-START-DATE TO HR13F4-PT-HDB-START-DATE
284200                 MOVE CRT-MORE-RECS          TO CRT-MESSAGE.
284300
284400     IF  (HR13F4-FC          NOT = "-")
284500     AND (HR13WS-RECORD-ORIG = "B")
284600     AND (I1               NOT > 10)                   
284700         PERFORM 610-MOVE-EMD-TO-SCREEN
284800         THRU    610-END
284900             UNTIL (I1 = 10)              
285000             OR    (EMDEPEND-NOTFOUND)
285100             OR    (EMD-COMPANY   NOT = DB-COMPANY)
285200             OR    (EMD-EMPLOYEE  NOT = DB-EMPLOYEE)
285300         IF (EMDEPEND-NOTFOUND)
285400         OR (EMD-COMPANY   NOT = DB-COMPANY)
285500         OR (EMD-EMPLOYEE  NOT = DB-EMPLOYEE)
285600             MOVE "E"            TO HR13F4-PT-RECORD-ORIG
285700             MOVE ZEROS          TO HR13F4-PT-HDB-DEPENDENT
285800             MOVE ZEROS          TO HR13F4-PT-HDB-START-DATE    
285900             MOVE CRT-INQ-COMPLETE           TO CRT-MESSAGE
286000             IF (HR13F4-HDB-DEPENDENT (1) = ZEROS)
286100                 MOVE WS-HIGH-VALUES TO HR13F4-PT-HDB-DEPENDENT
286200                 MOVE "B"            TO HR13F4-PT-RECORD-ORIG
286300             ELSE
286400                 NEXT SENTENCE
286500         ELSE
286600             MOVE "B"            TO HR13F4-PT-RECORD-ORIG
286700             MOVE EMD-SEQ-NBR    TO HR13F4-PT-HDB-DEPENDENT
286800             MOVE CRT-MORE-RECS              TO CRT-MESSAGE.
286900
287000 480-END.
287100
287200******************************************************************
287300 520-MOVE-DTL-SCR-TO-WS.
287400******************************************************************
287500
287600     INITIALIZE HRHDB-SCR-FIELDS.    
287700
287800     MOVE HR13F4-PTB-COMPANY         TO HRHDB-COMPANY.   
287900     MOVE HR13F4-PTB-COMPANY-FN      TO HRHDB-COMPANY-FN. 
288000     MOVE HR13F4-PTB-PARTICIPNT      TO HRHDB-PARTICIPNT. 
044921     MOVE HR13F4-PTB-PARTICIPNT-FN   TO HRHDB-PARTICIPNT-FN.
288200     MOVE HR13F4-PTB-EMPLOYEE        TO HRHDB-EMPLOYEE. 
288300     MOVE HR13F4-PTB-EMPLOYEE-FN     TO HRHDB-EMPLOYEE-FN.
288400     MOVE HR13F4-PTB-PLAN-TYPE       TO HRHDB-PLAN-TYPE. 
288500     MOVE HR13F4-PTB-PLAN-TYPE-FN    TO HRHDB-PLAN-TYPE-FN.
288600     MOVE HR13F4-PTB-PLAN-CODE       TO HRHDB-PLAN-CODE. 
288700     MOVE HR13F4-PTB-PLAN-CODE-FN    TO HRHDB-PLAN-CODE-FN.
288800     MOVE HR13F4-PTB-START-DATE      TO HRHDB-BEN-START-DATE. 
288900     MOVE HR13F4-PTB-START-DATE-FN   TO HRHDB-BEN-START-DATE-FN.
289000     MOVE HR13F4-PTB-STOP-DATE       TO HRHDB-BEN-STOP-DATE. 
289100     MOVE HR13F4-PTB-STOP-DATE-FN    TO HRHDB-BEN-STOP-DATE-FN.
289200     MOVE HR13F4-LINE-FC (I1)        TO HRHDB-FC.
289300     MOVE HR13F4-LINE-FC-FN (I1)     TO HRHDB-FC-FN.
289400     MOVE HR13F4-HDB-START-DATE (I1) TO HRHDB-START-DATE. 
289500     MOVE HR13F4-HDB-START-DATE-FN (I1)
289600                                     TO HRHDB-START-DATE-FN.
289700     MOVE HR13F4-HDB-STOP-DATE (I1)  TO HRHDB-STOP-DATE. 
289800     MOVE HR13F4-HDB-STOP-DATE-FN (I1)
289900                                     TO HRHDB-STOP-DATE-FN.
290000     MOVE HR13F4-HDB-DEPENDENT (I1)  TO HRHDB-DEPENDENT.
290100     MOVE HR13F4-HDB-DEPENDENT-FN (I1) TO HRHDB-DEPENDENT-FN.
290200     MOVE HR13F4-HDB-EMP-START (I1)  TO HRHDB-EMP-START. 
290300     MOVE HR13F4-HDB-EMP-START-FN (I1)
290400                                     TO HRHDB-EMP-START-FN.
J10377*     MOVE HR13F4-HDB-PARTICIPNT (I1) TO HRHDB-PARTICIPNT.
J10377*     MOVE HR13F4-HDB-PARTICIPNT-FN (I1) 
J10377*                                     TO HRHDB-PARTICIPNT.
290500
290600     MOVE HR13F4-HILIGHT (I1)        TO HRHDB-HILIGHT.   
290700     MOVE HR13F4-PTB-COVER-TYPE      TO HRHDB-COVER-TYPE.   
290800
015500     MOVE HR13F4-BNT-CREATE-TRANS (I1)   TO HRHDB-CREATE-TRANS.
015600     MOVE HR13F4-BNT-CREATE-TRANS-FN(I1) TO HRHDB-CREATE-TRANS-FN.
015500     MOVE HR13F4-BNT-REASON (I1)         TO HRHDB-REASON.
015600     MOVE HR13F4-BNT-REASON-FN (I1)      TO HRHDB-REASON-FN.
015500     MOVE HR13F4-BNT-MEMBER-ID (I1)      TO HRHDB-MEMBER-ID.
015600     MOVE HR13F4-BNT-MEMBER-ID-FN (I1)   TO HRHDB-MEMBER-ID-FN.

290900 520-END.
291000
291100******************************************************************
291200 530-MOVE-DTL-WS-TO-SCR.
291300******************************************************************
291400
291500     MOVE HRHDB-START-DATE       TO HR13F4-HDB-START-DATE (I1).
291600     MOVE HRHDB-START-DATE-FN    TO HR13F4-HDB-START-DATE-FN (I1).
291700     MOVE HRHDB-STOP-DATE        TO HR13F4-HDB-STOP-DATE (I1).
291800     MOVE HRHDB-STOP-DATE-FN     TO HR13F4-HDB-STOP-DATE-FN (I1).
291900     MOVE HRHDB-DEPENDENT        TO HR13F4-HDB-DEPENDENT (I1).
292000     MOVE HRHDB-DEPENDENT-FN     TO HR13F4-HDB-DEPENDENT-FN (I1).
292100     MOVE HRHDB-PLAN-TYPE        TO HR13F4-PTB-PLAN-TYPE.       
292200     MOVE HRHDB-PLAN-TYPE-FN     TO HR13F4-PTB-PLAN-TYPE-FN.      
292300     MOVE HRHDB-PLAN-CODE        TO HR13F4-PTB-PLAN-CODE.       
292400     MOVE HRHDB-PLAN-CODE-FN     TO HR13F4-PTB-PLAN-CODE-FN.     
292500     MOVE HRHDB-BEN-START-DATE   TO HR13F4-PTB-START-DATE.       
292600     MOVE HRHDB-BEN-START-DATE-FN 
292700                                 TO HR13F4-PTB-START-DATE-FN.    
292800     MOVE HRHDB-BEN-STOP-DATE    TO HR13F4-PTB-STOP-DATE.     
292900     MOVE HRHDB-BEN-STOP-DATE-FN TO HR13F4-PTB-STOP-DATE-FN.     
293000     MOVE HRHDB-EMP-START        TO HR13F4-HDB-EMP-START (I1). 
293100     MOVE HRHDB-EMP-START-FN     TO HR13F4-HDB-EMP-START-FN (I1). 
293200
293300     MOVE HRHDB-HILIGHT          TO HR13F4-HILIGHT (I1).   
293400
           MOVE HRHDB-CREATE-TRANS     TO HR13F4-BNT-CREATE-TRANS (I1).
           MOVE HRHDB-REASON           TO HR13F4-BNT-REASON (I1).
           MOVE HRHDB-MEMBER-ID        TO HR13F4-BNT-MEMBER-ID (I1).
           

J10377     MOVE HRHDB-PARTICIPNT       TO HR13F4-HDB-PARTICIPNT (I1).

293500 530-END.
293600
293700******************************************************************
293800 600-MOVE-HDB-TO-SCREEN.
293900******************************************************************
294000
294100     IF (HDB-COMPANY    NOT = DB-COMPANY)
294200     OR (HDB-EMPLOYEE   NOT = DB-EMPLOYEE) 
294300     OR (HDB-PLAN-TYPE  NOT = DB-PLAN-TYPE) 
294400     OR (HDB-PLAN-CODE  NOT = DB-PLAN-CODE) 
294500     OR (HDB-EMP-START  NOT = DB-EMP-START)
294600        GO TO 600-FIND-NEXT-HRDEPBEN.
294700
294800     IF (HR13F4-FC = "-")
294900         SUBTRACT 1              FROM I1
295000     ELSE
295100         ADD 1                   TO I1.
295200
295300     MOVE CRT-HILITE             TO HR13F4-HILIGHT (I1)
                                         HR13WS-SAVED-HILITE (I1).
295400
295500     MOVE "D"                    TO HR13F4-RECORD-ORIG (I1).
295600     MOVE HDB-START-DATE         TO HR13F4-HDB-START-DATE (I1)
                                          HR13WS-SAVED-START-DATE (I1).
295700     MOVE HDB-STOP-DATE          TO HR13F4-HDB-STOP-DATE (I1).
295800     MOVE HDB-DEPENDENT          TO HR13F4-HDB-DEPENDENT (I1)
                                          HR13WS-SAVED-DEPENDENT (I1).
295900     MOVE HDB-EMP-START          TO HR13F4-HDB-EMP-START (I1).
           MOVE HDB-PLAN-TYPE          TO HR13WS-SAVED-PLAN-TYPE (I1).
           MOVE HDB-PLAN-CODE          TO HR13WS-SAVED-PLAN-CODE (I1).
J10377     MOVE HDB-PARTICIPNT         TO HR13F4-HDB-PARTICIPNT (I1).           
296000     
296100     MOVE HDB-COMPANY                TO DB-COMPANY.
296200     MOVE HDB-EMPLOYEE               TO DB-EMPLOYEE.
296300     MOVE HDB-DEPENDENT              TO DB-SEQ-NBR. 
296400     PERFORM 840-FIND-EMDSET1.
296500     IF (EMDEPEND-FOUND)
296600         MOVE EMD-SEQ-NBR        TO HR13F4-HDB-DEPENDENT (I1)
296700         MOVE EMD-DEP-TYPE       TO HR13F4-EMD-DEP-TYPE (I1)
296800         MOVE EMD-REL-CODE       TO HR13F4-EMD-REL-CODE (I1)
296900         MOVE EMD-STUDENT        TO HR13F4-EMD-STUDENT (I1)
297000         MOVE EMD-DISABLED       TO HR13F4-EMD-DISABLED (I1)
297100     ELSE
297200         MOVE ZEROES             TO HR13F4-HDB-DEPENDENT (I1)
297300         MOVE SPACES             TO HR13F4-EMD-DEP-TYPE (I1)
297400                                    HR13F4-EMD-REL-CODE (I1)
297500                                    HR13F4-EMD-STUDENT (I1)
297600                                    HR13F4-EMD-DISABLED (I1).
297700
297800     MOVE EMD-LAST-NAME          TO HRWS-LAST-NAME.
297900     MOVE EMD-FIRST-NAME         TO HRWS-FIRST-NAME.
298000     MOVE EMD-MIDDLE-INIT        TO HRWS-MIDDLE-INIT.
298100     PERFORM 750-HR-FORMAT-NAME.
298200     MOVE HRWS-FORMAT-NAME       TO HR13F4-EMD-FULL-NAME (I1).
298300
           MOVE PLN-CREATE-TRANS       TO HR13F4-BNT-CREATE-TRANS (I1).
           IF (PLN-CREATE-TRANS        = "Y")
195200         MOVE 106                TO CRT-MSG-NBR
091500         MOVE "HRCOM"            TO CRT-ERROR-CAT
091700         PERFORM 790-GET-MSG
091800         MOVE CRT-MESSAGE        TO HR13F4-HIPAA (I1).

298400 600-FIND-NEXT-HRDEPBEN.
298500
298600     IF (HR13F4-FC = "-")
298700         PERFORM 870-FIND-PREV-HDBSET3
298800     ELSE
298900         PERFORM 860-FIND-NEXT-HDBSET3.
299000
299100 600-END.
299200
299300******************************************************************
299400 610-MOVE-EMD-TO-SCREEN.
299500******************************************************************
299600
J06202     IF (EMD-ACTIVE-FLAG             = "I")
J06202         GO TO 610-FIND-NEXT-EMDEPEND.

299700     MOVE HR13F4-PTB-COMPANY         TO DB-COMPANY.
299800     MOVE HR13F4-PTB-PLAN-TYPE       TO DB-PLAN-TYPE.
299900     MOVE HR13F4-PTB-PLAN-CODE       TO DB-PLAN-CODE.
300000     PERFORM 840-FIND-PLNSET1.
300100     IF (PLAN-NOTFOUND)
300200         GO TO 610-FIND-NEXT-EMDEPEND.
300300
300400     MOVE HR13F4-PTB-PARTICIPNT      TO DB-PARTICIPNT.
300500*     MOVE HR13F4-PTB-EMPLOYEE        TO DB-EMPLOYEE.
300600     IF (HR13F4-PTB-PARTICIPNT = ZEROES)
300700         MOVE HR13F4-PTB-EMPLOYEE    TO DB-EMPLOYEE
300800     ELSE
300900         MOVE ZEROES                 TO DB-EMPLOYEE.
301000     MOVE HR13F4-PTB-START-DATE      TO DB-START-DATE.
301100     PERFORM 840-FIND-PTBSET1.
301200
236500     MOVE HR13F4-PTB-EMPLOYEE        TO DB-EMPLOYEE.

301300     IF (PLN-COVERAGE-TYPE           = "0")
301400         GO TO 610-FIND-NEXT-EMDEPEND.
301500
           IF (PTB-EMPLOYEE                NOT = ZEROES)
               MOVE "R"                    TO HRDEP-COVER-TYPE
           ELSE
               MOVE "C"                    TO HRDEP-COVER-TYPE.
           INITIALIZE HRDEP-CALC-AGE-DATE.
           PERFORM 5110-FIND-DEP-N-STUD-AGE.
           IF (ERROR-FOUND)
               GO TO 610-FIND-NEXT-EMDEPEND
           ELSE
118400         MOVE HRDEP-DEP-AGE          TO HR13WS-DEP-AGE
118500         MOVE HRDEP-STUD-AGE         TO HR13WS-STUDENT-AGE
118600         MOVE HRDEP-COV-DEPS         TO HR13WS-COV-DEPENDENTS
               MOVE HRDEP-PT-STUDENTS      TO HR13WS-PT-STUDENTS
118600         MOVE HRDEP-TERM-OPT         TO HR13WS-TERM-OPT.
217900
J07540* (E)MPLOYEE, (S)POUSE, (P)ARTNER, SPOUSE (O)R PARTNER
303500     IF  (EMD-DEP-TYPE               = "D")
J07540     AND (HR13WS-COV-DEPENDENTS      = "E" OR "S" OR "P" OR "O")
303700         GO TO 610-FIND-NEXT-EMDEPEND.
303800
J07540* (E)MPLOYEE, (D)EPENDENTS, PA(R)TNER DEPS
J07540     IF  (EMD-DEP-TYPE               = "S" OR "P") 
J07540     AND (HR13WS-COV-DEPENDENTS      = "E" OR "D" OR "R")
304100         GO TO 610-FIND-NEXT-EMDEPEND.
304200
304300     IF (HR13F4-FC = "-")
304400         SUBTRACT 1              FROM I1
304500     ELSE
304600         ADD 1                   TO I1.
304700
304800     MOVE "B"                        TO HR13F4-RECORD-ORIG (I1).
304900
J07540     IF (EMD-DEP-TYPE = "S" OR "P")
305100         MOVE HR13F4-PTB-START-DATE  TO HR13F4-HDB-START-DATE (I1)
305200         MOVE HR13F4-PTB-STOP-DATE   TO HR13F4-HDB-STOP-DATE (I1).
305300
305400     IF (EMD-DEP-TYPE = "D")
305500         IF (HR13F4-PTB-START-DATE NOT < EMD-BIRTHDATE)
305600             MOVE HR13F4-PTB-START-DATE 
305700                                     TO HR13F4-HDB-START-DATE (I1)
305800         ELSE
305900             MOVE EMD-BIRTHDATE  TO HR13F4-HDB-START-DATE (I1)
306000         END-IF
306100     END-IF.
306200
           COMPUTE I4 = (I1 - 1).
           PERFORM
               UNTIL   (I4  = 0)
               OR     ((HR13WS-SAVED-PLAN-TYPE (I4) = PTB-PLAN-TYPE)
               AND     (HR13WS-SAVED-PLAN-CODE (I4) = PTB-PLAN-CODE)
               AND     (HR13WS-SAVED-DEPENDENT (I4) = EMD-SEQ-NBR))
                       SUBTRACT 1 FROM I4
           END-PERFORM.

           IF  (I4 NOT = ZEROS)
           AND (HR13WS-SAVED-HILITE (I4)    = "*")
           AND (HR13WS-SAVED-PLAN-TYPE (I4) = PTB-PLAN-TYPE)
           AND (HR13WS-SAVED-PLAN-CODE (I4) = PTB-PLAN-CODE)
           AND (HR13WS-SAVED-DEPENDENT (I4) = EMD-SEQ-NBR)
               MOVE HR13WS-SAVED-START-DATE (I4)
                                       TO HR13F4-HDB-START-DATE (I1).

J07540     IF (EMD-DEP-TYPE            = "S" OR "P")
           OR (EMD-DISABLED            = "Y")
               CONTINUE
           ELSE
               INITIALIZE HRDEP-CALC-DEP-N-STUD
               PERFORM 5200-DEP-AGE-DATE-CALC

               MOVE HRDEP-DEP-END-DATE     TO HR13WS-DEP-END-DATE

221800         IF (HR13WS-DEP-END-DATE     < HR13F4-PTB-START-DATE)
221900             INITIALIZE HR13F4-HDB-START-DATE (I1)
222000                        HR13F4-HDB-STOP-DATE (I1)
222100         ELSE
222200         IF (HR13WS-DEP-END-DATE     NOT < HR13F4-PTB-START-DATE)
222300             IF (HR13WS-DEP-END-DATE < HR13F4-PTB-STOP-DATE)
222400                 MOVE HR13WS-DEP-END-DATE 
222500                                     TO HR13F4-HDB-STOP-DATE (I1)
222600             ELSE
222700             IF (HR13F4-PTB-STOP-DATE NOT = ZEROES)
222800                 MOVE HR13F4-PTB-STOP-DATE 
222900                                     TO HR13F4-HDB-STOP-DATE (I1)
223000             ELSE
223100                 MOVE HR13WS-DEP-END-DATE  
223200                                     TO HR13F4-HDB-STOP-DATE (I1).
226100
311700     IF  (EMD-DEP-TYPE = "D")
311800     AND (EMD-DISABLED = "Y")
311900         MOVE HR13F4-PTB-STOP-DATE   TO HR13F4-HDB-STOP-DATE (I1).
312000
312100     MOVE HR13F4-PTB-COMPANY         TO DB-COMPANY. 
312200     MOVE HR13F4-PTB-EMPLOYEE        TO DB-EMPLOYEE. 
312300     MOVE EMD-SEQ-NBR                TO DB-DEPENDENT. 
312400     MOVE HR13F4-PTB-PLAN-TYPE       TO DB-PLAN-TYPE. 
312500     MOVE HR13F4-PTB-PLAN-CODE       TO DB-PLAN-CODE. 
312600     MOVE HR13F4-PTB-START-DATE      TO DB-EMP-START.
312700     MOVE HR13F4-HDB-START-DATE (I1) TO DB-START-DATE
                                              HRDEP-HDB-START-DATE.
312700     MOVE HR13F4-HDB-STOP-DATE (I1)  TO HRDEP-HDB-STOP-DATE.
           MOVE PTB-STOP-DATE              TO HRDEP-BEN-STOP-DATE.
           PERFORM 5300-GET-NEW-HDB-START-N-STOP.

           MOVE HRDEP-HDB-START-DATE   TO HR13F4-HDB-START-DATE (I1).
           MOVE HRDEP-HDB-STOP-DATE    TO HR13F4-HDB-STOP-DATE (I1).

313900     MOVE HR13F4-PTB-START-DATE      TO HR13F4-HDB-EMP-START (I1).
314000
314100     MOVE EMD-SEQ-NBR        TO HR13F4-HDB-DEPENDENT (I1).
314200     MOVE EMD-DEP-TYPE       TO HR13F4-EMD-DEP-TYPE (I1).
314300     MOVE EMD-REL-CODE       TO HR13F4-EMD-REL-CODE (I1).
314400     MOVE EMD-STUDENT        TO HR13F4-EMD-STUDENT (I1).
314500     MOVE EMD-DISABLED       TO HR13F4-EMD-DISABLED (I1).
314600
314700     MOVE EMD-LAST-NAME          TO HRWS-LAST-NAME.
314800     MOVE EMD-FIRST-NAME         TO HRWS-FIRST-NAME.
314900     MOVE EMD-MIDDLE-INIT        TO HRWS-MIDDLE-INIT.
315000     PERFORM 750-HR-FORMAT-NAME.
315100     MOVE HRWS-FORMAT-NAME       TO HR13F4-EMD-FULL-NAME (I1).
315200
           MOVE PLN-CREATE-TRANS       TO HR13F4-BNT-CREATE-TRANS (I1).
           IF (PLN-CREATE-TRANS        = "Y")
195200         MOVE 106                TO CRT-MSG-NBR
091500         MOVE "HRCOM"            TO CRT-ERROR-CAT
091700         PERFORM 790-GET-MSG
091800         MOVE CRT-MESSAGE        TO HR13F4-HIPAA (I1).

315300 610-FIND-NEXT-EMDEPEND.
315400
315500     IF (HR13F4-FC = "-")
315600         PERFORM 870-FIND-PREV-EMDSET1
315700     ELSE
315800         PERFORM 860-FIND-NEXT-EMDSET1.
315900
316000 610-END.             
316100
316200******************************************************************
316300 HR13S4-TRANSACTION-END.
316400******************************************************************
316500
