******* BN70PD 17 <3201176168>
      ******************************************************************
      *               M O D I F I C A T I O N   L O G:                 *
      ******************************************************************
      ******************************************************************
      *  ACS001  11/09/10    REAPPLIED CUSTOM MODIFICATIONS AFTER 9.0  *
      *     M. HUNTER - ACS  APPS UPGRADE.  PF TRIGGER CODE WAS ADDED  *
      *                      PREVIOUSLY.                               *
      *          08/30/11    REAPPLIED CUSTOM MODIFICATIONS AFTER      *
      *     M. HUNTER - ACS  9.0.1. APPS UPGRADE.                      *
      *                                                                *
      ******************************************************************

000100******************************************************************
000200 BN70S1-TRANSACTION              SECTION 46.
000300******************************************************************
000400 BN70S1-START.
000500
000600     PERFORM 200-EDIT-TRAN
000700     THRU    200-END.
000800
000900     IF (NO-ERROR-FOUND)
001000         PERFORM 400-PROCESS-TRAN
001100         THRU    400-END.
001200
001300     GO TO BN70S1-TRANSACTION-END.
001400
001500******************************************************************
001600 200-EDIT-TRAN.
001700******************************************************************
001800
001900     PERFORM 210-EDIT-ACCESS
002000     THRU    210-END.
002100
002200     IF (ERROR-FOUND)
002300         GO TO 200-END.
002400
002500     IF (BN70F1-FC = "D")
002600         PERFORM 220-EDIT-DELETE
002700         THRU    220-END
002800         GO TO 200-END.
002900
003000     IF (BN70F1-FC = "A" OR "C")
003100         PERFORM 230-EDIT-DATA
003200         THRU    230-END
003300         GO TO 200-END.
003400
003500 200-END.
003600
003700******************************************************************
003800 210-EDIT-ACCESS.
003900******************************************************************
004000
P04521     PERFORM 1900-PAR-PRS-COMPANY-NAME
P04521     THRU    1900-PRS-END.
004200     IF (PRSYSTEM-NOTFOUND)
004300         MOVE 52                              TO CRT-ERROR-NBR
004400         MOVE BN70F1-PAR-COMPANY-FN           TO CRT-FIELD-NBR
004500         GO TO 210-END.
004600
P04521     PERFORM 1900-PAR-BNC-COMPANY
P04521     THRU    1900-BNC-END.
004800     IF (BNCOMPANY-NOTFOUND)
004900         MOVE 53                              TO CRT-ERROR-NBR
005000         MOVE BN70F1-PAR-COMPANY-FN           TO CRT-FIELD-NBR
005100         GO TO 210-END.
005200
005300     MOVE BN70F1-PAR-COMPANY     TO DB-COMPANY.
005400     MOVE BN70F1-PAR-PARTICIPNT  TO DB-PARTICIPNT.
005500
005600     IF  (BN70F1-FC = "I" OR "C" OR "D")
005700     AND (BN70F1-PAR-PARTICIPNT = ZEROS)
005800         MOVE 101                             TO CRT-ERROR-NBR
005900         MOVE BN70F1-PAR-PARTICIPNT-FN        TO CRT-FIELD-NBR
006000         GO TO 210-END.
006100
006200     PERFORM 840-FIND-PARSET1.
006300
006400     IF (BN70F1-FC = "N")
006500         IF (PARTICIPNT-FOUND)
006600             PERFORM 850-FIND-NLT-PARSET1
006700             PERFORM 860-FIND-NEXT-PARSET1
006800         ELSE
006900             PERFORM 850-FIND-NLT-PARSET1.
007000
007100     IF (BN70F1-FC = "P")
007200         PERFORM 850-FIND-NLT-PARSET1
007300         PERFORM 870-FIND-PREV-PARSET1.
007400
007500     IF (BN70F1-FC = "N" OR "P")
007600         IF (PARTICIPNT-NOTFOUND)
007700         OR (PAR-COMPANY NOT = BN70F1-PAR-COMPANY)
007800             MOVE 11                          TO CRT-ERROR-NBR
007900             MOVE BN70F1-FC-FN                TO CRT-FIELD-NBR
008000             GO TO 210-END
008100         ELSE
008200             MOVE PAR-PARTICIPNT TO BN70F1-PAR-PARTICIPNT
008300                                    DB-PARTICIPNT.
008400
008500     IF  (PARTICIPNT-NOTFOUND)
008600     AND (BN70F1-FC = "I" OR "C" OR "D")
008700         MOVE 102                             TO CRT-ERROR-NBR
008800         MOVE BN70F1-PAR-PARTICIPNT-FN        TO CRT-FIELD-NBR
008900         GO TO 210-END.
009000
009100     IF  (PARTICIPNT-FOUND)
009200     AND (BN70F1-FC = "A")
009300         MOVE 103                             TO CRT-ERROR-NBR
009400         MOVE BN70F1-PAR-PARTICIPNT-FN        TO CRT-FIELD-NBR
009500         GO TO 210-END.
009600
009700 210-END.
009800
009900******************************************************************
010000 220-EDIT-DELETE.
010100******************************************************************
010200
P04521*    PERFORM 1905-PAR-PTB-PART-BENEFIT.
P04521     PERFORM 1905-PAR-PTB-PART-BENEFIT
P04521     THRU    1905-PTB-END.
010400     IF (PARTBEN-FOUND)
010500         MOVE 104                             TO CRT-ERROR-NBR
010600         MOVE BN70F1-PAR-PARTICIPNT-FN        TO CRT-FIELD-NBR
010700         GO TO 220-END.

J93627* Check for COBRA ACA records
J93627*    MOVE PAR-COMPANY            TO DB-COMPANY.
J93527*    MOVE "3"                    TO DB-TYPE.
J93627*    MOVE ZEROES                 TO DB-EMPLOYEE.
J93627*    MOVE PAR-PARTICIPNT         TO DB-PARTICIPNT.
J93627*    MOVE PPISET2-PARTICIPNT     TO WS-DB-BEG-RNG.
J93627*    PERFORM 850-KFIND-BEGRNG-PPISET2.
J93627*    IF  (BNACAIRS-KFOUND)
J93627*        MOVE 120                                 TO CRT-ERROR-NBR
J93627*        MOVE BN70F1-PAR-PARTICIPNT-FN            TO CRT-FIELD-NBR
J93627*        GO TO 220-END.
J52049     MOVE "I"                        TO HACAF1-FC.
J52049     MOVE PAR-COMPANY                TO HACAF1-COMPANY.
J52049     MOVE "3"                        TO HACAF1-TYPE.
J52049     MOVE ZEROES                     TO HACAF1-EMPLOYEE.
J52049     MOVE PAR-PARTICIPNT             TO HACAF1-PARTICIPNT.
J52049     INVOKE "HACA.1".
J09473     MOVE ZEROES                     TO CRT-ERROR-NBR.
J52049     IF (HACAF1-ERROR-NBR NOT = ZEROES)
J09473         MOVE 120                    TO CRT-ERROR-NBR
J09473         MOVE BN70F1-PAR-PARTICIPNT-FN TO CRT-FIELD-NBR
J09473         GO TO 220-END
J09473     END-IF.
J09473
J09473* Check for Retiree ACA Records
J52049     MOVE "I"                        TO HACAF1-FC.
J52049     MOVE PAR-COMPANY                TO HACAF1-COMPANY.
J52049     MOVE "2"                        TO HACAF1-TYPE.
J52049     MOVE PAR-EMPLOYEE               TO HACAF1-EMPLOYEE.
J52049     MOVE ZEROES                     TO HACAF1-PARTICIPNT.
J52049     INVOKE "HACA.1".
J52049     MOVE ZEROES                     TO CRT-ERROR-NBR.
J52049     IF (HACAF1-ERROR-NBR NOT = ZEROES)
J09473         MOVE 120                    TO CRT-ERROR-NBR
J09473         MOVE BN70F1-PAR-PARTICIPNT-FN TO CRT-FIELD-NBR
J09473         GO TO 220-END
J09473     END-IF.

           MOVE BN70F1-PAR-COMPANY                  TO DB-COMPANY.
           MOVE ZEROES                              TO DB-EMPLOYEE.
           MOVE BN70F1-PAR-PARTICIPNT               TO DB-PARTICIPNT.
           MOVE BNTSET2-PARTICIPNT                  TO WS-DB-BEG-RNG. 
           PERFORM 850-KFIND-BEGRNG-BNTSET2.
           IF (BNTRANS-KFOUND)
009300         MOVE 131                             TO CRT-ERROR-NBR
009400         MOVE BN70F1-PAR-PARTICIPNT-FN        TO CRT-FIELD-NBR
009500         GO TO 220-END.
010800
010900 220-END.
011000
011100******************************************************************
011200 230-EDIT-DATA.
011300******************************************************************
011400
011500     IF  (BN70F1-FC              = "A")
011600     AND (BNC-AUTO-PART          = "Y")
011700     AND (BN70F1-PAR-PARTICIPNT  NOT = ZEROS)
011800         MOVE 115                                TO CRT-ERROR-NBR
011900         MOVE BN70F1-PAR-PARTICIPNT-FN           TO CRT-FIELD-NBR
012000         GO TO 230-END.
012100
012200     IF  (BN70F1-FC              = "A")
012300     AND (BNC-AUTO-PART          NOT = "Y")
012400     AND (BN70F1-PAR-PARTICIPNT  = ZEROS)
012500         MOVE 101                                TO CRT-ERROR-NBR
012600         MOVE BN70F1-PAR-PARTICIPNT-FN           TO CRT-FIELD-NBR
012700         GO TO 230-END.
012800
012900     IF  (BN70F1-PAR-DEPENDENT NOT = ZEROS)
013000     AND (BN70F1-PAR-EMPLOYEE  = ZEROS)
013100         MOVE 122                                TO CRT-ERROR-NBR
013200         MOVE BN70F1-PAR-EMPLOYEE-FN             TO CRT-FIELD-NBR
013300         GO TO 230-END.
013400
013500     IF (BN70F1-PAR-DEPENDENT NOT = ZEROS)
013600         PERFORM 1900-PAR-EMD-DEPENDENT
P04521         THRU    1900-EMD-END
013700         IF (EMDEPEND-NOTFOUND)
013800             MOVE 106                         TO CRT-ERROR-NBR
013900             MOVE BN70F1-PAR-DEPENDENT-FN     TO CRT-FIELD-NBR
014000             GO TO 230-END
014100         ELSE
014200             IF  (BN70F1-PAR-LAST-NAME    = SPACES)
014300             AND (BN70F1-PAR-FIRST-NAME   = SPACES)
014400             AND (BN70F1-PAR-MIDDLE-INIT  = SPACES)
014500             AND (BN70F1-PAR-ADDR1        = SPACES)
014600             AND (BN70F1-PAR-ADDR2        = SPACES)
014700             AND (BN70F1-PAR-ADDR3        = SPACES)
014800             AND (BN70F1-PAR-ADDR4        = SPACES)
014900             AND (BN70F1-PAR-CITY         = SPACES)
015000             AND (BN70F1-PAR-STATE        = SPACES)
015100             AND (BN70F1-PAR-ZIP          = SPACES)
015200             AND (BN70F1-PAR-COUNTRY-CODE = SPACES)
                       MOVE EMD-LAST-NAME    TO HRWS-LAST-NAME
                       MOVE EMD-LAST-NAME-PRE
                                             TO HRWS-LAST-NAME-PRE
                       MOVE EMD-NAME-SUFFIX  TO HRWS-NAME-SUFFIX
                       INITIALIZE HRWS-FIRST-NAME
                                  HRWS-MIDDLE-INIT
                       PERFORM 750-HR-FORMAT-NAME
                       MOVE HRWS-FORMAT-NAME TO BN70F1-PAR-LAST-NAME
015400                 MOVE EMD-FIRST-NAME   TO BN70F1-PAR-FIRST-NAME
015500                 MOVE EMD-MIDDLE-INIT  TO BN70F1-PAR-MIDDLE-INIT
015600                 MOVE EMD-ADDR1        TO BN70F1-PAR-ADDR1
015700                 MOVE EMD-ADDR2        TO BN70F1-PAR-ADDR2
015800                 MOVE EMD-ADDR3        TO BN70F1-PAR-ADDR3
015900                 MOVE EMD-ADDR4        TO BN70F1-PAR-ADDR4
016000                 MOVE EMD-CITY         TO BN70F1-PAR-CITY
016100                 MOVE EMD-STATE        TO BN70F1-PAR-STATE
016200                 MOVE EMD-ZIP          TO BN70F1-PAR-ZIP
016300                 MOVE EMD-COUNTRY-CODE TO BN70F1-PAR-COUNTRY-CODE
016400                 PERFORM 236-DFLT-EMD-INFO
016500                 THRU    236-END
016600             ELSE
016700                 IF  (BN70F1-PAR-ADDR1        = SPACES)
016800                 AND (BN70F1-PAR-ADDR2        = SPACES)
016900                 AND (BN70F1-PAR-ADDR3        = SPACES)
017000                 AND (BN70F1-PAR-ADDR4        = SPACES)
017100                 AND (BN70F1-PAR-CITY         = SPACES)
017200                 AND (BN70F1-PAR-STATE        = SPACES)
017300                 AND (BN70F1-PAR-ZIP          = SPACES)
017400                 AND (BN70F1-PAR-COUNTRY-CODE = SPACES)
017500                     MOVE EMD-ADDR1        TO BN70F1-PAR-ADDR1
017600                     MOVE EMD-ADDR2        TO BN70F1-PAR-ADDR2
017700                     MOVE EMD-ADDR3        TO BN70F1-PAR-ADDR3
017800                     MOVE EMD-ADDR4        TO BN70F1-PAR-ADDR4
017900                     MOVE EMD-CITY         TO BN70F1-PAR-CITY
018000                     MOVE EMD-STATE        TO BN70F1-PAR-STATE
018100                     MOVE EMD-ZIP          TO BN70F1-PAR-ZIP
018200                     MOVE EMD-COUNTRY-CODE TO
018200                                           BN70F1-PAR-COUNTRY-CODE
018300                     PERFORM 236-DFLT-EMD-INFO
018400                     THRU    236-END.
018500
018600     IF (BN70F1-PAR-EMPLOYEE NOT = ZEROS)
018700         PERFORM 1900-PAR-PEM-PAEMPLOYEE
P04521         THRU    1900-PEM-END
018800         PERFORM 1900-PAR-EMP-EMPLOYEE
P04521         THRU    1900-EMP-END
018900         IF (EMPLOYEE-NOTFOUND)
019000         OR (PAEMPLOYEE-NOTFOUND)
019100             MOVE 105                         TO CRT-ERROR-NBR
019200             MOVE BN70F1-PAR-EMPLOYEE-FN      TO CRT-FIELD-NBR
019300             GO TO 230-END
019400         ELSE
019500             IF  (BN70F1-PAR-LAST-NAME    = SPACES)
019600             AND (BN70F1-PAR-FIRST-NAME   = SPACES)
019700             AND (BN70F1-PAR-MIDDLE-INIT  = SPACES)
019800             AND (BN70F1-PAR-ADDR1        = SPACES)
019900             AND (BN70F1-PAR-ADDR2        = SPACES)
020000             AND (BN70F1-PAR-ADDR3        = SPACES)
020100             AND (BN70F1-PAR-ADDR4        = SPACES)
020200             AND (BN70F1-PAR-CITY         = SPACES)
020300             AND (BN70F1-PAR-STATE        = SPACES)
020400             AND (BN70F1-PAR-ZIP          = SPACES)
020500             AND (BN70F1-PAR-COUNTRY-CODE = SPACES)
                       MOVE EMP-LAST-NAME    TO HRWS-LAST-NAME
                       MOVE EMP-LAST-NAME-PRE
                                             TO HRWS-LAST-NAME-PRE
                       MOVE EMP-NAME-SUFFIX  TO HRWS-NAME-SUFFIX
                       INITIALIZE HRWS-FIRST-NAME
                                  HRWS-MIDDLE-INIT
                       PERFORM 750-HR-FORMAT-NAME
                       MOVE HRWS-FORMAT-NAME TO BN70F1-PAR-LAST-NAME
020700                 MOVE EMP-FIRST-NAME   TO BN70F1-PAR-FIRST-NAME
020800                 MOVE EMP-MIDDLE-INIT  TO BN70F1-PAR-MIDDLE-INIT
020900                 MOVE EMP-ADDR1        TO BN70F1-PAR-ADDR1
021000                 MOVE EMP-ADDR2        TO BN70F1-PAR-ADDR2
021100                 MOVE EMP-ADDR3        TO BN70F1-PAR-ADDR3
021200                 MOVE EMP-ADDR4        TO BN70F1-PAR-ADDR4
021300                 MOVE EMP-CITY         TO BN70F1-PAR-CITY
021400                 MOVE EMP-STATE        TO BN70F1-PAR-STATE
021500                 MOVE EMP-ZIP          TO BN70F1-PAR-ZIP
021600                 MOVE EMP-COUNTRY-CODE TO BN70F1-PAR-COUNTRY-CODE
021700                 PERFORM 234-DFLT-EMP-INFO
021800                 THRU    234-END
021900             ELSE
022000                 IF  (BN70F1-PAR-ADDR1       = SPACES)
022100                 AND (BN70F1-PAR-ADDR2       = SPACES)
022200                 AND (BN70F1-PAR-ADDR3       = SPACES)
022300                 AND (BN70F1-PAR-ADDR4       = SPACES)
022400                 AND (BN70F1-PAR-CITY        = SPACES)
022500                 AND (BN70F1-PAR-STATE       = SPACES)
022600                 AND (BN70F1-PAR-ZIP         = SPACES)
022700                     MOVE EMP-ADDR1        TO BN70F1-PAR-ADDR1
022800                     MOVE EMP-ADDR2        TO BN70F1-PAR-ADDR2
022900                     MOVE EMP-ADDR3        TO BN70F1-PAR-ADDR3
023000                     MOVE EMP-ADDR4        TO BN70F1-PAR-ADDR4
023100                     MOVE EMP-CITY         TO BN70F1-PAR-CITY
023200                     MOVE EMP-STATE        TO BN70F1-PAR-STATE
023300                     MOVE EMP-ZIP          TO BN70F1-PAR-ZIP
023400                     MOVE EMP-COUNTRY-CODE TO
023400                                           BN70F1-PAR-COUNTRY-CODE
023500                     IF  (BN70F1-PAR-LAST-NAME  = EMP-LAST-NAME)
023600                     AND (BN70F1-PAR-FIRST-NAME = EMP-FIRST-NAME)
023700                     AND (BN70F1-PAR-MIDDLE-INIT
023800                                                = EMP-MIDDLE-INIT)
023900                         PERFORM 234-DFLT-EMP-INFO
024000                         THRU    234-END.
024100
024200     IF (BN70F1-PAR-LAST-NAME = SPACES)
024300         MOVE 108                             TO CRT-ERROR-NBR
024400         MOVE BN70F1-PAR-LAST-NAME-FN         TO CRT-FIELD-NBR
024500         GO TO 230-END.
024600
024700     IF (BN70F1-PAR-FIRST-NAME = SPACES)
024800         MOVE 109                             TO CRT-ERROR-NBR
024900         MOVE BN70F1-PAR-FIRST-NAME-FN        TO CRT-FIELD-NBR
025000         GO TO 230-END.
025100
025200     IF  ((BN70F1-PAR-ADDR1           NOT = SPACES)
025200     OR   (BN70F1-PAR-ADDR2           NOT = SPACES)
025200     OR   (BN70F1-PAR-ADDR3           NOT = SPACES)
025200     OR   (BN70F1-PAR-ADDR4           NOT = SPACES)
025200     OR   (BN70F1-PAR-CITY            NOT = SPACES)
025200     OR   (BN70F1-PAR-ZIP             NOT = SPACES))
           AND  (BN70F1-PAR-COUNTRY-CODE    = SPACES)
               MOVE "US"                    TO BN70F1-PAR-COUNTRY-CODE.

025200     IF (BN70F1-PAR-ADDR1 = SPACES)
025300         MOVE 123                             TO CRT-ERROR-NBR
025400         MOVE BN70F1-PAR-ADDR1-FN             TO CRT-FIELD-NBR
025500         GO TO 230-END.
025600
025700     IF (BN70F1-PAR-CITY = SPACES)
025800         MOVE 124                             TO CRT-ERROR-NBR
025900         MOVE BN70F1-PAR-CITY-FN              TO CRT-FIELD-NBR
026000         GO TO 230-END.
026100
026200     IF (BN70F1-PAR-STATE = SPACES)
               IF (BN70F1-PAR-COUNTRY-CODE = "US")
026300             MOVE 125                         TO CRT-ERROR-NBR
026400             MOVE BN70F1-PAR-STATE-FN         TO CRT-FIELD-NBR
026500             GO TO 230-END
               ELSE
               IF (BN70F1-PAR-COUNTRY-CODE = "CA")
026300             MOVE 129                         TO CRT-ERROR-NBR
026400             MOVE BN70F1-PAR-STATE-FN         TO CRT-FIELD-NBR
026500             GO TO 230-END.
026600
           IF  (BN70F1-PAR-COUNTRY-CODE    = "US")
           AND (BN70F1-PAR-STATE           NOT = "AA" AND "AE" AND "AP")
                MOVE BN70F1-PAR-STATE      TO DB-STATE
130000          PERFORM 840-KFIND-PSASET1
130100          IF (PRSTATE-KNOTFOUND)
130200              MOVE 133                        TO CRT-ERROR-NBR
130300              MOVE BN70F1-PAR-STATE-FN        TO CRT-FIELD-NBR
                    GO TO 230-END.

           IF (BN70F1-PAR-COUNTRY-CODE     = "CA")
                MOVE BN70F1-PAR-STATE      TO DB-PROVINCE
130000          PERFORM 840-KFIND-PPVSET1
130100          IF (PRPROVINCE-KNOTFOUND)
130200              MOVE 132                        TO CRT-ERROR-NBR
130300              MOVE BN70F1-PAR-STATE-FN        TO CRT-FIELD-NBR
                    GO TO 230-END.
130900
026700     IF (BN70F1-PAR-ZIP = SPACES)
026800         MOVE 126                             TO CRT-ERROR-NBR
026900         MOVE BN70F1-PAR-ZIP-FN               TO CRT-FIELD-NBR
027000         GO TO 230-END.
027100
027200     IF (BN70F1-PAR-BIRTHDATE NOT < WS-SYSTEM-DATE-YMD)
027300         MOVE 119                                TO CRT-ERROR-NBR
027400         MOVE BN70F1-PAR-BIRTHDATE-FN            TO CRT-FIELD-NBR
027500         GO TO 230-END.
027600
PO4521     PERFORM 1900-PAR-OCC-OCCUR-TYPE
P04521     THRU    1900-OCC-END.
027800     IF (OCCURTYPE-NOTFOUND)
027900         MOVE 107                             TO CRT-ERROR-NBR
028000         MOVE BN70F1-PAR-OCCUR-TYPE-FN        TO CRT-FIELD-NBR
028100         GO TO 230-END
028200     ELSE
028300         MOVE OCC-MONTHS-EXT     TO BN70WS-OCC-MONTHS-EXT.
028400
028500     IF  (BN70F1-PAR-EVENT-DATE < BN70F1-PAR-OCCUR-DATE)
028600     AND (BN70F1-PAR-EVENT-DATE NOT = ZEROS)
028700         MOVE 118                             TO CRT-ERROR-NBR
028800         MOVE BN70F1-PAR-EVENT-DATE-FN        TO CRT-FIELD-NBR
028900         GO TO 230-END.
029000
           IF  (BN70F1-PT-XMIT-NBR1 = ZEROES)
               MOVE BN70F1-PAR-FICA-NBR        TO HRWS-SOC-NBR
               MOVE BN70F1-PAR-COUNTRY-CODE    TO HRWS-WORK-COUNTRY
               IF  (HRWS-WORK-COUNTRY = SPACES)
                   MOVE PRS-WORK-COUNTRY       TO HRWS-WORK-COUNTRY
               END-IF
               MOVE BN70F1-PAR-BIRTHDATE       TO HRWS-DATE-8-FIELD
               PERFORM 760-HR-FORMAT-SOC-NBR
               IF (ERROR-FOUND)
                   MOVE HRWS-WORK-COUNTRY      TO CRT-ERR-VAR1
                   MOVE BN70F1-PAR-FICA-NBR-FN TO CRT-FIELD-NBR
                   MOVE 216                    TO CRT-ERROR-NBR
                   MOVE 1                      TO BN70F1-PT-XMIT-NBR1
                   GO TO 230-END
               END-IF
               MOVE HRWS-SOC-NBR               TO BN70F1-PAR-FICA-NBR
           END-IF.
029800
029900     IF  (BN70F1-FC              = "C")
030000     AND (BN70F1-PAR-EMPLOYEE    NOT = PAR-EMPLOYEE)
030100         MOVE WS-FALSE           TO BN70WS-RES-WARN
030200         MOVE BN70F1-PAR-COMPANY TO DB-COMPANY
030300         MOVE BN70F1-PAR-PARTICIPNT
030400                                 TO DB-EMPLOYEE
030500         INITIALIZE              DB-PLAN-CODE
030600                                 DB-START-DATE
030700                                 DB-DATE
030800                                 DB-CHECK-NBR
030900                                 BN70WS-RES-WARN
031000         PERFORM 850-FIND-NLT-RTRSET2
031100         IF  (RESTRANS-FOUND)
031200         AND (RTR-COMPANY        = DB-COMPANY)
031300         AND (RTR-EMPLOYEE       = DB-EMPLOYEE)
031400             MOVE WS-TRUE        TO BN70WS-RES-WARN
031500             IF (BN70F1-PAR-EMPLOYEE = ZEROES)
031600                 MOVE 128                        TO CRT-ERROR-NBR
031700                 MOVE BN70F1-PAR-EMPLOYEE-FN     TO CRT-FIELD-NBR
031800                 GO TO 230-END.
031900
          IF (BN70F1-PAR-COUNTRY-CODE NOT = SPACES)
              MOVE BN70F1-PAR-COUNTRY-CODE TO DB-COUNTRY-CODE
              PERFORM 840-FIND-INTSET1
              IF (INSTCTRYCD-NOTFOUND)
                  MOVE 110                             TO CRT-ERROR-NBR
                  MOVE BN70F1-PAR-COUNTRY-CODE-FN      TO CRT-FIELD-NBR
                  GO TO 230-END.

032000*** IF DOING A CHANGE TO EMPLOYEE-NBR, ENSURE THAT BENEFITS DO 
032100*** NOT ALREADY EXIST BEFORE PERMITTING THE CHANGE.
032200     IF  (BN70F1-FC              = "C")
032300     AND (BN70F1-PAR-EMPLOYEE    NOT = PAR-EMPLOYEE)
032400         MOVE PAR-COMPANY            TO DB-COMPANY
032500         MOVE ZEROS                  TO DB-EMPLOYEE
032600         MOVE PAR-PARTICIPNT         TO DB-PARTICIPNT
032700         MOVE SPACES                 TO DB-PLAN-TYPE
032800         MOVE SPACES                 TO DB-PLAN-CODE
032900         MOVE ZEROS                  TO DB-START-DATE
033000         PERFORM 850-FIND-NLT-PTBSET5
033100         IF  (PARTBEN-FOUND)
033200         AND (PTB-COMPANY    = DB-COMPANY)
033300         AND (PTB-PARTICIPNT = DB-PARTICIPNT)
033400             MOVE 215                            TO CRT-ERROR-NBR
033500             MOVE BN70F1-PAR-EMPLOYEE-FN         TO CRT-FIELD-NBR
033600             GO TO 230-END.
033700
033800
033900 230-END.
034000
034100******************************************************************
034200 234-DFLT-EMP-INFO.
034300******************************************************************
034400*
           IF (BN70F1-PAR-CONSENT = SPACES)
               MOVE PEM-CONSENT        TO BN70F1-PAR-CONSENT.

034500     IF (BN70F1-PAR-SMOKER = SPACES)
034600         MOVE PEM-SMOKER         TO BN70F1-PAR-SMOKER.
034700*
034800     IF (BN70F1-PAR-SEX = SPACES)
034900         MOVE PEM-SEX            TO BN70F1-PAR-SEX.
035000*
035100     IF (BN70F1-PAR-FICA-NBR = SPACES)
035200         MOVE EMP-FICA-NBR       TO BN70F1-PAR-FICA-NBR.
035300*
035400     IF (BN70F1-PAR-BIRTHDATE = ZEROS)
035500         MOVE PEM-BIRTHDATE      TO BN70F1-PAR-BIRTHDATE.
035600*
035700     IF  (BN70F1-PAR-HM-PHONE-CNTRY = SPACES)
035800     AND (BN70F1-PAR-HM-PHONE-NBR   = SPACES)
035900         MOVE PEM-HM-PHONE-CNTRY TO BN70F1-PAR-HM-PHONE-CNTRY
036000         MOVE PEM-HM-PHONE-NBR   TO BN70F1-PAR-HM-PHONE-NBR.
036100*
HIPAA      IF (BN70F1-PAR-MEDICARE-IND    = SPACES)
HIPAA          MOVE PEM-MEDICARE-IND   TO BN70F1-PAR-MEDICARE-IND.
HIPAA *   
036200 234-END.
036300*
036400******************************************************************
036500 236-DFLT-EMD-INFO.
036600******************************************************************
036700*
036800     IF (BN70F1-PAR-SEX = SPACES)
036900         MOVE EMD-SEX            TO BN70F1-PAR-SEX.
037000*
037100     IF (BN70F1-PAR-FICA-NBR = SPACES)
037200         MOVE EMD-FICA-NBR       TO BN70F1-PAR-FICA-NBR.
037300*
037400     IF (BN70F1-PAR-BIRTHDATE = ZEROS)
037500         MOVE EMD-BIRTHDATE      TO BN70F1-PAR-BIRTHDATE.
037600*
037700     IF  (BN70F1-PAR-HM-PHONE-CNTRY = SPACES)
037800     AND (BN70F1-PAR-HM-PHONE-NBR   = SPACES)
037900         MOVE EMD-HM-PHONE-CNTRY TO BN70F1-PAR-HM-PHONE-CNTRY
038000         MOVE EMD-HM-PHONE-NBR   TO BN70F1-PAR-HM-PHONE-NBR.
038100*
HIPAA      IF (BN70F1-PAR-MEDICARE-IND    = SPACES)
HIPAA          MOVE EMD-MEDICARE-IND   TO BN70F1-PAR-MEDICARE-IND.
HIPAA *  
038200 236-END.
038300*
038400******************************************************************
038500 400-PROCESS-TRAN.
038600******************************************************************
038700
038800     IF (BN70F1-FC = "A")
038900         PERFORM 410-ADD
039000         THRU    410-END
039100     ELSE
039200     IF (BN70F1-FC = "C")
039300         PERFORM 420-CHANGE
039400         THRU    420-END
039500     ELSE
039600     IF (BN70F1-FC = "D")
039700         PERFORM 440-DELETE
039800         THRU    440-END
039900     ELSE
040000     IF (BN70F1-FC = "I" OR "N" OR "P")
040100         PERFORM 480-INQUIRE
040200         THRU    480-END.
040300
040400 400-END.
040500
040600******************************************************************
040700 410-ADD.
040800******************************************************************
040900*
041000     PERFORM 910-AUDIT-BEGIN.
041100     IF (DMS-ABORTED)
041200         GO TO 410-END.
041300*
041400     PERFORM 840-MODIFY-BNCSET1.
041500
041600     MOVE ZEROS                  TO BN70WS-PARTICIPNT.
041700
041800     IF (BNC-AUTO-PART = "Y")
041900         MOVE BNC-LAST-PART      TO DB-PARTICIPNT
042000                                    BN70WS-PARTICIPNT
042100         PERFORM 850-FIND-NLT-PARSET1
042200         MOVE WS-FALSE           TO BN70WS-NUMBER-SW
042300         PERFORM 412-GET-PART-NUMBER
042400         THRU    412-END
042500             UNTIL (BN70WS-NUMBER-FOUND).
042600
042700     IF (BN70F1-PAR-PARTICIPNT = ZEROS)
042800         MOVE BN70WS-PARTICIPNT  TO BN70F1-PAR-PARTICIPNT.
042900
043000     IF (BN70WS-PARTICIPNT       NOT = ZEROS)
043100         MOVE BN70WS-PARTICIPNT  TO BNC-LAST-PART
043200     ELSE
043300         IF (BN70F1-PAR-PARTICIPNT   > BNC-LAST-PART)
043400             MOVE BN70F1-PAR-PARTICIPNT   TO BNC-LAST-PART.
043500*
043600     PERFORM 800-CREATE-PARTICIPNT.
043700*
043800     PERFORM 500-MOVE-DATA
043900     THRU    500-END.
044000*
044100     PERFORM 820-STORE-PARTICIPNT.
044200*
044300     PERFORM 820-STORE-BNCOMPANY.
044400*
044500     MOVE CRT-ADD-COMPLETE                    TO CRT-MESSAGE.
044600     PERFORM 920-AUDIT-END.
044700     PERFORM 481-MOVE-TO-SCREEN
044800     THRU    481-END.
044900*
045000 410-END.
045100*
045200******************************************************************
045300 412-GET-PART-NUMBER.
045400******************************************************************
045500
045600     IF (BN70WS-PARTICIPNT       = ZEROS)
045700         ADD 1                   TO BN70WS-PARTICIPNT
045800     ELSE
045900         IF (PARTICIPNT-NOTFOUND)
046000         OR (PAR-COMPANY         NOT = DB-COMPANY)
046100             MOVE WS-TRUE        TO BN70WS-NUMBER-SW
046200         ELSE
046300             IF (PAR-PARTICIPNT  NOT = BN70WS-PARTICIPNT)
046400                 MOVE WS-TRUE    TO BN70WS-NUMBER-SW
046500             ELSE
046600                 ADD 1           TO BN70WS-PARTICIPNT
046700                 PERFORM 860-FIND-NEXT-PARSET1.
046800
046900 412-END.
047000
047100******************************************************************
047200 420-CHANGE.
047300******************************************************************
047400
047500     PERFORM 910-AUDIT-BEGIN.
047600     IF (DMS-ABORTED)
047700         GO TO 420-END.
047800
047900     MOVE BN70F1-PAR-COMPANY     TO DB-COMPANY.
048000     MOVE BN70F1-PAR-PARTICIPNT  TO DB-PARTICIPNT.
048100
048200     PERFORM 840-MODIFY-PARSET1.
048300
048400     PERFORM 500-MOVE-DATA
048500     THRU    500-END.
048600
048700     PERFORM 820-STORE-PARTICIPNT.
048800
048900     MOVE CRT-CHG-COMPLETE                    TO CRT-MESSAGE.
049000     PERFORM 920-AUDIT-END.
049100     PERFORM 481-MOVE-TO-SCREEN
049200     THRU    481-END.
049300
049400 420-END.
049500
049600******************************************************************
049700 440-DELETE.
049800******************************************************************
049900
050000     PERFORM 910-AUDIT-BEGIN.
050100     IF (DMS-ABORTED)
050200         GO TO 440-END.
050300
050400     MOVE BN70F1-PAR-COMPANY     TO DB-COMPANY.
050500     MOVE BN70F1-PAR-PARTICIPNT  TO DB-PARTICIPNT.
050600
050700     PERFORM 840-MODIFY-PARSET1.
050800
050900     MOVE SPACES                 TO DB-LETTER-CODE.
051000     MOVE ZEROS                  TO DB-EMPLOYEE
051100                                    DB-DEPENDENT.
051200     MOVE BN70F1-PAR-PARTICIPNT  TO DB-PARTICIPNT.
051300     MOVE ZEROS                  TO DB-DATE
051400                                    DB-SEQ-NBR.
051500     PERFORM 850-MODIFY-NLT-LTTSET1.
051600
051700     PERFORM 444-DELETE-LETTERTEXT
051800     THRU    444-END
051900         UNTIL (LETTERTEXT-NOTFOUND)
052000         OR    (LTT-LETTER-CODE NOT = SPACES)
052100         OR    (LTT-EMPLOYEE    NOT = ZEROS)
052200         OR    (LTT-DEPENDENT   NOT = ZEROS)
052300         OR    (LTT-PARTICIPNT  NOT = DB-PARTICIPNT).
052400
052500     MOVE SPACES                 TO DB-LETTER-CODE.
052600     MOVE ZEROS                  TO DB-EMPLOYEE
052700                                    DB-DEPENDENT.
052800     MOVE BN70F1-PAR-PARTICIPNT  TO DB-PARTICIPNT.
052900     MOVE ZEROS                  TO DB-DATE.
053000     PERFORM 850-MODIFY-NLT-LTCSET1.
053100
053200     PERFORM 446-DELETE-LETTERCODE
053300     THRU    446-END
053400         UNTIL (LETTERCODE-NOTFOUND)
053500         OR    (LTC-LETTER-CODE NOT = SPACES)
053600         OR    (LTC-EMPLOYEE    NOT = ZEROS)
053700         OR    (LTC-DEPENDENT   NOT = ZEROS)
053800         OR    (LTC-PARTICIPNT  NOT = DB-PARTICIPNT).
053900
054000     PERFORM 830-DELETE-PARTICIPNT.
054100
054200     MOVE CRT-RECS-DELETED                    TO CRT-MESSAGE.
054300     PERFORM 920-AUDIT-END.
054400
054500 440-END.
054600
054700******************************************************************
054800 444-DELETE-LETTERTEXT.
054900******************************************************************
055000
055100     PERFORM 830-DELETE-LETTERTEXT.
055200     PERFORM 860-MODIFY-NEXT-LTTSET1.
055300 444-END.
055400
055500******************************************************************
055600 446-DELETE-LETTERCODE.
055700******************************************************************
055800
055900     PERFORM 830-DELETE-LETTERCODE.
056000     PERFORM 860-MODIFY-NEXT-LTCSET1.
056100 446-END.
056200
056300******************************************************************
056400 480-INQUIRE.
056500******************************************************************
056600
056700     PERFORM 481-MOVE-TO-SCREEN
056800     THRU    481-END.
056900     MOVE CRT-INQ-COMPLETE                    TO CRT-MESSAGE.
057000
057100 480-END.
057200
057300******************************************************************
057400 481-MOVE-TO-SCREEN.
057500******************************************************************
057600*
057700     MOVE BN70F1-PAR-PARTICIPNT  TO BN70F1-PT-PAR-PARTICIPNT.
           INITIALIZE                     BN70F1-PT-XMIT-NBR1.
057800*
057900     MOVE PAR-COMPANY            TO BN70F1-PAR-COMPANY.
058000     MOVE PAR-PARTICIPNT         TO BN70F1-PAR-PARTICIPNT.
058100     MOVE PAR-LAST-NAME          TO BN70F1-PAR-LAST-NAME.
058200     MOVE PAR-FIRST-NAME         TO BN70F1-PAR-FIRST-NAME.
058300     MOVE PAR-MIDDLE-INIT        TO BN70F1-PAR-MIDDLE-INIT.
058400     MOVE PAR-EMPLOYEE           TO BN70F1-PAR-EMPLOYEE.
058500     MOVE PAR-DEPENDENT          TO BN70F1-PAR-DEPENDENT.
058600     MOVE PAR-ADDR1              TO BN70F1-PAR-ADDR1.
058700     MOVE PAR-ADDR2              TO BN70F1-PAR-ADDR2.
058800     MOVE PAR-ADDR3              TO BN70F1-PAR-ADDR3.
058900     MOVE PAR-ADDR4              TO BN70F1-PAR-ADDR4.
059000     MOVE PAR-CITY               TO BN70F1-PAR-CITY.
059100     MOVE PAR-STATE              TO BN70F1-PAR-STATE.
059200     MOVE PAR-ZIP                TO BN70F1-PAR-ZIP.
059300     MOVE PAR-COUNTRY-CODE       TO BN70F1-PAR-COUNTRY-CODE.
059400     MOVE PAR-HM-PHONE-CNTRY     TO BN70F1-PAR-HM-PHONE-CNTRY.
059500     MOVE PAR-HM-PHONE-NBR       TO BN70F1-PAR-HM-PHONE-NBR.
059600     MOVE PAR-FICA-NBR           TO BN70F1-PAR-FICA-NBR.
059700     MOVE PAR-BIRTHDATE          TO BN70F1-PAR-BIRTHDATE.
059800     MOVE PAR-SEX                TO BN70F1-PAR-SEX.
059900     MOVE PAR-SMOKER             TO BN70F1-PAR-SMOKER.
           MOVE PAR-CONSENT            TO BN70F1-PAR-CONSENT.
060000     MOVE PAR-OCCUR-TYPE         TO BN70F1-PAR-OCCUR-TYPE.
060100     MOVE PAR-OCCUR-DATE         TO BN70F1-PAR-OCCUR-DATE.
060200     MOVE PAR-EVENT-DATE         TO BN70F1-PAR-EVENT-DATE.
060300     MOVE PAR-TERM-DATE          TO BN70F1-PAR-TERM-DATE.
HIPAA      MOVE PAR-MEDICARE-IND       TO BN70F1-PAR-MEDICARE-IND.
060400*
060500     MOVE PRS-NAME               TO BN70F1-PRS-NAME.
060600*
060700     MOVE SPACES                 TO BN70F1-EMP-NAME.
060800*
060900     IF (PAR-EMPLOYEE NOT = ZEROS)
061000         PERFORM 1900-PAR-EMP-EMPLOYEE
P04521         THRU    1900-EMP-END
061100         IF (EMPLOYEE-FOUND)
061200             MOVE EMP-LAST-NAME      TO HRWS-LAST-NAME
061300             MOVE EMP-FIRST-NAME     TO HRWS-FIRST-NAME
061400             MOVE EMP-MIDDLE-INIT    TO HRWS-MIDDLE-INIT
                   MOVE EMP-NAME-SUFFIX    TO HRWS-NAME-SUFFIX
                   MOVE EMP-LAST-NAME-PRE  TO HRWS-LAST-NAME-PRE
061500             PERFORM 750-HR-FORMAT-NAME
061600             MOVE HRWS-FORMAT-NAME   TO BN70F1-EMP-NAME.
061700*
061800     MOVE SPACES                     TO BN70F1-PCO-DESCRIPTION.
061900*
062000     IF (PAR-DEPENDENT NOT = ZEROS)
062100         MOVE PAR-EMPLOYEE       TO DB-EMPLOYEE
062200         MOVE PAR-DEPENDENT      TO DB-SEQ-NBR
062300         PERFORM 840-FIND-EMDSET1
062400         IF (EMDEPEND-FOUND)
062500             MOVE "DP"               TO DB-TYPE
062600             MOVE EMD-REL-CODE       TO DB-CODE
062700             PERFORM 840-FIND-PCOSET1
062800             IF (PCODES-FOUND)
062900                 MOVE PCO-DESCRIPTION   TO BN70F1-PCO-DESCRIPTION
063000             ELSE
063100                 MOVE 100            TO CRT-MSG-NBR
063200                 PERFORM 790-GET-MSG
063300                 MOVE CRT-MESSAGE    TO BN70F1-PCO-DESCRIPTION.
063400*
PO4521     PERFORM 1900-PAR-OCC-OCCUR-TYPE
P04521     THRU    1900-OCC-END.
063600     IF (OCCURTYPE-FOUND)
063700         MOVE OCC-DESC           TO BN70F1-OCC-DESC
063800     ELSE
063900         MOVE 100                TO CRT-MSG-NBR
064000         PERFORM 790-GET-MSG
064100         MOVE CRT-MESSAGE        TO BN70F1-OCC-DESC.
064200*
064300     IF (BN70WS-RES-EXISTS)
064400         MOVE 130                TO CRT-MSG-NBR
064500         PERFORM 790-GET-MSG.
064600*
064700 481-END.
064800*
064900******************************************************************
065000 500-MOVE-DATA.
065100******************************************************************
065200*
065300     MOVE BN70F1-PAR-COMPANY         TO PAR-COMPANY.
065400     MOVE BN70F1-PAR-PARTICIPNT      TO PAR-PARTICIPNT.
065500     MOVE BN70F1-PAR-LAST-NAME       TO PAR-LAST-NAME.
065600     MOVE BN70F1-PAR-FIRST-NAME      TO PAR-FIRST-NAME.
065700     MOVE BN70F1-PAR-MIDDLE-INIT     TO PAR-MIDDLE-INIT.
065800     MOVE BN70F1-PAR-EMPLOYEE        TO PAR-EMPLOYEE.
065900     MOVE BN70F1-PAR-DEPENDENT       TO PAR-DEPENDENT.
066000     MOVE BN70F1-PAR-ADDR1           TO PAR-ADDR1.
066100     MOVE BN70F1-PAR-ADDR2           TO PAR-ADDR2.
066200     MOVE BN70F1-PAR-ADDR3           TO PAR-ADDR3.
066300     MOVE BN70F1-PAR-ADDR4           TO PAR-ADDR4.
066400     MOVE BN70F1-PAR-CITY            TO PAR-CITY.
066500     MOVE BN70F1-PAR-STATE           TO PAR-STATE.
066600     MOVE BN70F1-PAR-ZIP             TO PAR-ZIP.
066700     MOVE BN70F1-PAR-COUNTRY-CODE    TO PAR-COUNTRY-CODE.
066800     MOVE BN70F1-PAR-HM-PHONE-CNTRY  TO PAR-HM-PHONE-CNTRY.
066900     MOVE BN70F1-PAR-HM-PHONE-NBR    TO PAR-HM-PHONE-NBR.
067000     MOVE BN70F1-PAR-FICA-NBR        TO PAR-FICA-NBR.
067100     MOVE BN70F1-PAR-BIRTHDATE       TO PAR-BIRTHDATE.
067200     MOVE BN70F1-PAR-SEX             TO PAR-SEX.
067300     MOVE BN70F1-PAR-SMOKER          TO PAR-SMOKER.
           MOVE BN70F1-PAR-CONSENT         TO PAR-CONSENT.
067400     MOVE BN70F1-PAR-OCCUR-TYPE      TO PAR-OCCUR-TYPE.
067500     MOVE BN70F1-PAR-OCCUR-DATE      TO PAR-OCCUR-DATE.
067600     MOVE BN70F1-PAR-EVENT-DATE      TO PAR-EVENT-DATE.
HIPAA      MOVE BN70F1-PAR-MEDICARE-IND    TO PAR-MEDICARE-IND.
067700*
067800     PERFORM 502-CALC-TERM-DATE
067900     THRU    502-END.
068000*
068100     IF  (PAR-REFUSE-DATE NOT = ZEROS)
068200     AND (PAR-REFUSE-DATE < PAR-TERM-DATE)
068300         MOVE PAR-REFUSE-DATE    TO PAR-TERM-DATE.
068400
068500     IF  (PAR-CANCEL-DATE NOT = ZEROS)
068600     AND (PAR-CANCEL-DATE < PAR-TERM-DATE)
068700         MOVE PAR-CANCEL-DATE    TO PAR-TERM-DATE.
068800
068900     IF  (PAR-DIF-PLN-DATE NOT = ZEROS)
069000     AND (PAR-DIF-PLN-DATE < PAR-TERM-DATE)
069100         MOVE PAR-DIF-PLN-DATE    TO PAR-TERM-DATE.
069200
069300     IF  (PAR-MEDIC-DATE NOT = ZEROS)
069400     AND (PAR-MEDIC-DATE < PAR-TERM-DATE)
069500         MOVE PAR-MEDIC-DATE    TO PAR-TERM-DATE.
069600
069700     IF  (PAR-DEFLT-DATE NOT = ZEROS)
069800     AND (PAR-DEFLT-DATE < PAR-TERM-DATE)
069900         MOVE PAR-DEFLT-DATE    TO PAR-TERM-DATE.
J72310
J72310     IF  (BN70F1-FC = "A")
J72310         MOVE WS-SYSTEM-DATE-YMD     TO PAR-CREATE-DATE
J72310         MOVE HHMMSS                 TO PAR-CREATE-TIME
J72310         MOVE CRT-USER-NAME          TO PAR-CREATE-USER-ID
J72310     END-IF.
J72310
J72310     IF  (BN70F1-FC = "A" OR "C")
J72310         MOVE WS-SYSTEM-DATE-YMD     TO PAR-DATE-STAMP
J72310         MOVE HHMMSS                 TO PAR-TIME-STAMP
J72310         MOVE CRT-USER-NAME          TO PAR-USER-ID
J72310     END-IF.
J72310
070000*
070100 500-END.
070200*
070300******************************************************************
070400 502-CALC-TERM-DATE.
070500******************************************************************
070600
070700     IF (BN70F1-PAR-EVENT-DATE NOT = ZEROS)
070800         MOVE BN70F1-PAR-OCCUR-DATE  TO WSDR-FR-DATE
070900         PERFORM 900-DATE-TO-JULIAN
071000         ADD BNC-NOT-EVENT           TO WSDR-JULIAN-DAYS
071100         PERFORM 900-JULIAN-TO-DATE
071200         IF (BN70F1-PAR-EVENT-DATE > WSDR-FR-DATE)
071300             MOVE BN70F1-PAR-OCCUR-DATE  TO PAR-TERM-DATE
071400             GO TO 502-END.
071500
071600     MOVE BN70F1-PAR-OCCUR-DATE      TO BN70WS-DATE.
071700
071800     MOVE BN70WS-MNTH                TO BN70WS-MONTH.
071900
072000     IF  (PAR-OCCUR-DATE2 = ZEROS)
072100         COMPUTE BN70WS-MONTH        = BN70WS-MONTH
072200                                     + BN70WS-OCC-MONTHS-EXT
072300     ELSE
072400         IF  (PAR-EVENT-DATE2 NOT = ZEROS)
072500         AND (BN70F1-FC           = "C")
072600             MOVE PAR-OCCUR-DATE2    TO WSDR-FR-DATE
072700             PERFORM 900-DATE-TO-JULIAN
072800             ADD BNC-NOT-EVENT       TO WSDR-JULIAN-DAYS
072900             PERFORM 900-JULIAN-TO-DATE
073000             IF  (PAR-EVENT-DATE2     > WSDR-FR-DATE)
073100                 COMPUTE BN70WS-MONTH = BN70WS-MONTH
073200                                      + BN70WS-OCC-MONTHS-EXT
073300             ELSE
073400                 IF  (BN70WS-OCC-MONTHS-EXT
073500                                          + BN70WS-OCC-MONTHS-EXT2
073600                                          > BNC-CBR-MAX-MNTH)
073700                     COMPUTE BN70WS-MONTH = BN70WS-MONTH
073800                                          + BNC-CBR-MAX-MNTH
073900                 ELSE
074000                     COMPUTE BN70WS-MONTH = BN70WS-MONTH
074100                                          + BN70WS-OCC-MONTHS-EXT
074200                                          + BN70WS-OCC-MONTHS-EXT2
074300         ELSE
074400             IF  (BN70WS-OCC-MONTHS-EXT + BN70WS-OCC-MONTHS-EXT2
074500                                        > BNC-CBR-MAX-MNTH)
074600                 COMPUTE BN70WS-MONTH   = BN70WS-MONTH
074700                                        + BNC-CBR-MAX-MNTH
074800             ELSE
074900                 COMPUTE BN70WS-MONTH   = BN70WS-MONTH
075000                                        + BN70WS-OCC-MONTHS-EXT
075100                                        + BN70WS-OCC-MONTHS-EXT2.
075200
075300
075400     PERFORM 504-CALC-MONTH-YEAR
075500     THRU    504-END
075600         UNTIL (BN70WS-MONTH < 13).
075700
076700     MOVE BN70WS-MONTH           TO BN70WS-MNTH.
076800
076900     MOVE BN70WS-DATE            TO WSDR-FR-DATE.
           PERFORM 900-IS-DATE-INVALID.
           IF  (WSDR-DATE-ERROR-EXISTS)
               PERFORM 5000-SET-TO-EOM.
               
077000     PERFORM 900-DATE-TO-JULIAN.
077100     SUBTRACT 1                  FROM WSDR-JULIAN-DAYS.
077200     PERFORM 900-JULIAN-TO-DATE.
077300     MOVE WSDR-FR-DATE           TO BN70WS-DATE.  
077400     MOVE BN70WS-DATE            TO PAR-TERM-DATE.
077500
077600 502-END.
077700
077800******************************************************************
077900 504-CALC-MONTH-YEAR.
078000******************************************************************
078100     SUBTRACT 12                 FROM BN70WS-MONTH.
078200     ADD 1                       TO BN70WS-YEAR.
078300
078400 504-END.
078500
078600******************************************************************
078700 1900-PAR-BNC-COMPANY.
078800******************************************************************
078900
079000     MOVE BN70F1-PAR-COMPANY     TO DB-COMPANY.
079100     PERFORM 840-FIND-BNCSET1.
079200
P04521 1900-BNC-END.
079300******************************************************************
079400 1900-PAR-PRS-COMPANY-NAME.
079500******************************************************************
079600
079700     MOVE BN70F1-PAR-COMPANY     TO DB-COMPANY.
079800     MOVE SPACES                 TO DB-PROCESS-LEVEL.
079900     PERFORM 840-FIND-PRSSET1.
080000
P04521 1900-PRS-END.
080100******************************************************************
080200 1905-PAR-PTB-PART-BENEFIT.
080300******************************************************************
080400
080500     MOVE PAR-COMPANY            TO DB-COMPANY.
080600     MOVE ZEROS                  TO DB-EMPLOYEE.
080700     MOVE PAR-PARTICIPNT         TO DB-PARTICIPNT.
080800     MOVE SPACES                 TO DB-PLAN-TYPE.
080900     MOVE SPACES                 TO DB-PLAN-CODE.
081000     MOVE ZEROS                  TO DB-START-DATE.
081100     PERFORM 850-FIND-NLT-PTBSET5.
081200
081300     IF (PARTBEN-NOTFOUND)
081400     OR (PTB-COMPANY    NOT = DB-COMPANY)
081500     OR (PTB-EMPLOYEE   NOT = DB-EMPLOYEE)
081600     OR (PTB-PARTICIPNT NOT = DB-PARTICIPNT)
081700         MOVE WS-TRUE            TO PARTBEN-SW.
081800
P04521 1905-PTB-END.
081900******************************************************************
082000 1900-PAR-EMP-EMPLOYEE.
082100******************************************************************
082200
082300     MOVE BN70F1-PAR-COMPANY     TO DB-COMPANY.
082400     MOVE BN70F1-PAR-EMPLOYEE    TO DB-EMPLOYEE.
082500     PERFORM 840-FIND-EMPSET1.
082600
P04521 1900-EMP-END.
082700******************************************************************
082800 1900-PAR-PEM-PAEMPLOYEE.
082900******************************************************************
083000
083100     MOVE BN70F1-PAR-COMPANY     TO DB-COMPANY.
083200     MOVE BN70F1-PAR-EMPLOYEE    TO DB-EMPLOYEE.
083300     PERFORM 840-FIND-PEMSET1.
083400
P04521 1900-PEM-END.
083500******************************************************************
083600 1900-PAR-EMD-DEPENDENT.
083700******************************************************************
083800
083900     MOVE BN70F1-PAR-COMPANY     TO DB-COMPANY.
084000     MOVE BN70F1-PAR-EMPLOYEE    TO DB-EMPLOYEE.
084100     MOVE BN70F1-PAR-DEPENDENT   TO DB-SEQ-NBR.
084200     PERFORM 840-FIND-EMDSET1.
084300
P04521 1900-EMD-END.
084400******************************************************************
084500 1900-PAR-OCC-OCCUR-TYPE.
084600******************************************************************
084700
084800     MOVE BN70F1-PAR-COMPANY     TO DB-COMPANY.
084900     MOVE BN70F1-PAR-OCCUR-TYPE  TO DB-OCCUR-TYPE.
085000     PERFORM 840-FIND-OCCSET1.
085100
085200
P04521 1900-OCC-END.
085300******************************************************************
085400 BN70S1-TRANSACTION-END.
085500******************************************************************
ACS001***********ADDED FOR BN70_A PROCESSFLOW******PEGGYLEE MOEHLMANN***
ACS001***********EMAIL ON A CODE 6 OR A CODE 12 ************************
ACS001
ACS001     PERFORM 1000-OPEN-WORKFLOW-DB.
ACS001
ACS001     MOVE BN70F1-PAR-COMPANY        TO WFAPI-CRITERION-1.
ACS001     MOVE "COMPANY"                 TO WFAPI-I-VARIABLE-NAME (1).
ACS001     MOVE BN70F1-PAR-COMPANY        TO WFAPI-I-VARIABLE-VAL  (1).
ACS001     MOVE "PARTICIPNT"              TO WFAPI-I-VARIABLE-NAME (2).
ACS001     MOVE BN70F1-PAR-PARTICIPNT     TO WFAPI-I-VARIABLE-VAL  (2).
ACS001     MOVE "EMPLOYEE"                TO WFAPI-I-VARIABLE-NAME (3).
ACS001     MOVE BN70F1-PAR-EMPLOYEE       TO WFAPI-I-VARIABLE-VAL  (3).
ACS001     MOVE "OCCURTYPE"               TO WFAPI-I-VARIABLE-NAME (4).
ACS001     MOVE BN70F1-PAR-OCCUR-TYPE     TO WFAPI-I-VARIABLE-VAL  (4).
ACS001     MOVE "NTUSER"                  TO WFAPI-I-VARIABLE-NAME (5).
ACS001     MOVE CRT-USER-NAME             TO WFAPI-I-VARIABLE-VAL  (5).
ACS001
ACS001     PERFORM 1000-PROCESS-FLOW.
ACS001
ACS001**************END OF PROCESSFLOW BN70_A **************************
085600
085700******************************************************************
085800 BN70S2-TRANSACTION              SECTION 47.
085900******************************************************************
086000 BN70S2-START.
086100
086200     PERFORM 200-EDIT-TRAN
086300     THRU    200-END.
086400
086500     IF (NO-ERROR-FOUND)
086600         PERFORM 400-PROCESS-TRAN
086700         THRU    400-END.
086800
086900     GO TO BN70S2-TRANSACTION-END.
087000
087100******************************************************************
087200 200-EDIT-TRAN.
087300******************************************************************
087400
087500     PERFORM 210-EDIT-ACCESS
087600     THRU    210-END.
087700
087800     IF (ERROR-FOUND)
087900         GO TO 200-END.
088000
088100     IF (BN70F2-FC = "A" OR "C")
088200         PERFORM 230-EDIT-DATA
088300         THRU    230-END
088400         GO TO 200-END.
088500
088600 200-END.
088700
088800******************************************************************
088900 210-EDIT-ACCESS.
089000******************************************************************
089100
P04521     PERFORM 1900-PAR-PRS-COMPANY-NAME
P04521     THRU    1900-PRS-END.
089300     IF (PRSYSTEM-NOTFOUND)
089400         MOVE 52                              TO CRT-ERROR-NBR
089500         MOVE BN70F2-PAR-COMPANY-FN           TO CRT-FIELD-NBR
089600         GO TO 210-END.
089700
P04521     PERFORM 1900-PAR-BNC-COMPANY
P04521     THRU    1900-BNC-END.
089900     IF (BNCOMPANY-NOTFOUND)
090000         MOVE 53                              TO CRT-ERROR-NBR
090100         MOVE BN70F2-PAR-COMPANY-FN           TO CRT-FIELD-NBR
090200         GO TO 210-END.
090300
090400     MOVE BN70F2-PAR-COMPANY     TO DB-COMPANY.
090500     MOVE BN70F2-PAR-PARTICIPNT  TO DB-PARTICIPNT.
090600
090700     PERFORM 840-FIND-PARSET1.
090800
090900     IF (BN70F2-FC = "N")
091000         IF (PARTICIPNT-FOUND)
091100             PERFORM 850-FIND-NLT-PARSET1
091200             PERFORM 860-FIND-NEXT-PARSET1
091300         ELSE
091400             PERFORM 850-FIND-NLT-PARSET1.
091500
091600     IF (BN70F2-FC = "P")
091700         PERFORM 850-FIND-NLT-PARSET1
091800         PERFORM 870-FIND-PREV-PARSET1.
091900
092000     IF (BN70F2-FC = "N" OR "P")
092100         IF (PARTICIPNT-NOTFOUND)
092200         OR (PAR-COMPANY NOT = BN70F2-PAR-COMPANY)
092300             MOVE 11                          TO CRT-ERROR-NBR
092400             MOVE BN70F2-FC-FN                TO CRT-FIELD-NBR
092500             GO TO 210-END
092600         ELSE
092700             MOVE PAR-PARTICIPNT TO BN70F2-PAR-PARTICIPNT
092800                                    DB-PARTICIPNT.
092900
093000     IF (PARTICIPNT-NOTFOUND)
093100         IF (BN70F2-FC = "A")
093200             MOVE 203                             TO CRT-ERROR-NBR
093300             MOVE BN70F2-PAR-PARTICIPNT-FN        TO CRT-FIELD-NBR
093400             GO TO 210-END
093500         ELSE
093600             IF (BN70F2-FC = "I" OR "C")
093700                 MOVE 202                         TO CRT-ERROR-NBR
093800                 MOVE BN70F2-PAR-PARTICIPNT-FN    TO CRT-FIELD-NBR
093900                 GO TO 210-END.
094000
094100 210-END.
094200
094300******************************************************************
094400 230-EDIT-DATA.
094500******************************************************************
094600
094700     IF  (BN70F2-PAR-RIGHTS-DATE < PAR-OCCUR-DATE)
094800     AND (BN70F2-PAR-RIGHTS-DATE NOT = ZEROS)
094900         MOVE 212                             TO CRT-ERROR-NBR
095000         MOVE BN70F2-PAR-RIGHTS-DATE-FN       TO CRT-FIELD-NBR
095100         GO TO 230-END.
095200
095300     IF  (BN70F2-PAR-ELECT-DATE < PAR-OCCUR-DATE)
095400     AND (BN70F2-PAR-ELECT-DATE NOT = ZEROS)
095500         MOVE 213                             TO CRT-ERROR-NBR
095600         MOVE BN70F2-PAR-ELECT-DATE-FN        TO CRT-FIELD-NBR
095700         GO TO 230-END.
095800
095900     IF  (BN70F2-PAR-DELINQ-DATE < PAR-OCCUR-DATE)
096000     AND (BN70F2-PAR-DELINQ-DATE NOT = ZEROS)
096100         MOVE 214                             TO CRT-ERROR-NBR
096200         MOVE BN70F2-PAR-DELINQ-DATE-FN       TO CRT-FIELD-NBR
096300         GO TO 230-END.
096400
096500     IF  (BN70F2-PAR-REFUSE-DATE NOT = ZEROS)
096600     AND (BN70F2-PAR-REFUSE-DATE < PAR-OCCUR-DATE)
096700         MOVE 206                                 TO CRT-ERROR-NBR
096800         MOVE BN70F2-PAR-REFUSE-DATE-FN           TO CRT-FIELD-NBR
096900         GO TO 230-END.
097000
097100     IF  (BN70F2-PAR-CANCEL-DATE NOT = ZEROS)
097200     AND (BN70F2-PAR-CANCEL-DATE < PAR-OCCUR-DATE)
097300         MOVE 207                                 TO CRT-ERROR-NBR
097400         MOVE BN70F2-PAR-CANCEL-DATE-FN           TO CRT-FIELD-NBR
097500         GO TO 230-END.
097600
097700     IF  (BN70F2-PAR-DIF-PLN-DATE NOT = ZEROS)
097800     AND (BN70F2-PAR-DIF-PLN-DATE < PAR-OCCUR-DATE)
097900         MOVE 208                                 TO CRT-ERROR-NBR
098000         MOVE BN70F2-PAR-DIF-PLN-DATE-FN          TO CRT-FIELD-NBR
098100         GO TO 230-END.
098200
098300     IF  (BN70F2-PAR-MEDIC-DATE NOT = ZEROS)
098400     AND (BN70F2-PAR-MEDIC-DATE < PAR-OCCUR-DATE)
098500         MOVE 210                                TO CRT-ERROR-NBR
098600         MOVE BN70F2-PAR-MEDIC-DATE-FN           TO CRT-FIELD-NBR
098700         GO TO 230-END.
098800
098900     IF  (BN70F2-PAR-DEFLT-DATE NOT = ZEROS)
099000     AND (BN70F2-PAR-DEFLT-DATE < PAR-OCCUR-DATE)
099100         MOVE 211                                TO CRT-ERROR-NBR
099200         MOVE BN70F2-PAR-DEFLT-DATE-FN           TO CRT-FIELD-NBR
099300         GO TO 230-END.
099400
099500     MOVE PAR-OCCUR-TYPE         TO DB-OCCUR-TYPE.
099600     PERFORM 840-FIND-OCCSET1.
099700
099800     IF (OCCURTYPE-NOTFOUND)
099900         MOVE 204                             TO CRT-ERROR-NBR
100000         MOVE BN70F2-FC-FN                    TO CRT-FIELD-NBR
100100         GO TO 230-END
100200     ELSE
100300         MOVE OCC-MONTHS-EXT          TO BN70WS-OCC-MONTHS-EXT
100400         IF (BN70F2-PAR-OCCUR-TYPE NOT = SPACES)
100500             MOVE BN70F2-PAR-OCCUR-TYPE     TO DB-OCCUR-TYPE
100600             PERFORM 840-FIND-OCCSET1
100700             IF (OCCURTYPE-NOTFOUND)
100800                 MOVE 205                     TO CRT-ERROR-NBR
100900                 MOVE BN70F2-FC-FN            TO CRT-FIELD-NBR
101000                 GO TO 230-END
101100             ELSE
101200                 MOVE OCC-MONTHS-EXT  TO BN70WS-OCC-MONTHS-EXT2.
101300
101400 230-END.
101500
101600******************************************************************
101700 400-PROCESS-TRAN.
101800******************************************************************
101900
102000     IF (BN70F2-FC = "A" OR "C")
102100         PERFORM 420-CHANGE
102200         THRU    420-END
102300     ELSE
102400     IF (BN70F2-FC = "I" OR "N" OR "P")
102500         PERFORM 480-INQUIRE
102600         THRU    480-END.
102700
102800 400-END.
102900
103000******************************************************************
103100 420-CHANGE.
103200******************************************************************
103300
103400     PERFORM 910-AUDIT-BEGIN.
103500     IF (DMS-ABORTED)
103600         GO TO 420-END.
103700
103800     MOVE BN70F2-PAR-COMPANY     TO DB-COMPANY.
103900     MOVE BN70F2-PAR-PARTICIPNT  TO DB-PARTICIPNT.
104000
104100     PERFORM 840-MODIFY-PARSET1.
104200
104300     PERFORM 500-MOVE-DATA
104400     THRU    500-END.
104500
104600     PERFORM 820-STORE-PARTICIPNT.
104700
104800     IF (BN70F2-FC = "A")
104900         MOVE CRT-ADD-COMPLETE                TO CRT-MESSAGE
105000     ELSE
105100         MOVE CRT-CHG-COMPLETE                TO CRT-MESSAGE.
105200
105300     PERFORM 920-AUDIT-END.
105400     PERFORM 481-MOVE-TO-SCREEN
105500     THRU    481-END.
105600
105700 420-END.
105800
105900******************************************************************
106000 480-INQUIRE.
106100******************************************************************
106200
106300     PERFORM 481-MOVE-TO-SCREEN
106400     THRU    481-END.
106500     MOVE CRT-INQ-COMPLETE                    TO CRT-MESSAGE.
106600
106700 480-END.
106800
106900******************************************************************
107000 481-MOVE-TO-SCREEN.
107100******************************************************************
107200
107300     MOVE PAR-COMPANY            TO BN70F2-PAR-COMPANY.
107400     MOVE PAR-PARTICIPNT         TO BN70F2-PAR-PARTICIPNT.
107500     MOVE PAR-RIGHTS-DATE        TO BN70F2-PAR-RIGHTS-DATE.
107600     MOVE PAR-ELECT-DATE         TO BN70F2-PAR-ELECT-DATE.
107700     MOVE PAR-DELINQ-DATE        TO BN70F2-PAR-DELINQ-DATE.
107800     MOVE PAR-REFUSE-DATE        TO BN70F2-PAR-REFUSE-DATE.
107900     MOVE PAR-CANCEL-DATE        TO BN70F2-PAR-CANCEL-DATE.
108000     MOVE PAR-DIF-PLN-DATE       TO BN70F2-PAR-DIF-PLN-DATE.
108100     MOVE PAR-MEDIC-DATE         TO BN70F2-PAR-MEDIC-DATE.
108200     MOVE PAR-DEFLT-DATE         TO BN70F2-PAR-DEFLT-DATE.
108300     MOVE PAR-EMPLOYEE           TO BN70F2-PAR-EMPLOYEE.
108400     MOVE PAR-OCCUR-TYPE2        TO BN70F2-PAR-OCCUR-TYPE.
108500     MOVE PAR-OCCUR-DATE2        TO BN70F2-PAR-OCCUR-DATE.
108600
108700     MOVE PRS-NAME               TO BN70F2-PRS-NAME.
108800
108900     MOVE PAR-LAST-NAME          TO HRWS-LAST-NAME.
109000     MOVE PAR-FIRST-NAME         TO HRWS-FIRST-NAME.
109100     MOVE PAR-MIDDLE-INIT        TO HRWS-MIDDLE-INIT.
109200     PERFORM 750-HR-FORMAT-NAME.
109300     MOVE HRWS-FORMAT-NAME       TO BN70F2-PAR-NAME.
109400
109500     MOVE SPACES                 TO BN70F2-EMP-NAME.
109600
109700     IF (PAR-EMPLOYEE NOT = ZEROS)
109800         MOVE PAR-EMPLOYEE           TO DB-EMPLOYEE
109900         PERFORM 840-FIND-EMPSET1
110000         IF (EMPLOYEE-FOUND)
110100             MOVE EMP-LAST-NAME      TO HRWS-LAST-NAME
110200             MOVE EMP-FIRST-NAME     TO HRWS-FIRST-NAME
110300             MOVE EMP-MIDDLE-INIT    TO HRWS-MIDDLE-INIT
                   MOVE EMP-NAME-SUFFIX    TO HRWS-NAME-SUFFIX
                   MOVE EMP-LAST-NAME-PRE  TO HRWS-LAST-NAME-PRE
110400             PERFORM 750-HR-FORMAT-NAME
110500             MOVE HRWS-FORMAT-NAME   TO BN70F2-EMP-NAME.
110600
110700     MOVE PAR-OCCUR-TYPE2            TO DB-OCCUR-TYPE.
110800     PERFORM 840-FIND-OCCSET1.
110900     IF (OCCURTYPE-FOUND)
111000         MOVE OCC-DESC               TO BN70F2-OCC-DESC
111100     ELSE
111200         MOVE SPACES                 TO BN70F2-OCC-DESC.
111300
111400 481-END.
111500
111600******************************************************************
111700 500-MOVE-DATA.
111800******************************************************************
111900
112000     MOVE BN70F2-PAR-COMPANY      TO PAR-COMPANY.
112100     MOVE BN70F2-PAR-PARTICIPNT   TO PAR-PARTICIPNT.
112200     MOVE BN70F2-PAR-RIGHTS-DATE  TO PAR-RIGHTS-DATE.
112300     MOVE BN70F2-PAR-ELECT-DATE   TO PAR-ELECT-DATE.
112400     MOVE BN70F2-PAR-DELINQ-DATE  TO PAR-DELINQ-DATE.
112500     MOVE BN70F2-PAR-REFUSE-DATE  TO PAR-REFUSE-DATE.
112600     MOVE BN70F2-PAR-CANCEL-DATE  TO PAR-CANCEL-DATE.
112700     MOVE BN70F2-PAR-DIF-PLN-DATE TO PAR-DIF-PLN-DATE.
112800     MOVE BN70F2-PAR-MEDIC-DATE   TO PAR-MEDIC-DATE.
112900     MOVE BN70F2-PAR-DEFLT-DATE   TO PAR-DEFLT-DATE.
113000
113100     PERFORM 502-CALC-TERM-DATE
113200     THRU    502-END.
113300
113400     IF  (PAR-REFUSE-DATE NOT = ZEROS)
113500     AND (PAR-REFUSE-DATE < PAR-TERM-DATE)
113600         MOVE PAR-REFUSE-DATE    TO PAR-TERM-DATE.
113700
113800     IF  (PAR-CANCEL-DATE NOT = ZEROS)
113900     AND (PAR-CANCEL-DATE < PAR-TERM-DATE)
114000         MOVE PAR-CANCEL-DATE    TO PAR-TERM-DATE.
114100
114200     IF  (PAR-DIF-PLN-DATE NOT = ZEROS)
114300     AND (PAR-DIF-PLN-DATE < PAR-TERM-DATE)
114400         MOVE PAR-DIF-PLN-DATE   TO PAR-TERM-DATE.
114500
114600     IF  (PAR-MEDIC-DATE NOT = ZEROS)
114700     AND (PAR-MEDIC-DATE < PAR-TERM-DATE)
114800         MOVE PAR-MEDIC-DATE     TO PAR-TERM-DATE.
114900
115000     IF  (PAR-DEFLT-DATE NOT = ZEROS)
115100     AND (PAR-DEFLT-DATE < PAR-TERM-DATE)
115200         MOVE PAR-DEFLT-DATE     TO PAR-TERM-DATE.
115300*
115400     MOVE BN70F2-PAR-OCCUR-TYPE  TO PAR-OCCUR-TYPE2.
115500     MOVE BN70F2-PAR-OCCUR-DATE  TO PAR-OCCUR-DATE2.
J39300*
J39300*    IF  (BN70F2-FC = "A")
J39300*        MOVE WS-SYSTEM-DATE-YMD     TO PAR-CREATE-DATE
J39300*        MOVE HHMMSS                 TO PAR-CREATE-TIME
J39300*        MOVE CRT-USER-NAME          TO PAR-CREATE-USER-ID
J39300*    END-IF.
J72310
J72310     IF  (BN70F2-FC = "A" OR "C")
J72310         MOVE WS-SYSTEM-DATE-YMD     TO PAR-DATE-STAMP
J72310         MOVE HHMMSS                 TO PAR-TIME-STAMP
J72310         MOVE CRT-USER-NAME          TO PAR-USER-ID
J72310     END-IF.
J72310
115600
115700 500-END.
115800*
115900******************************************************************
116000 502-CALC-TERM-DATE.
116100******************************************************************
116200
116300     IF (PAR-EVENT-DATE NOT = ZEROS)
116400         MOVE PAR-OCCUR-DATE         TO WSDR-FR-DATE
116500         PERFORM 900-DATE-TO-JULIAN
116600         ADD BNC-NOT-EVENT           TO WSDR-JULIAN-DAYS
116700         PERFORM 900-JULIAN-TO-DATE
116800         IF (PAR-EVENT-DATE > WSDR-FR-DATE)
116900             MOVE PAR-OCCUR-DATE     TO PAR-TERM-DATE
117000             GO TO 502-END.
117100
117200     MOVE PAR-OCCUR-DATE             TO BN70WS-DATE.
117300
117400     MOVE BN70WS-MNTH                TO BN70WS-MONTH.
117500
117600     IF (BN70F2-PAR-OCCUR-DATE = ZEROS)
117700         ADD BN70WS-OCC-MONTHS-EXT   TO BN70WS-MONTH
117800     ELSE
117900         IF (BN70F2-PAR-EVENT-DATE NOT = ZEROS)
118000             MOVE BN70F2-PAR-OCCUR-DATE        TO WSDR-FR-DATE
118100             PERFORM 900-DATE-TO-JULIAN
118200             ADD BNC-NOT-EVENT           TO WSDR-JULIAN-DAYS
118300             PERFORM 900-JULIAN-TO-DATE
118400             IF (BN70F2-PAR-EVENT-DATE > WSDR-FR-DATE)
118500                 ADD BN70WS-OCC-MONTHS-EXT   TO BN70WS-MONTH
118600             ELSE
118700                 IF (BN70WS-OCC-MONTHS-EXT
118800                                          + BN70WS-OCC-MONTHS-EXT2
118900                                          > BNC-CBR-MAX-MNTH)
119000                     COMPUTE BN70WS-MONTH = BN70WS-MONTH
119100                                          + BNC-CBR-MAX-MNTH
119200                 ELSE
119300                     COMPUTE BN70WS-MONTH = BN70WS-MONTH
119400                                          + BN70WS-OCC-MONTHS-EXT
119500                                         + BN70WS-OCC-MONTHS-EXT2
119600         ELSE
119700             IF (BN70WS-OCC-MONTHS-EXT + BN70WS-OCC-MONTHS-EXT2
119800                                              > BNC-CBR-MAX-MNTH)
119900                 COMPUTE BN70WS-MONTH     = BN70WS-MONTH
120000                                          + BNC-CBR-MAX-MNTH
120100             ELSE
120200                 COMPUTE BN70WS-MONTH     = BN70WS-MONTH
120300                                          + BN70WS-OCC-MONTHS-EXT
120400                                         + BN70WS-OCC-MONTHS-EXT2.
120500
120600     PERFORM 504-CALC-MONTH-YEAR
120700     THRU    504-END
120800         UNTIL (BN70WS-MONTH < 13).
120900
121900     MOVE BN70WS-MONTH           TO BN70WS-MNTH.
122000
122100     MOVE BN70WS-DATE            TO WSDR-FR-DATE.
           PERFORM 900-IS-DATE-INVALID.
           IF  (WSDR-DATE-ERROR-EXISTS)
               PERFORM 5000-SET-TO-EOM.
               
122200     PERFORM 900-DATE-TO-JULIAN.
122300     SUBTRACT 1                  FROM WSDR-JULIAN-DAYS.
122400     PERFORM 900-JULIAN-TO-DATE.
122500     MOVE WSDR-FR-DATE           TO BN70WS-DATE.  
122600     MOVE BN70WS-DATE            TO PAR-TERM-DATE.
122700
122800 502-END.
122900
123000******************************************************************
123100 504-CALC-MONTH-YEAR.
123200******************************************************************
123300     SUBTRACT 12                 FROM BN70WS-MONTH.
123400     ADD 1                       TO BN70WS-YEAR.
123500
123600 504-END.
123700
123800******************************************************************
123900 1900-PAR-BNC-COMPANY.
124000******************************************************************
124100
124200     MOVE BN70F2-PAR-COMPANY     TO DB-COMPANY.
124300     PERFORM 840-FIND-BNCSET1.
124400
P04521 1900-BNC-END.
124500******************************************************************
124600 1900-PAR-PRS-COMPANY-NAME.
124700******************************************************************
124800
124900     MOVE BN70F2-PAR-COMPANY     TO DB-COMPANY.
125000     MOVE SPACES                 TO DB-PROCESS-LEVEL.
125100     PERFORM 840-FIND-PRSSET1.
125200
P04521 1900-PRS-END.
125300******************************************************************
125400 BN70S2-TRANSACTION-END.
125500******************************************************************
125600
