******* BN65PD 2.1.3 <2906031103>
000100******************************************************************
000200*Should we remove BuySellFlag                                    *
000300******************************************************************
000400******************************************************************
000500 BN65S1-TRANSACTION              SECTION 42.
000600******************************************************************
000700 BN65S1-START.
000800
000900     PERFORM 200-EDIT-TRAN
001000     THRU    200-END.
001100
001200     IF (NO-ERROR-FOUND)
001300         PERFORM 400-PROCESS-TRAN
001400         THRU    400-END.
001500
001600     GO TO BN65S1-TRANSACTION-END.
001700
001800******************************************************************
001900 200-EDIT-TRAN.
002000******************************************************************
002100
002200     PERFORM 210-EDIT-ACCESS
002300     THRU    210-END.
002400
002500     IF (ERROR-FOUND)
002600         GO TO 200-END.
002700
002800     IF (BN65F1-FC                   = "A" OR "C")
002900         PERFORM 230-EDIT-DATA
003000         THRU    230-END
003100         GO TO 200-END.
003200
003300 200-END.
003400
003500******************************************************************
003600 210-EDIT-ACCESS.
003700******************************************************************
003800
003900     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
004000     INITIALIZE DB-PROCESS-LEVEL.
004100     PERFORM 840-FIND-PRSSET1.
004200     IF (PRSYSTEM-NOTFOUND)
004300******** Company does not exist
004400         MOVE 52                     TO CRT-ERROR-NBR
004500         MOVE BN65F1-BSH-COMPANY-FN  TO CRT-FIELD-NBR
004600         GO TO 210-END.
004700
004800     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
004900     PERFORM 840-FIND-BNCSET1.
005000     IF (BNCOMPANY-NOTFOUND)
005100******** Company does not exist in BNCOMPANY
005200         MOVE 53                     TO CRT-ERROR-NBR
005300         MOVE BN65F1-BSH-COMPANY-FN  TO CRT-FIELD-NBR
005400         GO TO 210-END.
005500
005600     MOVE PRS-NAME                   TO BNWS-PRS-NAME.
005700
005800     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
005900     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
006000     PERFORM 840-FIND-EMPSET1.
006100     IF (EMPLOYEE-NOTFOUND)
006200******** Employee does not exist
006300         MOVE 50                     TO CRT-ERROR-NBR
006400         MOVE BN65F1-BSH-EMPLOYEE-FN TO CRT-FIELD-NBR
006500         GO TO 210-END.
006600
006700     IF (EMPLOYEE-FOUND)
006800         MOVE EMP-COMPANY            TO CRT-COMPANY
006900         MOVE EMP-PROCESS-LEVEL      TO CRT-PROCESS-LEVEL
007000         PERFORM 700-HR-EMP-SECURITY
007100         IF (HRWS-EMP-SECURED)
                   MOVE 142                    TO CRT-ERROR-NBR
007200             MOVE BN65F1-BSH-EMPLOYEE-FN TO CRT-FIELD-NBR
007300             GO TO 210-END.
007400
007500     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
007600     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
007700     PERFORM 840-FIND-PEMSET1.
007800     IF (PAEMPLOYEE-NOTFOUND)
007900******** Employee does not exist
008000         MOVE 50                     TO CRT-ERROR-NBR
008100         MOVE BN65F1-BSH-EMPLOYEE-FN TO CRT-FIELD-NBR
008200         GO TO 210-END.
008300
008400     IF (BN65F1-PT-BSH-COMPANY       NOT = BN65F1-BSH-COMPANY)
008500     OR (BN65F1-PT-BSH-EMPLOYEE      NOT = BN65F1-BSH-EMPLOYEE)
008600     OR (BN65F1-PT-BSH-DATE          NOT = BN65F1-BSH-DATE)
008700         PERFORM 212-CALC-FLEX-DOL
008800         THRU    212-END.
008900
009000     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
009100     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
009200     PERFORM 840-FIND-BSHSET1.
009300     IF  (BNSIMHDR-NOTFOUND)
009400     AND (BN65F1-FC                  = "C" OR "D")
009500******** Simulation record does not exist
009600         MOVE 100                    TO CRT-ERROR-NBR
009700         MOVE BN65F1-FC-FN           TO CRT-FIELD-NBR
009800         GO TO 210-END.
009900
010000     IF  (BNSIMHDR-FOUND)
010100     AND (BN65F1-FC                  = "A")
010200******** Simulation record already exist
010300         MOVE 101                    TO CRT-ERROR-NBR
010400         MOVE BN65F1-FC-FN           TO CRT-FIELD-NBR
010500         GO TO 210-END.
010600
010700 210-END.
010800
010900******************************************************************
011000 212-CALC-FLEX-DOL.
011100******************************************************************
011200
011300     INITIALIZE BN65F1-FLD-FLEX-PLAN
011400                BN65WS-FF-ERROR-NBR
011500                BN65WS-FF-ERROR-CAT
011600                BN65WS-FF-ERR-VAR1
011700                BN65WS-FF-ERR-VAR2
011800                BN65WS-FF-ERR-VAR3
011900                BN65WS-FD-ERROR-NBR
012000                BN65WS-FD-ERROR-CAT
012100                BN65WS-FD-ERR-VAR1
012200                BN65WS-FD-ERR-VAR2
012300                BN65WS-FD-ERR-VAR3.
012400
012500     MOVE BN65F1-BSH-COMPANY         TO BNFFWS-COMPANY.
012600     MOVE BN65F1-BSH-EMPLOYEE        TO BNFFWS-EMPLOYEE.
012700     PERFORM 5000-FIND-FLEXPLAN-70.
012800     IF (ERROR-FOUND)
012900         MOVE CRT-ERROR-NBR          TO BN65WS-FF-ERROR-NBR
013000         MOVE CRT-ERROR-CAT          TO BN65WS-FF-ERROR-CAT
013100         MOVE CRT-ERR-VAR1           TO BN65WS-FF-ERR-VAR1
013200         MOVE CRT-ERR-VAR2           TO BN65WS-FF-ERR-VAR2
013300         MOVE CRT-ERR-VAR3           TO BN65WS-FF-ERR-VAR3
013400         INITIALIZE CRT-ERROR-NBR
013500                    CRT-ERROR-CAT
013600                    CRT-ERR-VAR1
013700                    CRT-ERR-VAR2
013800                    CRT-ERR-VAR3
013900     ELSE
014000******** FIND FLEX DOLLAR RECORD
014100         MOVE BN65F1-BSH-COMPANY     TO BNREWS-COMPANY
014200         MOVE BNFFWS-FLEX-PLAN       TO BNREWS-PLAN-CODE
014300         MOVE BN65F1-BSH-EMPLOYEE    TO BNREWS-EMPLOYEE
014400         MOVE BN65F1-BSH-DATE        TO BNREWS-AS-OF-DATE
014500         MOVE "FLD"                  TO BNREWS-FILE-PREFIX
014600         PERFORM 5000-FIND-RECS-BY-EMP-GROUP-70
014700         IF (ERROR-FOUND)
014800              MOVE CRT-ERROR-NBR     TO BN65WS-FD-ERROR-NBR
014900              MOVE CRT-ERROR-CAT     TO BN65WS-FD-ERROR-CAT
015000              MOVE CRT-ERR-VAR1      TO BN65WS-FD-ERR-VAR1
015100              MOVE CRT-ERR-VAR2      TO BN65WS-FD-ERR-VAR2
015200              MOVE CRT-ERR-VAR3      TO BN65WS-FD-ERR-VAR3
015300             INITIALIZE CRT-ERROR-NBR
015400                        CRT-ERROR-CAT
015500                        CRT-ERR-VAR1
015600                        CRT-ERR-VAR2
015700                        CRT-ERR-VAR3
015800         ELSE
015900             MOVE FLD-FLEX-PLAN      TO BN65F1-FLD-FLEX-PLAN
016000             PERFORM 520-CALCULATE-DOLLARS
016100             THRU    520-END.
016200
016300 212-END.
016400
016500******************************************************************
016600 230-EDIT-DATA.
016700******************************************************************
016800
016900     IF (BN65F1-FC                   = "A")
017000         COMPUTE BN65WS-TOTAL-REMAIN = BNFDWS-FLEX-CREDITS
017100                                     + BNFDWS-PT-DOLLARS
017200     ELSE
017300         COMPUTE BN65WS-TOTAL-REMAIN = BSH-COMP-FLX-AMT
017400                                     + BSH-EMP-FLEX-AMT
017500                                     - BSH-COMP-SPENT
017600                                     - BSH-EMP-SPENT.
017700
017800     PERFORM 260-EDIT-DTL-TRAN
017900     THRU    260-END
018000         VARYING I1 FROM 1 BY 1
018100         UNTIL  (I1 > 11)
018200         OR     (ERROR-FOUND).
018300
018400 230-END.
018500
018600******************************************************************
018700 260-EDIT-DTL-TRAN.
018800******************************************************************
018900
019000     IF (BN65F1-LINE-FC (I1)         = SPACES)
019100         GO TO 260-END.
019200
019300     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
019400     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
019500     MOVE BN65F1-BSD-PLAN-TYPE (I1)  TO DB-PLAN-TYPE.
019600     MOVE BN65F1-BSD-PLAN-CODE (I1)  TO DB-PLAN-CODE.
019700     PERFORM 840-FIND-BSDSET1.
019800     IF  (BNSIMDTL-NOTFOUND)
019900     AND (BN65F1-LINE-FC (I1)        = "C" OR "D")
020000******** Simulation detail record does not exist
020100         MOVE 102                    TO CRT-ERROR-NBR
020200         MOVE BN65F1-LINE-FC-FN (I1) TO CRT-FIELD-NBR
020300         GO TO 260-END.
020400
020500     IF  (BNSIMDTL-FOUND)
020600     AND (BN65F1-LINE-FC (I1)        = "A")
020700******** Simulation detail record already exist
020800         MOVE 103                    TO CRT-ERROR-NBR
020900         MOVE BN65F1-LINE-FC-FN (I1) TO CRT-FIELD-NBR
021000         GO TO 260-END.
021100
021200     IF (BN65F1-LINE-FC (I1)         = "A" OR "C")
021300         PERFORM 264-EDIT-DTL-DATA
021400         THRU    264-END.
021500
021600     IF (BN65F1-LINE-FC (I1)         = "D")
021700         PERFORM 280-EDIT-DTL-DELETE
021800         THRU    280-END.
021900
022000 260-END.
022100
022200******************************************************************
022300 264-EDIT-DTL-DATA.
022400******************************************************************
022500
022600     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
022700     MOVE BN65F1-BSD-PLAN-TYPE (I1)  TO DB-PLAN-TYPE.
022800     MOVE BN65F1-BSD-PLAN-CODE (I1)  TO DB-PLAN-CODE.
022900     PERFORM 840-FIND-PLNSET1.
023000     IF (PLAN-NOTFOUND)
023100******** Plan does not exist
023200         MOVE 127                    TO CRT-ERROR-NBR
023300         MOVE BN65F1-LINE-FC-FN (I1) TO CRT-FIELD-NBR
023400         GO TO 264-END.
023500
023600     IF  (BN65F1-BSD-FLEX-FLAG (I1)   = "Y")
023700     AND (BN65F1-FLD-FLEX-PLAN        = SPACES)
023800         IF (BN65WS-FF-ERROR-NBR      NOT = ZEROES)
023900             MOVE BN65WS-FF-ERROR-NBR TO CRT-ERROR-NBR
024000             MOVE BN65WS-FF-ERROR-CAT TO CRT-ERROR-CAT
024100             MOVE BN65WS-FF-ERR-VAR1  TO CRT-ERR-VAR1
024200             MOVE BN65WS-FF-ERR-VAR2  TO CRT-ERR-VAR2
024300             MOVE BN65WS-FF-ERR-VAR3  TO CRT-ERR-VAR3
024400         ELSE
024500         IF (BN65WS-FD-ERROR-NBR      NOT = ZEROES)
024600             MOVE BN65WS-FD-ERROR-NBR TO CRT-ERROR-NBR
024700             MOVE BN65WS-FD-ERROR-CAT TO CRT-ERROR-CAT
024800             MOVE BN65WS-FD-ERR-VAR1  TO CRT-ERR-VAR1
024900             MOVE BN65WS-FD-ERR-VAR2  TO CRT-ERR-VAR2
025000             MOVE BN65WS-FD-ERR-VAR3  TO CRT-ERR-VAR3
025100         END-IF
025200         END-IF
025300         MOVE BN65F1-LINE-FC-FN (I1)  TO CRT-FIELD-NBR
025400         GO TO 264-END.
025500
025600     IF (PLN-COVERAGE-TYPE           = "0")
025700         IF (BN65F1-BSD-COV-OPTION (I1)
025800                                     NOT = ZEROES)
025900************ Cannot enter coverage option for zero coverage type
026000             MOVE 133                TO CRT-ERROR-NBR
026100             MOVE BN65F1-BSD-COV-OPTION-FN (I1)
026200                                     TO CRT-FIELD-NBR
026300         END-IF
026400         IF (BN65F1-BSD-MULTIPLE (I1)
026500                                     NOT = ZEROES)
026600************ Cannot enter multiple for zero coverage type
026700             MOVE 134                TO CRT-ERROR-NBR
026800             MOVE BN65F1-BSD-MULTIPLE-FN (I1)
026900                                     TO CRT-FIELD-NBR
027000         END-IF
027100         IF (ERROR-FOUND)
027200             GO TO 264-END.
027300
027400     IF (PLN-CONTRIB-TYPE            = "0")
027500         IF (BN65F1-BSD-EMP-PRE-CONT (I1)
027600                                     NOT = ZEROES)
027700************ Cannot enter contribution for zero contribution type
027800             MOVE 136                TO CRT-ERROR-NBR
027900             MOVE BN65F1-BSD-EMP-PRE-CONT-FN (I1)
028000                                     TO CRT-FIELD-NBR
028100         END-IF
028200         IF (BN65F1-BSD-EMP-AFT-CONT (I1)
028300                                     NOT = ZEROES)
028400************ Cannot enter contribution for zero contribution type
028500             MOVE 136                TO CRT-ERROR-NBR
028600             MOVE BN65F1-BSD-EMP-AFT-CONT-FN (I1)
028700                                     TO CRT-FIELD-NBR
028800         END-IF
028900         IF (ERROR-FOUND)
029000             GO TO 264-END.
029100
029200     IF (PLN-COVERAGE-TYPE           NOT = "0")
029300         PERFORM 266-CALL-COVERAGE
029400         THRU    266-END
029500         IF (ERROR-FOUND)
029600             GO TO 264-END.
029700
029800     IF (PLN-CONTRIB-TYPE            NOT = "0")
029900         PERFORM 268-CALL-CONTRIBUTION
030000         THRU    268-END
030100         IF (ERROR-FOUND)
030200             GO TO 264-END.
030300
030400     IF (BN65F1-BSD-FLEX-FLAG (I1)         = "Y")
030500         IF (BN65F1-BSD-EMP-PRE-CONT (I1)  > ZEROES)
030600             PERFORM 270-CHECK-AVAILABLE
030700             THRU    270-END
030800             IF (ERROR-FOUND)
030900                 GO TO 264-END
031000             END-IF
031100         ELSE
031200         IF  (BN65F1-BSD-EMP-PRE-CONT (I1) < ZEROES)
031300         AND (PLN-FLEX-PAY-CODE            = SPACES)
031400             COMPUTE BN65WS-TOTAL-REMAIN   = BN65WS-TOTAL-REMAIN
031500                                   + (BN65F1-BSD-EMP-PRE-CONT (I1)
031600                                   * NEGATIVE-ONE).
031700
031800 264-END.
031900
032000******************************************************************
032100 266-CALL-COVERAGE.
032200******************************************************************
032300
032400     IF (PLN-COVERAGE-TYPE               = "2")
032500         MOVE BN65F1-BSH-COMPANY         TO BNREWS-COMPANY
032600         MOVE BN65F1-BSD-PLAN-TYPE (I1)  TO BNREWS-PLAN-TYPE
032700         MOVE BN65F1-BSD-PLAN-CODE (I1)  TO BNREWS-PLAN-CODE
032800         MOVE "E"                        TO BNREWS-COVER-TYPE
032900         MOVE BN65F1-BSH-EMPLOYEE        TO BNREWS-EMPLOYEE
033000         MOVE BN65F1-BSH-DATE            TO BNREWS-AS-OF-DATE
033100         MOVE "CVR"                      TO BNREWS-FILE-PREFIX
033200         INITIALIZE                         BNREWS-FIND-ST-GN
033300         PERFORM 5000-FIND-RECS-BY-EMP-GROUP-70
033400         IF (ERROR-FOUND)
033500             MOVE BN65F1-LINE-FC-FN (I1) TO CRT-FIELD-NBR
033600             GO TO 266-END.
033700
P54241     INITIALIZE BN65WS-CVR-BEN-PCT.
033800     IF (CVR-BENEFIT-PCT              NOT = ZEROES)
033900         MOVE CVR-BENEFIT-PCT         TO BN65WS-CVR-BEN-PCT
034000         INITIALIZE CVR-BENEFIT-PCT.
034100
034200*
034300**** CALL COVERAGE ROUTINE FOR COVOPTION AND COVERAGE AMOUNT
034400*
034500     MOVE BN65F1-BSH-COMPANY         TO BNCVWS-COMPANY.
034600     MOVE BN65F1-BSD-PLAN-TYPE (I1)  TO BNCVWS-PLAN-TYPE.
034700     MOVE BN65F1-BSD-PLAN-CODE (I1)  TO BNCVWS-PLAN-CODE.
034800     MOVE "E"                        TO BNCVWS-COVER-TYPE.
034900     MOVE BN65F1-BSH-EMPLOYEE        TO BNCVWS-EMPLOYEE.
035000     MOVE BN65F1-BSH-DATE            TO BNCVWS-AS-OF-DATE
035100                                        BNCVWS-START-DATE.
035200     INITIALIZE BNCVWS-STOP-DATE
035300                BNCVWS-FC.
035400     MOVE BN65F1-BSD-COV-OPTION (I1) TO BNCVWS-IN-COVER-OPT.
035500     MOVE BN65F1-BSD-COVER-AMT  (I1) TO BNCVWS-IN-COVER-AMT.
035600     MOVE BN65F1-BSD-MULTIPLE (I1)   TO BNCVWS-MULT-OF-SALARY.
035700     IF  (PLN-COVERAGE-TYPE           = "1")
035800     OR  ((PLN-COVERAGE-TYPE          = "2")
035900     AND  ((CVR-CALC-TYPE             = "F")
036000     OR    (CVR-CALC-TYPE             = "S")))
036100         INITIALIZE BNCVWS-IN-ANNUAL-SALARY
036200     ELSE
036300         MOVE BN65F1-BSH-GROSS-PAY   TO BNCVWS-IN-ANNUAL-SALARY.
036400     PERFORM 5000-DO-COVERAGE-70.
036500     IF (ERROR-FOUND)
036600         IF (BNCVWS-FIELD-NBR        = 1)
036700             MOVE  BN65F1-BSD-COV-OPTION-FN (I1)
036800                                     TO CRT-FIELD-NBR
036900         ELSE
037000         IF (BNCVWS-FIELD-NBR        = 2)
037100             MOVE  BN65F1-BSD-MULTIPLE-FN (I1)
037200                                     TO CRT-FIELD-NBR
037300         ELSE
037400         IF (BNCVWS-FIELD-NBR        = 3)
037500             MOVE  BN65F1-BSD-COVER-AMT-FN (I1)
037600                                     TO CRT-FIELD-NBR
037700         ELSE
037800             MOVE BN65F1-LINE-FC-FN (I1)
037900                                     TO CRT-FIELD-NBR
038000         END-IF
038100         END-IF
038200         END-IF
038300         GO TO 266-END.
038400
038500     IF (BN65WS-CVR-BEN-PCT          NOT = ZEROES)
038600         PERFORM 267-CHECK-LIMIT-OF-BEN
038700         THRU    267-END
038800         IF (ERROR-FOUND)
038900             GO TO 266-END.
039000
039100 266-CONTINUE.
039200     MOVE BNCVWS-COVER-OPT       TO BN65F1-BSD-COV-OPTION (I1).
039300     MOVE BNCVWS-COVER-AMT       TO BN65F1-BSD-COVER-AMT  (I1).
039400     MOVE BNCVWS-MULT-OF-SALARY  TO BN65F1-BSD-MULTIPLE   (I1).
039500
039600 266-END.
039700
039800******************************************************************
039900 267-CHECK-LIMIT-OF-BEN.
040000******************************************************************
040100
040200     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
040300     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
040400     MOVE CVR-BN-PLAN-TYPE           TO DB-PLAN-TYPE.
040500     MOVE CVR-BN-PLAN-CODE           TO DB-PLAN-CODE.
040600     PERFORM 840-FIND-BSDSET1.
040700     IF (BNSIMDTL-NOTFOUND)
040800******** Limit is based on coverage in \ plan \
040900         MOVE 138                    TO CRT-ERROR-NBR
041000         MOVE BN65F1-LINE-FC-FN (I1) TO CRT-FIELD-NBR
041100         MOVE CVR-BN-PLAN-TYPE       TO CRT-ERR-VAR1
041200         MOVE CVR-BN-PLAN-CODE       TO CRT-ERR-VAR2
041300         GO TO 267-END.
041400
041500     COMPUTE BN65WS-MAX-COVER-AMT    = (BSD-COVER-AMT
041600                                     * (CVR-BENEFIT-PCT
041700                                     / 100)).
041800
041900     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
042000     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
042100     MOVE BN65F1-BSD-PLAN-TYPE (I1)  TO DB-PLAN-TYPE.
042200     MOVE BN65F1-BSD-PLAN-CODE (I1)  TO DB-PLAN-CODE.
042300     PERFORM 840-FIND-BSDSET1.
042400
042500     IF (BN65F1-BSD-COVER-AMT (I1)         > BN65WS-MAX-COVER-AMT)
042600******** Coverage is > amount based on other benefit
042700         MOVE 141                          TO CRT-ERROR-NBR
042800         MOVE BN65F1-BSD-COVER-AMT-FN (I1) TO CRT-FIELD-NBR
042900         GO TO 267-END.
043000
043100 267-END.
043200
043300******************************************************************
043400 268-CALL-CONTRIBUTION.
043500******************************************************************
043600
043700*
043800**** CALL CONTRIBUTION ROUTINE TO CALCULATE PRE AND AFT CONTS
043900*
044000     MOVE BN65F1-BSH-COMPANY         TO BNCTWS-COMPANY.
044100     MOVE BN65F1-BSD-PLAN-TYPE (I1)  TO BNCTWS-PLAN-TYPE.
044200     MOVE BN65F1-BSD-PLAN-CODE (I1)  TO BNCTWS-PLAN-CODE.
044300     MOVE "E"                        TO BNCTWS-COVER-TYPE.
044400     MOVE BN65F1-BSH-EMPLOYEE        TO BNCTWS-EMPLOYEE.
044500     MOVE BN65F1-BSH-DATE            TO BNCTWS-AS-OF-DATE.
044600     MOVE BN65F1-BSD-EMP-PRE-CONT (I1)  TO BNCTWS-IN-EMP-PT-CONT.
044700     MOVE BN65F1-BSD-EMP-AFT-CONT (I1)  TO BNCTWS-IN-EMP-AT-CONT.
044800     MOVE BN65F1-BSD-COV-OPTION (I1) TO BNCTWS-CONTRIB-OPT.
P08335    IF (BN65F1-BSD-PLAN-TYPE (I1)   = "VA")
P08335         MOVE BN65F1-BSD-COVER-AMT  (I1) TO BNCTWS-NBR-OF-HOURS
P08335     ELSE
P08335         MOVE BN65F1-BSD-COVER-AMT  (I1) TO BNCTWS-COVER-AMT.
P08335
045000     INITIALIZE BNCTWS-IN-PAY-PER-AMT.
045100     MOVE BN65F1-BSH-GROSS-PAY       TO BNCTWS-ANNUAL-SALARY.
045200     IF (PLN-CONTRIB-TYPE            = "5" OR "6" OR "7")
045300         MOVE BN65F1-BSD-COVER-AMT (I1) TO BNCTWS-IN-ANNUAL-AMT.
045400
P08335    IF (BN65F1-BSD-PLAN-TYPE (I1)   = "VA")
P08335        INITIALIZE BNCTWS-IN-ANNUAL-AMT.
P08335
045500     MOVE "A"                        TO BNCTWS-IN-PCT-AMT-FLAG.
045600     INITIALIZE BNCTWS-IN-PRE-AFT-FLAG
045700                BNCTWS-IN-SMOKER-SW
045800                BNCTWS-CYC-REMAIN.
045900     MOVE BN65F1-BSH-DATE            TO BNCTWS-START-DATE.
046000     INITIALIZE BNCTWS-STOP-DATE.
046300     INITIALIZE BNCTWS-FC.
046400     PERFORM 5000-DO-CONTRIBUTION-70.
046500     IF (ERROR-FOUND)
046600         IF (BNCTWS-FIELD-NBR        = 1)
046700             MOVE  BN65F1-BSD-COV-OPTION-FN (I1)
046800                                     TO CRT-FIELD-NBR
046900         ELSE
047000         IF (BNCTWS-FIELD-NBR        = 4)
047100             MOVE  BN65F1-BSD-COVER-AMT-FN (I1)
047200                                     TO CRT-FIELD-NBR
047300         ELSE
047400         IF (BNCTWS-FIELD-NBR        = 9)
047500             MOVE  BN65F1-BSD-EMP-PRE-CONT-FN (I1)
047600                                     TO CRT-FIELD-NBR
047700         ELSE
047800         IF (BNCTWS-FIELD-NBR        = 10)
047900             MOVE  BN65F1-BSD-EMP-AFT-CONT-FN (I1)
048000                                     TO CRT-FIELD-NBR
048100         ELSE
P08335         IF  (BNCTWS-FIELD-NBR        = 2)
P08335         AND (BN65F1-BSD-PLAN-TYPE (I1)   = "VA")
P08335              MOVE BN65F1-BSD-COVER-AMT-FN (I1)
P08335                                     TO CRT-FIELD-NBR
P08335         ELSE
048200             MOVE BN65F1-LINE-FC-FN (I1)
048300                                     TO CRT-FIELD-NBR
048400         END-IF
048500         END-IF
048600         END-IF
048700         END-IF
048800         GO TO 268-END.
048900
049000     MOVE BNCTWS-EMP-CONT        TO BN65F1-BSD-OPTION-COST (I1).
049100     MOVE BNCTWS-EMP-PT-CONT     TO BN65F1-BSD-EMP-PRE-CONT (I1).
049200     MOVE BNCTWS-EMP-AT-CONT     TO BN65F1-BSD-EMP-AFT-CONT (I1).
049300
049400     IF (PLN-CONTRIB-TYPE        = "5" OR "6" OR "7")
049500         MOVE BNCTWS-CYC-REMAIN  TO BN65F1-BSD-COV-OPTION (I1).
049600
049700 268-END.
049800
049900******************************************************************
050000 270-CHECK-AVAILABLE.
050100******************************************************************
050200
050300     IF (BN65F1-LINE-FC (I1)        = "A")
050400         MOVE BN65F1-BSD-EMP-PRE-CONT (I1)
050500                                    TO BN65WS-COST-DIFF
050600     ELSE
050700         COMPUTE BN65WS-COST-DIFF   = BN65F1-BSD-EMP-PRE-CONT (I1)
050800                                    - BSD-EMP-PRE-CONT.
050900
051000     IF (BN65WS-COST-DIFF           > BN65WS-TOTAL-REMAIN)
051100******** Cannot spend pre-tax; not enough dollars left
051200         MOVE 137                   TO CRT-ERROR-NBR
051300         MOVE BN65F1-BSD-EMP-PRE-CONT-FN (I1)
051400                                    TO CRT-FIELD-NBR
051500         GO TO 270-END.
051600
051700     COMPUTE BN65WS-TOTAL-REMAIN    = BN65WS-TOTAL-REMAIN
051800                                    - BN65WS-COST-DIFF.
051900
052000 270-END.
052100
052200******************************************************************
052300 280-EDIT-DTL-DELETE.
052400******************************************************************
052500
052600     IF (BN65F1-BSD-FLEX-FLAG  (I1)  = "Y")
052700         COMPUTE BN65WS-TOTAL-REMAIN = BN65WS-TOTAL-REMAIN
052800                                     + BSD-OPTION-COST.
052900
053000     IF (BN65F1-BSD-EMP-PRE-CONT (I1) > ZEROES)
053100         GO TO 280-END.
053200
053300     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
053400     MOVE BN65F1-BSD-PLAN-TYPE (I1)  TO DB-PLAN-TYPE.
053500     MOVE BN65F1-BSD-PLAN-CODE (I1)  TO DB-PLAN-CODE.
053600     PERFORM 840-FIND-PLNSET1.
053700     IF  (PLAN-FOUND)
053800     AND (PLN-FLEX-PAY-CODE          NOT = SPACES)
053900         GO TO 280-END.
054000
054100     MOVE BSH-COMP-FLX-AMT           TO BN65WS-COMPANY-DOLLARS.
054200     PERFORM 282-CHECK-SPENT-DOLLARS
054300     THRU    282-END
054400         VARYING I2 FROM 1 BY 1
054500         UNTIL  (I2 > 11).
054600
054700     IF ((BN65F1-BSD-EMP-PRE-CONT (I1) * NEGATIVE-ONE)  
054800                                     > BN65WS-COMPANY-DOLLARS)
054900******** Cannot delete: credits have been spent
055000         MOVE 131                    TO CRT-ERROR-NBR
055100         MOVE BN65F1-FC-FN           TO CRT-FIELD-NBR
055200         GO TO 280-END.
055300
055400     IF  (BSD-EMP-PRE-CONT           > ZEROES)
055500     OR  ((BSD-EMP-PRE-CONT          < ZEROES)
055600     AND  (PLN-FLEX-PAY-CODE         = SPACES))
055700         SUBTRACT BSD-EMP-PRE-CONT   FROM BN65WS-TOTAL-PRE-CONT.
055800
055900     SUBTRACT BSD-EMP-AFT-CONT       FROM BN65WS-TOTAL-AFT-CONT.
056000     SUBTRACT BSD-OPTION-COST       FROM BN65WS-TOTAL-OPTION-COST.
056100
056200 280-END.
056300
056400******************************************************************
056500 282-CHECK-SPENT-DOLLARS.
056600******************************************************************
056700
056800     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
056900     MOVE BN65F1-BSD-PLAN-TYPE (I2)  TO DB-PLAN-TYPE.
057000     MOVE BN65F1-BSD-PLAN-CODE (I2)  TO DB-PLAN-CODE.
057100     PERFORM 840-FIND-PLNSET1.
057200     IF  (PLAN-FOUND)
057300     AND (PLN-FLEX-PAY-CODE          = SPACES)
057400         SUBTRACT BN65F1-BSD-EMP-PRE-CONT (I2) 
057500                                     FROM BN65WS-COMPANY-DOLLARS.
057600            
057700 282-END.
057800
057900******************************************************************
058000 400-PROCESS-TRAN.
058100******************************************************************
058200
058300     IF (BN65F1-FC                   = "A")
058400         PERFORM 410-ADD
058500         THRU    410-END
058600         MOVE "I"                    TO BN65F1-FC
058700     ELSE
058800     IF (BN65F1-FC                   = "C")
058900         PERFORM 420-CHANGE
059000         THRU    420-END
059100     ELSE
059200     IF (BN65F1-FC                   = "D")
059300         PERFORM 440-DELETE
059400         THRU    440-END
059500     ELSE
059600     IF (BN65F1-FC                   = "I" OR "+" OR "-")
059700         PERFORM 480-INQUIRE
059800         THRU    480-END.
059900
060000 400-END.
060100
060200******************************************************************
060300 410-ADD.
060400******************************************************************
060500
060600     PERFORM 910-AUDIT-BEGIN.
060700     IF (DMS-ABORTED)
060800         GO TO 410-END.
060900
061000     PERFORM 800-CREATE-BNSIMHDR.
061100
061200     PERFORM 500-MOVE-DATA
061300     THRU    500-END.
061400
061500     PERFORM 422-PROCESS-DETAIL
061600     THRU    422-END
061700         VARYING I1 FROM 1 BY 1
061800         UNTIL  (I1 > 11).
061900
062000     PERFORM 820-STORE-BNSIMHDR.
062100
062200     MOVE CRT-ADD-COMPLETE           TO CRT-MESSAGE.
062300
062400     PERFORM 920-AUDIT-END.
062500
062600     PERFORM 481-MOVE-TO-SCREEN
062700     THRU    481-END.
062800
062900 410-END.
063000
063100******************************************************************
063200 420-CHANGE.
063300******************************************************************
063400
063500     PERFORM 910-AUDIT-BEGIN.
063600     IF (DMS-ABORTED)
063700         GO TO 420-END.
063800
063900     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
064000     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
064100
064200     PERFORM 840-MODIFY-BSHSET1.
064300
064400     IF (BN65F1-BSH-DATE             NOT = BSH-DATE)
064500     OR (BN65F1-BSH-GROSS-PAY        NOT = BSH-GROSS-PAY)
064600         PERFORM 600-DELETE-ALL-BNSIMDTL
064700         THRU    600-END.
064800
064900     PERFORM 500-MOVE-DATA
065000     THRU    500-END.
065100
065200     IF (BN65F1-BSH-DATE             NOT = BSH-DATE)
065300     OR (BN65F1-BSH-GROSS-PAY        NOT = BSH-GROSS-PAY)
065400         PERFORM 480-INQUIRE
065500         THRU    480-END
065600
065700         MOVE "C"                    TO BN65F1-FC
065800
065900         MOVE 120                    TO CRT-MSG-NBR
066000     ELSE
066100         PERFORM 422-PROCESS-DETAIL
066200         THRU    422-END
066300             VARYING I1 FROM 1 BY 1
066400             UNTIL  (I1 > 11)
066500
066600         PERFORM 481-MOVE-TO-SCREEN
066700         THRU    481-END
066800
066900         MOVE CRT-CHG-COMPLETE       TO CRT-MESSAGE.
067000
067100     PERFORM 820-STORE-BNSIMHDR.
067200
067300     PERFORM 920-AUDIT-END.
067400
067500 420-END.
067600
067700******************************************************************
067800 422-PROCESS-DETAIL.
067900******************************************************************
068000
068100     IF (BN65F1-LINE-FC (I1)         = SPACES)
068200         GO TO 422-END
068300     ELSE
068400     IF (BN65F1-LINE-FC (I1)         = "A")
068500         PERFORM 431-ADD-BSD
068600         THRU    431-END
068700     ELSE
068800     IF (BN65F1-LINE-FC (I1)         = "C")
068900         PERFORM 432-CHANGE-BSD
069000         THRU    432-END
069100     ELSE
069200     IF (BN65F1-LINE-FC (I1)         = "D")
069300         PERFORM 434-DELETE-BSD
069400         THRU    434-END.
069500
069600 422-END.
069700
069800******************************************************************
069900 431-ADD-BSD.
070000******************************************************************
070100
070200     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
070300     MOVE BN65F1-BSD-PLAN-TYPE (I1)  TO DB-PLAN-TYPE.
070400     MOVE BN65F1-BSD-PLAN-CODE (I1)  TO DB-PLAN-CODE.
070500     PERFORM 840-FIND-PLNSET1.
070600
070700     PERFORM 800-CREATE-BNSIMDTL.
070800
070900     IF (BN65F1-BSD-FLEX-FLAG (I1)   = "Y")
071000         PERFORM 436-CALC-DOLLARS-SPENT
071100         THRU    436-END.
071200
071300     PERFORM 510-MOVE-DTL-DATA
071400     THRU    510-END.
071500
071600     PERFORM 820-STORE-BNSIMDTL.
071700
071800     MOVE "*"                        TO BN65F1-SELECTED-FLAG (I1).
071900
072000     INITIALIZE BN65F1-LINE-FC (I1).
072100
072200     IF  (BSD-EMP-PRE-CONT           > ZEROES)
072300     OR  ((BSD-EMP-PRE-CONT          < ZEROES)
072400     AND  (PLN-FLEX-PAY-CODE         = SPACES))
072500         ADD BSD-EMP-PRE-CONT        TO BN65WS-TOTAL-PRE-CONT.
072600
072700     ADD BSD-EMP-AFT-CONT            TO BN65WS-TOTAL-AFT-CONT.
072800     ADD BSD-OPTION-COST             TO BN65WS-TOTAL-OPTION-COST.
072900
073000 431-END.
073100
073200******************************************************************
073300 432-CHANGE-BSD.
073400******************************************************************
073500
073600     IF  (BSD-EMP-PRE-CONT           > ZEROES)
073700     OR  ((BSD-EMP-PRE-CONT          < ZEROES)
073800     AND  (PLN-FLEX-PAY-CODE         = SPACES))
073900         SUBTRACT BSD-EMP-PRE-CONT   FROM BN65WS-TOTAL-PRE-CONT.
074000
074100     SUBTRACT BSD-EMP-AFT-CONT       FROM BN65WS-TOTAL-AFT-CONT.
074200     SUBTRACT BSD-OPTION-COST       FROM BN65WS-TOTAL-OPTION-COST.
074300
074400     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
074500     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
074600     MOVE BN65F1-BSD-PLAN-TYPE (I1)  TO DB-PLAN-TYPE.
074700     MOVE BN65F1-BSD-PLAN-CODE (I1)  TO DB-PLAN-CODE.
074800
074900     PERFORM 840-FIND-PLNSET1.
075000
075100     PERFORM 840-MODIFY-BSDSET1.
075200
075300     IF (BN65F1-BSD-FLEX-FLAG (I1)   = "Y")
075400         PERFORM 436-CALC-DOLLARS-SPENT
075500         THRU    436-END.
075600
075700     PERFORM 510-MOVE-DTL-DATA
075800     THRU    510-END.
075900
076000     PERFORM 820-STORE-BNSIMDTL.
076100
076200     INITIALIZE BN65F1-LINE-FC (I1).
076300
076400     IF  (BSD-EMP-PRE-CONT           > ZEROES)
076500     OR  ((BSD-EMP-PRE-CONT          < ZEROES)
076600     AND  (PLN-FLEX-PAY-CODE         = SPACES))
076700         ADD BSD-EMP-PRE-CONT        TO BN65WS-TOTAL-PRE-CONT.
076800
076900     ADD BSD-EMP-AFT-CONT            TO BN65WS-TOTAL-AFT-CONT.
077000     ADD BSD-OPTION-COST             TO BN65WS-TOTAL-OPTION-COST.
077100
077200 432-END.
077300
077400******************************************************************
077500 434-DELETE-BSD.
077600******************************************************************
077700
077800     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
077900     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
078000     MOVE BN65F1-BSD-PLAN-TYPE (I1)  TO DB-PLAN-TYPE.
078100     MOVE BN65F1-BSD-PLAN-CODE (I1)  TO DB-PLAN-CODE.
078200
078300     PERFORM 840-FIND-PLNSET1.
078400
078500     PERFORM 840-MODIFY-BSDSET1.
078600
078700     IF (BN65F1-BSD-FLEX-FLAG (I1)   = "Y")
078800         PERFORM 436-CALC-DOLLARS-SPENT
078900         THRU    436-END.
079000
079100     PERFORM 830-DELETE-BNSIMDTL.
079200
079300     INITIALIZE BN65F1-LINE-FC          (I1)
079400                BN65F1-SELECTED-FLAG    (I1)
079500                BN65F1-BSD-COV-OPTION   (I1)
079600                BN65F1-BSD-MULTIPLE     (I1)
079700                BN65F1-BSD-COVER-AMT    (I1)
079800                BN65F1-BSD-EMP-PRE-CONT (I1)
079900                BN65F1-BSD-EMP-AFT-CONT (I1)
080000                BN65F1-BSD-OPTION-COST  (I1).
080100
080200 434-END.
080300
080400******************************************************************
080500 436-CALC-DOLLARS-SPENT.
080600******************************************************************
080700
080800     IF (BN65F1-LINE-FC (I1)         = "C" OR "D")
080900         PERFORM 437-SUBTRACT-OLD-AMT
081000         THRU    437-END.
081100
081200     IF (BN65F1-LINE-FC (I1)         = "A" OR "C")
081300         PERFORM 438-ADD-NEW-AMT
081400         THRU    438-END.
081500
081600 436-END.
081700
081800******************************************************************
081900 437-SUBTRACT-OLD-AMT.
082000******************************************************************
082100
082200     IF  (BSD-EMP-PRE-CONT           < ZEROES)
082300     AND (PLN-FLEX-PAY-CODE          NOT = SPACES)
082400         GO TO 437-END.
082500
082600     IF (BSD-EMP-PRE-CONT            < ZEROES)
082700         COMPUTE BSH-COMP-FLX-AMT    = BSH-COMP-FLX-AMT
082800                                     - (BSD-EMP-PRE-CONT
082900                                     * NEGATIVE-ONE)
083000     ELSE
083100     IF (BSH-EMP-SPENT               NOT = ZEROES)
083200         COMPUTE BSH-EMP-SPENT       = BSH-EMP-SPENT
083300                                     - BSD-EMP-PRE-CONT
083400         IF (BSH-EMP-SPENT           < ZEROES)
083500             COMPUTE BN65WS-DIFF     = BSD-EMP-PRE-CONT
083600                                     - BSH-EMP-SPENT
083700             COMPUTE BSH-COMP-SPENT  = BSH-COMP-SPENT
083800                                     - BN65WS-DIFF
083900             INITIALIZE BSH-EMP-SPENT
084000         END-IF
084100     ELSE
084200         COMPUTE BSH-COMP-SPENT      = BSH-COMP-SPENT
084300                                     - BSD-EMP-PRE-CONT.
084400
084500 437-END.
084600
084700******************************************************************
084800 438-ADD-NEW-AMT.
084900******************************************************************
085000
085100     IF  (BN65F1-BSD-EMP-PRE-CONT (I1) < ZEROES)
085200     AND (PLN-FLEX-PAY-CODE            NOT = SPACES)
085300         GO TO 438-END.
085400
085500     IF (BN65F1-BSD-EMP-PRE-CONT (I1)  < ZEROES)
085600         COMPUTE BSH-COMP-FLX-AMT  = BSH-COMP-FLX-AMT
085700                                   + (BN65F1-BSD-EMP-PRE-CONT (I1)
085800                                   * NEGATIVE-ONE)
085900     ELSE
086000     IF (BSH-EMP-SPENT                 NOT = ZEROES)
086100         ADD BN65F1-BSD-EMP-PRE-CONT (I1)
086200                                       TO BSH-EMP-SPENT
086300     ELSE
086400         ADD BN65F1-BSD-EMP-PRE-CONT (I1)
086500                                       TO BSH-COMP-SPENT
086600         IF (BSH-COMP-SPENT            > BSH-COMP-FLX-AMT)
086700             COMPUTE BN65WS-DIFF       = BSH-COMP-SPENT
086800                                       - BSH-COMP-FLX-AMT
086900             MOVE BSH-COMP-FLX-AMT     TO BSH-COMP-SPENT
087000             MOVE BN65WS-DIFF          TO BSH-EMP-SPENT.
087100
087200 438-END.
087300
087400******************************************************************
087500 440-DELETE.
087600******************************************************************
087700
087800     PERFORM 910-AUDIT-BEGIN.
087900     IF (DMS-ABORTED)
088000         GO TO 440-END.
088100
088200     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
088300     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
088400
088500     PERFORM 600-DELETE-ALL-BNSIMDTL
088600     THRU    600-END.
088700
088800     PERFORM
088900         VARYING I1 FROM 1 BY 1
089000         UNTIL  (I1 > 11)
089100
089200         INITIALIZE BN65F1-LINE-FC          (I1)
089300                    BN65F1-SELECTED-FLAG    (I1)
089400                    BN65F1-BSD-COV-OPTION   (I1)
089500                    BN65F1-BSD-COVER-AMT    (I1)
089600                    BN65F1-BSD-EMP-PRE-CONT (I1)
089700                    BN65F1-BSD-EMP-AFT-CONT (I1)
089800                    BN65F1-BSD-OPTION-COST  (I1)
089900     END-PERFORM.
090000
090100     PERFORM 840-MODIFY-BSHSET1.
090200
090300     PERFORM 830-DELETE-BNSIMHDR.
090400
090500     MOVE CRT-RECS-DELETED           TO CRT-MESSAGE.
090600
090700     MOVE CRT-SEND-MESSAGE           TO CRT-REQUEST.
090800
090900     INITIALIZE BN65F1-KEY-PROTECTED.          
091000
091100     PERFORM 920-AUDIT-END.
091200
091300 440-END.
091400
091500******************************************************************
091600 480-INQUIRE.
091700******************************************************************
091800
091900     IF  (BNSIMHDR-FOUND)
092000     AND ((BN65F1-BSH-DATE           NOT = BSH-DATE)
092100     OR   (BN65F1-BSH-GROSS-PAY      NOT = BSH-GROSS-PAY))
092200         PERFORM 910-AUDIT-BEGIN
092300         IF (DMS-ABORTED)
092400             GO TO 480-END
092500         ELSE
092600             PERFORM 600-DELETE-ALL-BNSIMDTL
092700             THRU    600-END
092800
092900             PERFORM 840-MODIFY-BSHSET1
093000
093100             PERFORM 830-DELETE-BNSIMHDR
093200
093300             PERFORM 920-AUDIT-END.
093400
093500     PERFORM 481-MOVE-TO-SCREEN
093600     THRU    481-END.
093700
093800     IF (BN65F1-FC                   = "C")
093900         MOVE "I"                    TO BN65F1-FC.
094000
094100     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
094200     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
094300
094400     IF (BN65F1-FC                   = "I")
094500     OR (BN65F1-BSH-COMPANY          NOT = BN65F1-PT-BSH-COMPANY)
094600     OR (BN65F1-BSH-EMPLOYEE         NOT = BN65F1-PT-BSH-EMPLOYEE)
094700         INITIALIZE DB-PLAN-TYPE
094800                    DB-PLAN-CODE
094900         IF (BN65F1-FC               = "-")
095000             MOVE "+"                TO BN65F1-FC
095100         ELSE
095200             NEXT SENTENCE
095300         END-IF
095400     ELSE
095500     IF (BN65F1-FC                     = "+")
095600         MOVE BN65F1-PT-PLN-PLAN-TYPE  TO DB-PLAN-TYPE
095700         MOVE BN65F1-PT-PLN-PLAN-CODE  TO DB-PLAN-CODE
095800     ELSE
095900         MOVE BN65F1-BSD-PLAN-TYPE (1) TO DB-PLAN-TYPE
096000         MOVE BN65F1-BSD-PLAN-CODE (1) TO DB-PLAN-CODE.
096100
096200     IF (BN65F1-FC                   = "I")
096300         MOVE "+"                    TO BN65F1-FC.
096400
096500     IF (BN65F1-FC                   = "-")
096600         MOVE 13                     TO I1
096700     ELSE
096800         INITIALIZE I1.
096900
097000     PERFORM 850-FIND-NLT-PLNSET1.
097100     IF (BN65F1-FC                   = "-")
097200         PERFORM 870-FIND-PREV-PLNSET1
097300         IF  (PLAN-FOUND)
097400         AND (PLN-COMPANY            = DB-COMPANY)
097500             MOVE BN65F1-BSD-PLAN-TYPE (1)
097600                                     TO BN65F1-PT-PLN-PLAN-TYPE
097700             MOVE BN65F1-BSD-PLAN-CODE (1)
097800                                     TO BN65F1-PT-PLN-PLAN-CODE
097900             INITIALIZE BN65F1-DETAIL-GROUP.
098000
098100     IF (BN65F1-FC                   = "-")
098200         MOVE 11                     TO I1
098300         PERFORM 482-MOVE-DTL-TO-SCREEN
098400         THRU    482-END
098500             UNTIL  (I1              < 1)
098600             OR     (PLAN-NOTFOUND)
098700             OR     (PLN-COMPANY     NOT = DB-COMPANY)
098800
098900         IF (PLAN-NOTFOUND)
099000         OR (PLN-COMPANY             NOT = DB-COMPANY)
099100             MOVE CRT-INQ-COMPLETE   TO CRT-MESSAGE
099200         ELSE
099300             MOVE CRT-MORE-RECS      TO CRT-MESSAGE
099400         END-IF
099500     ELSE
099600         MOVE 1                      TO I1
099700         INITIALIZE BN65F1-DETAIL-GROUP
099800         PERFORM 482-MOVE-DTL-TO-SCREEN
099900         THRU    482-END
100000             UNTIL  (I1              > 11)
100100             OR     (PLAN-NOTFOUND)
100200             OR     (PLN-COMPANY     NOT = DB-COMPANY)
100300
100400         IF (PLAN-NOTFOUND)
100500         OR (PLN-COMPANY             NOT = DB-COMPANY)
100600             INITIALIZE BN65F1-PT-PLN-PLAN-TYPE
100700                        BN65F1-PT-PLN-PLAN-CODE
100800             MOVE CRT-INQ-COMPLETE   TO CRT-MESSAGE
100900         ELSE
101000             MOVE PLN-PLAN-TYPE      TO BN65F1-PT-PLN-PLAN-TYPE
101100             MOVE PLN-PLAN-CODE      TO BN65F1-PT-PLN-PLAN-CODE
101200             MOVE CRT-MORE-RECS      TO CRT-MESSAGE.
101300
101400 480-END.
101500
101600******************************************************************
101700 481-MOVE-TO-SCREEN.
101800******************************************************************
101900
102000     MOVE EMP-COMPANY                TO BN65F1-BSH-COMPANY.
102100     MOVE EMP-EMPLOYEE               TO BN65F1-BSH-EMPLOYEE.
102200
102300     MOVE BNWS-PRS-NAME              TO BN65F1-PRS-NAME.
102400
102500     MOVE EMP-LAST-NAME              TO HRWS-LAST-NAME.
102600     MOVE EMP-FIRST-NAME             TO HRWS-FIRST-NAME.
102700     MOVE EMP-MIDDLE-INIT            TO HRWS-MIDDLE-INIT.
           MOVE EMP-NAME-SUFFIX            TO HRWS-NAME-SUFFIX.
           MOVE EMP-LAST-NAME-PRE          TO HRWS-LAST-NAME-PRE.
102800     PERFORM 750-HR-FORMAT-NAME.
102900     MOVE HRWS-FORMAT-NAME           TO BN65F1-EMP-NAME.
103000
103100
103200     INITIALIZE BN65WS-TAXABLE-GROSS
103300                BN65WS-TOTAL-PRE-CONT
103400                BN65WS-TOTAL-AFT-CONT
103500                BN65WS-TOTAL-OPTION-COST.
103600
103700     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
103800     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
103900     MOVE BSDSET1-EMPLOYEE           TO WS-DB-BEG-RNG.
104000     PERFORM 850-FIND-BEGRNG-BSDSET1.
104100     PERFORM 
104200         UNTIL (BNSIMDTL-NOTFOUND)
104300
104400         MOVE BSD-COMPANY            TO DB-COMPANY
104500         MOVE BSD-PLAN-TYPE          TO DB-PLAN-TYPE
104600         MOVE BSD-PLAN-CODE          TO DB-PLAN-CODE
104700         PERFORM 840-FIND-PLNSET1
104800
104900         IF  (BSD-EMP-PRE-CONT       > ZEROES)
105000         OR  ((BSD-EMP-PRE-CONT      < ZEROES)
105100         AND  (PLN-FLEX-PAY-CODE     = SPACES))
105200             ADD BSD-EMP-PRE-CONT    TO BN65WS-TOTAL-PRE-CONT
105300         END-IF
105400         ADD BSD-EMP-AFT-CONT        TO BN65WS-TOTAL-AFT-CONT
105500         ADD BSD-OPTION-COST         TO BN65WS-TOTAL-OPTION-COST
105600
105700         PERFORM 860-FIND-NXTRNG-BSDSET1
105800     END-PERFORM.
105900
106000     MOVE BN65WS-TOTAL-PRE-CONT      TO BN65F1-TOTAL-PRE-CONT.
106100     MOVE BN65WS-TOTAL-AFT-CONT      TO BN65F1-TOTAL-AFT-CONT.
106200     MOVE BN65WS-TOTAL-OPTION-COST   TO BN65F1-TOTAL-OPTION-COST.
106300
106400 481-END.
106500
106600******************************************************************
106700 482-MOVE-DTL-TO-SCREEN.
106800******************************************************************
106900
107000*
107100**** CALL PLAN ELIGIBILITY SECTION WHICH VALIDATES ONLY
107200**** GROUP & ZIP CODE
107300*
107400     MOVE BN65F1-BSH-COMPANY         TO BNPEWS-COMPANY.
107500     MOVE PLN-PLAN-TYPE              TO BNPEWS-PLAN-TYPE.
107600     MOVE PLN-PLAN-CODE              TO BNPEWS-PLAN-CODE.
107700     MOVE BN65F1-BSH-EMPLOYEE        TO BNPEWS-EMPLOYEE.
107800     MOVE "E"                        TO BNPEWS-COVER-TYPE.
107900     PERFORM 5000-EDIT-GROUP-N-ZIP-70.
108000     IF (ERROR-FOUND)
108100         INITIALIZE CRT-ERROR-NBR
108200                    CRT-ERROR-CAT
108300                    CRT-ERR-VAR1
108400                    CRT-ERR-VAR2
108500                    CRT-ERR-VAR3
108600         GO TO 482-FIND-NEXT-PLAN.
108700
108800     IF  (PLN-START-DATE             > BN65F1-BSH-DATE)
108900     OR  ((PLN-STOP-DATE             < BN65F1-BSH-DATE)
109000     AND  (PLN-STOP-DATE             NOT = ZEROES))
109100         GO TO 482-FIND-NEXT-PLAN.
109200
109300     MOVE PLN-PLAN-TYPE              TO BN65F1-BSD-PLAN-TYPE (I1).
109400     MOVE PLN-PLAN-CODE              TO BN65F1-BSD-PLAN-CODE (I1).
109500     MOVE PLN-DISPLAY-DESC           TO BN65F1-PLN-DESC      (I1).
109600
109700     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
109800     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
109900     MOVE PLN-PLAN-TYPE              TO DB-PLAN-TYPE.
110000     MOVE PLN-PLAN-CODE              TO DB-PLAN-CODE.
110100     PERFORM 840-FIND-BSDSET1.
110200     IF (BNSIMDTL-FOUND)
110300         PERFORM 484-MOVE-FROM-BNSIMDTL
110400         THRU    484-END
110500     ELSE
110600         PERFORM 486-MOVE-FROM-PLAN
110700         THRU    486-END.
110800
110900     IF (BN65F1-FC                   = "-")
111000         SUBTRACT 1                  FROM I1
111100     ELSE
111200         ADD 1                       TO I1.
111300
111400 482-FIND-NEXT-PLAN.
111500     IF (BN65F1-FC = "-")
111600         PERFORM 870-FIND-PREV-PLNSET1
111700     ELSE
111800         PERFORM 860-FIND-NEXT-PLNSET1.
111900
112000 482-END.
112100
112200******************************************************************
112300 484-MOVE-FROM-BNSIMDTL.
112400******************************************************************
112500
112600     MOVE "*"                    TO BN65F1-SELECTED-FLAG    (I1).
112700     MOVE BSD-COV-OPTION         TO BN65F1-BSD-COV-OPTION   (I1).
112800     MOVE BSD-COVER-AMT          TO BN65F1-BSD-COVER-AMT    (I1).
112900     MOVE BSD-EMP-PRE-CONT       TO BN65F1-BSD-EMP-PRE-CONT (I1).
113000     MOVE BSD-EMP-AFT-CONT       TO BN65F1-BSD-EMP-AFT-CONT (I1).
113100     MOVE BSD-OPTION-COST        TO BN65F1-BSD-OPTION-COST  (I1).
113200     MOVE BSD-FLEX-FLAG          TO BN65F1-BSD-FLEX-FLAG    (I1).
113300
113400 484-END.
113500
113600******************************************************************
113700 486-MOVE-FROM-PLAN.
113800******************************************************************
113900
114000     INITIALIZE BN65F1-SELECTED-FLAG    (I1)
114100                BN65F1-BSD-COV-OPTION   (I1)
114200                BN65F1-BSD-COVER-AMT    (I1)
114300                BN65F1-BSD-EMP-PRE-CONT (I1)
114400                BN65F1-BSD-EMP-AFT-CONT (I1)
114500                BN65F1-BSD-OPTION-COST  (I1)
114600                BN65F1-BSD-FLEX-FLAG    (I1).
114700
114800     IF (PLN-FLEX-PLAN               NOT = SPACES)
114900         MOVE PLN-FLEX-PLAN          TO DB-FLEX-PLAN
115000         PERFORM 840-FIND-FLPSET1.
115100
115200     IF  (PLN-FLEX-PLAN              NOT = SPACES)
115300     AND (FLP-SPEND-ONLY             = "N")
115400         MOVE "Y"                    TO BN65F1-BSD-FLEX-FLAG (I1)
115500     ELSE
115600         MOVE "N"                    TO BN65F1-BSD-FLEX-FLAG (I1).
115700
115800 486-END.
115900
116000******************************************************************
116100 500-MOVE-DATA.
116200******************************************************************
116300
116400     MOVE BN65F1-BSH-COMPANY         TO BSH-COMPANY.
116500     MOVE BN65F1-BSH-EMPLOYEE        TO BSH-EMPLOYEE.
116600     MOVE BN65F1-BSH-DATE            TO BSH-DATE.
116700     MOVE BN65F1-BSH-GROSS-PAY       TO BSH-GROSS-PAY.
116800
116900     IF (BN65F1-FC                   = "A")
117000         PERFORM 502-MOVE-FLEX-DOL
117100         THRU    502-END.
117200
117300 500-END.
117400
117500******************************************************************
117600 502-MOVE-FLEX-DOL.
117700******************************************************************
117800
117900     IF (BN65F1-FLD-FLEX-PLAN        NOT = SPACES)
118000         MOVE FLD-ADD-TO-GRS-PCT     TO BSH-ADD-TO-GRS-PCT
118100         MOVE BNFDWS-FLEX-CREDITS    TO BSH-COMP-FLX-AMT
118200         MOVE BNFDWS-PT-DOLLARS      TO BSH-EMP-FLEX-AMT
118300     ELSE
118400         INITIALIZE BSH-COMP-FLX-AMT
118500                    BSH-EMP-FLEX-AMT
118600                    BSH-ADD-TO-GRS-PCT.
118700
118800 502-END.
118900
119000******************************************************************
119100 510-MOVE-DTL-DATA.
119200******************************************************************
119300
119400     MOVE BN65F1-BSH-COMPANY           TO BSD-COMPANY.
119500     MOVE BN65F1-BSH-EMPLOYEE          TO BSD-EMPLOYEE.
119600     MOVE BN65F1-BSD-PLAN-TYPE    (I1) TO BSD-PLAN-TYPE.
119700     MOVE BN65F1-BSD-PLAN-CODE    (I1) TO BSD-PLAN-CODE.
119800     MOVE BN65F1-PLN-DESC         (I1) TO BSD-DESC.
119900     MOVE BN65F1-BSD-COV-OPTION   (I1) TO BSD-COV-OPTION.
120000     MOVE BN65F1-BSD-COVER-AMT    (I1) TO BSD-COVER-AMT.
120100     MOVE BN65F1-BSD-EMP-PRE-CONT (I1) TO BSD-EMP-PRE-CONT.
120200     MOVE BN65F1-BSD-EMP-AFT-CONT (I1) TO BSD-EMP-AFT-CONT.
120300     MOVE BN65F1-BSD-OPTION-COST  (I1) TO BSD-OPTION-COST.
120400     MOVE BN65F1-BSD-FLEX-FLAG    (I1) TO BSD-FLEX-FLAG.
120500
120600 510-END.
120700
120800******************************************************************
120900 520-CALCULATE-DOLLARS.
121000******************************************************************
121100
121200     MOVE BN65F1-BSH-COMPANY         TO BNFDWS-COMPANY.
121300     MOVE BN65F1-FLD-FLEX-PLAN       TO BNFDWS-FLEX-PLAN.
121400     MOVE BN65F1-BSH-EMPLOYEE        TO BNFDWS-EMPLOYEE.
121500     MOVE BN65F1-BSH-DATE            TO BNFDWS-AS-OF-DATE
121600                                        BNFDWS-START-DATE.
121700     MOVE FLD-STOP-DATE              TO BNFDWS-STOP-DATE.
121800     MOVE BN65F1-BSH-GROSS-PAY       TO BNFDWS-IN-ANNUAL-SALARY.
121900     PERFORM 5000-DO-FLEX-DOLLAR-70.
122000
122100 520-END.
122200
122300******************************************************************
122400 600-DELETE-ALL-BNSIMDTL.
122500******************************************************************
122600
122700     MOVE BN65F1-BSH-COMPANY         TO DB-COMPANY.
122800     MOVE BN65F1-BSH-EMPLOYEE        TO DB-EMPLOYEE.
122900     MOVE BSDSET1-EMPLOYEE           TO WS-DB-BEG-RNG.
123000     PERFORM 850-MODIFY-BEGRNG-BSDSET1.
123100     PERFORM
123200         UNTIL (BNSIMDTL-NOTFOUND)
123300
123400         PERFORM 830-DELETE-BNSIMDTL
123500         
123600         PERFORM 860-MODIFY-NXTRNG-BSDSET1
123700     END-PERFORM.
123800
123900 600-END.
124000
124100******************************************************************
124200 BN65S1-TRANSACTION-END.
124300******************************************************************
124400
124500******************************************************************
124600 BN65S2-TRANSACTION              SECTION 43.
124700******************************************************************
124800 BN65S2-START.
124900
125000     INITIALIZE BN65WS-TAXABLE-GROSS
125100                BN65WS-TOTAL-PRE-CONT
125200                BN65WS-TOTAL-AFT-CONT
125300                BN65WS-TOTAL-OPTION-COST
125400                BN65WS-COMP-DOLLARS
125500                BN65WS-COMPANY-DOLLARS
125600                BN65WS-EMPLOYEE-DOLLARS
125700                BN65WS-COMPANY-REMAIN
125800                BN65WS-EMPLOYEE-REMAIN
125900                BN65WS-BENEFIT-CREDITS.
126000
126100     MOVE BN65F2-BSH-COMPANY         TO DB-COMPANY.
126200     MOVE BN65F2-BSH-EMPLOYEE        TO DB-EMPLOYEE.
126300     PERFORM 840-FIND-BSHSET1.
126400
126500     IF (BNSIMHDR-NOTFOUND)
126600*****  SIMULATION RECORD DOES NOT EXIST  *****
126700         MOVE 100                    TO CRT-MSG-NBR
126800         PERFORM 790-GET-MSG
126900         MOVE BN65F2-FC-FN           TO CRT-FIELD-NBR
127000         GO TO BN65S2-TRANSACTION-END.
127100
127200     MOVE BN65F2-FLD-FLEX-PLAN       TO DB-FLEX-PLAN.
127300     PERFORM 840-FIND-FLPSET1.
127400     IF (FLEXPLAN-FOUND)
127500         MOVE FLP-DESC               TO BN65F2-FLEX-PLAN
127600     ELSE
127700         INITIALIZE BN65F2-FLEX-PLAN.
127800
127900     MOVE BSH-EMP-FLEX-AMT           TO BN65WS-EMPLOYEE-DOLLARS.
128000     MOVE BSH-EMP-FLEX-AMT           TO BN65WS-EMPLOYEE-REMAIN. 
128100     MOVE BSH-COMP-FLX-AMT           TO BN65WS-COMPANY-DOLLARS.
128200     MOVE BSH-COMP-FLX-AMT           TO BN65WS-COMPANY-REMAIN.
128300     MOVE BSH-GROSS-PAY              TO BN65WS-TAXABLE-GROSS.
128400
128500     MOVE BSH-COMPANY                TO DB-COMPANY.
128600     MOVE BSH-EMPLOYEE               TO DB-EMPLOYEE.
128700     MOVE BSDSET1-EMPLOYEE           TO WS-DB-BEG-RNG.
128800     PERFORM 850-FIND-BEGRNG-BSDSET1.
128900     PERFORM 400-ADD-NEGATIVE-TOTALS
129000     THRU    400-END
129100         UNTIL  (BNSIMDTL-NOTFOUND).
129200
129300     MOVE BSH-COMPANY                TO DB-COMPANY.
129400     MOVE BSH-EMPLOYEE               TO DB-EMPLOYEE.
129500     MOVE BSDSET1-EMPLOYEE           TO WS-DB-BEG-RNG.
129600     PERFORM 850-FIND-BEGRNG-BSDSET1.
129700     PERFORM 410-ADD-POSITIVE-TOTALS
129800     THRU    410-END
129900         UNTIL  (BNSIMDTL-NOTFOUND).
130000
130100     MOVE BN65WS-EMPLOYEE-DOLLARS    TO BN65F2-EMPLOYEE-DOLLARS.
130200     MOVE BN65WS-EMPLOYEE-REMAIN     TO BN65F2-EMPLOYEE-REMAIN.
130300     MOVE BN65WS-COMPANY-DOLLARS     TO BN65F2-COMPANY-DOLLARS.
130400     MOVE BN65WS-COMPANY-REMAIN      TO BN65F2-COMPANY-REMAIN.
130500     MOVE BN65WS-TAXABLE-GROSS       TO BN65F2-TAXABLE-GROSS.
130600     ADD  BN65WS-COMPANY-REMAIN      TO BN65F2-TAXABLE-GROSS.
130700     MOVE BN65WS-BENEFIT-CREDITS     TO BN65F2-BENEFIT-CREDITS.
130800     ADD  BN65WS-BENEFIT-CREDITS     TO BN65F2-TAXABLE-GROSS.
130900     SUBTRACT BN65WS-EMPLOYEE-DOLLARS
131000                                     FROM BN65F2-TAXABLE-GROSS.
131100     ADD  BN65WS-EMPLOYEE-REMAIN     TO BN65F2-TAXABLE-GROSS.
131200
131300     MOVE CRT-EXIT-WINDOW            TO CRT-REQUEST.
131400
131500     GO TO BN65S2-TRANSACTION-END.
131600
131700******************************************************************
131800 400-ADD-NEGATIVE-TOTALS.
131900******************************************************************
132000
132100     IF (BSD-EMP-PRE-CONT            > ZEROES)
132200         GO TO 400-NEXT-BNSIMDTL.
132300
132400     MOVE BSD-COMPANY                TO DB-COMPANY.
132500     MOVE BSD-PLAN-TYPE              TO DB-PLAN-TYPE.
132600     MOVE BSD-PLAN-CODE              TO DB-PLAN-CODE.
132700     PERFORM 840-FIND-PLNSET1.
132800     IF  (PLAN-FOUND)
132900     AND (PLN-FLEX-PAY-CODE          = SPACES)
133000     AND (BSD-EMP-PRE-CONT           < ZEROES)
133100         SUBTRACT BSD-EMP-PRE-CONT   FROM BN65WS-COMPANY-DOLLARS
133200         SUBTRACT BSD-EMP-PRE-CONT   FROM BN65WS-COMPANY-REMAIN
133300     ELSE
133400     IF  (PLAN-FOUND)
133500     AND (PLN-FLEX-PAY-CODE          NOT = SPACES)
133600     AND (BSD-EMP-PRE-CONT           < ZEROES)
133700         SUBTRACT BSD-EMP-PRE-CONT   FROM BN65WS-BENEFIT-CREDITS.
133800
133900 400-NEXT-BNSIMDTL.
134000     PERFORM 860-FIND-NXTRNG-BSDSET1.
134100
134200 400-END.
134300
134400******************************************************************
134500 410-ADD-POSITIVE-TOTALS.
134600******************************************************************
134700
134800     IF (BSD-EMP-PRE-CONT            < ZEROES)
134900         GO TO 410-NEXT-BNSIMDTL.
135000
135100     IF (BSD-EMP-PRE-CONT            < BN65WS-COMPANY-REMAIN)
135200         SUBTRACT BSD-EMP-PRE-CONT   FROM BN65WS-COMPANY-REMAIN
135300     ELSE
135400     IF  (BSD-EMP-PRE-CONT           >= BN65WS-COMPANY-REMAIN)
135500     AND (BSD-EMP-PRE-CONT           > BN65WS-EMPLOYEE-REMAIN 
135600                                     + BN65WS-COMPANY-REMAIN)
135700         INITIALIZE BN65WS-COMPANY-REMAIN
135800                    BN65WS-EMPLOYEE-REMAIN
135900     ELSE
136000     IF (BSD-EMP-PRE-CONT            >= BN65WS-COMPANY-REMAIN)
136100         SUBTRACT BSD-EMP-PRE-CONT   FROM BN65WS-EMPLOYEE-REMAIN  
136200         ADD BN65WS-COMPANY-REMAIN   TO BN65WS-EMPLOYEE-REMAIN
136300         INITIALIZE BN65WS-COMPANY-REMAIN.
136400
136500 410-NEXT-BNSIMDTL.
136600     PERFORM 860-FIND-NXTRNG-BSDSET1.
136700
136800 410-END.
136900
137000******************************************************************
137100 BN65S2-TRANSACTION-END.
137200******************************************************************
137300
